;-------------------------
entry posCLEAR 
;-------------------------
	NR_CAIXA.SIS_DUMMY = $vlInicial$

	if (NR_CAIXA.SIS_DUMMY = 1)
		NR_CTA.SIS_DUMMY = $nrCtaPes$
	elseif (NR_CAIXA.SIS_DUMMY = 2)
		NR_CTA.SIS_DUMMY = $item("CD_CTAPES_CXFILIAL", $xlplemp$)
	elseif (NR_CAIXA.SIS_DUMMY = 3)
		NR_CTA.SIS_DUMMY = $item("CD_CTAPES_CXMATRIZ", $xlplemp$)
	endif

	$instancehandle->inicializa()
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")        
	endif 

	return(0)
End ; operation posCLEAR

;------------------------
entry preEDIT 
;------------------------

	if($inPdvOtimizado$ != <TRUE>)
		$instancehandle->carregaLista()
		if ($procerror)
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")        
		endif 
		$instancehandle->inicializa()
		if ($procerror)
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")        
		endif 
	else
		$instancehandle->carregaListaOutroUsuario()
		if ($procerror)
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")        
		endif 
		$instancehandle->inicializaOutroUsuario()
		if ($procerror)
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")        
		endif 

	endif


	return(0)
End ; operation preEDIT

;---------------------
partner operation INIT 
;---------------------
	$formtitle = "%%$componentname - Suprimento de Caixa"

	;;Retire o comentário das linhas abaixo para carregar os parâmetros locais
	;Parametros gerais
	$xlpl$ = ""
	putitem $xlpl$, -1, "CD_TPMANUT_CXUSUARIO"
	putitem $xlpl$, -1, "CD_TPMANUT_CXFILIAL"
	putitem $xlpl$, -1, "CD_TPMANUT_CXMATRIZ"
	activate "ADMSVCO001".GetLstParametro($xlpl$,$xlpl$,$xcderro$,$xctxerro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	elseif ($xcdErro$)
		$instancehandle->SetErroApl($xCtxErro$, $xCdErro$, $xCtxErro$)
	endif


	;;Parametros por empresa
	$xlplemp$ = ""
	putitem $xlplemp$, -1, "IN_CONTROLE_PAGPORT"
	putitem $xlplemp$, -1, "CD_CTAPES_CXFILIAL"
	putitem $xlplemp$, -1, "CD_CTAPES_CXMATRIZ"
	putitem $xlplemp$, -1, "IN_CAIXA_TERMINAL"
	putitem $xlplemp$, -1, "NR_MODREC_SUPRIMENTO"
	putitem $xlplemp$, -1, "IN_PDV_OTIMIZADO"
	activate "ADMSVCO001".GetParamEmpresa($item("CD_EMPRESA", $$gParamGlb), $xlplemp$, $xlplemp$, $xcderro$, $xctxerro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	elseif ($xcdErro$)
		$instancehandle->SetErroApl($xCtxErro$, $xCdErro$, $xCtxErro$)
	endif

	$inPdvOtimizado$ = $item("IN_PDV_OTIMIZADO", $xlplEmp$)

End ; operation INIT

;------------------------
partner operation CLEANUP 
;------------------------

End ; operation CLEANUP

;---------------------------
partner operation inicializa
;---------------------------
	variables
		numeric vInCxTerminal
	endvariables

	vInCxTerminal = $item("IN_CAIXA_TERMINAL", $xlplemp$)

	if (vInCxTerminal = 0)
		fieldsyntax CD_TERMINAL.GER_TERMINAL, "DIM"
		fieldsyntax CD_USUARIO.ADM_USUARIO, ""
		fieldsyntax BT_LTERMINAL.GER_TERMINAL, "DIM"
		fieldsyntax BT_LUSUARIO.ADM_USUARIO, ""
	else
		fieldsyntax CD_USUARIO.ADM_USUARIO, "DIM"
		fieldsyntax CD_TERMINAL.GER_TERMINAL, ""
		fieldsyntax BT_LUSUARIO.ADM_USUARIO, "DIM"
		fieldsyntax BT_LTERMINAL.GER_TERMINAL, ""
	endif

	clear/e "GER_EMPRESA"
	CD_EMPRESA.GER_EMPRESA = $item("CD_EMPRESA", $$gParamGlb)
	retrieve/e "GER_EMPRESA"
	if ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Empresa não encontrada!", "")
		$prompt = CD_USUARIO.ADM_USUARIO
		return(-1)
	endif
	
	return(0)
end

;-------------------------
partner operation confirma
;-------------------------
	variables
		string viParams, voParams
		numeric vCdTerminal, vNrSeq, vCdTerminalDest, vNrSeqDest
		date vDtAbertura, vDtAberturaDest
	endvariables

	if (NR_CTAPES.SIS_DUMMY = "")
		message/info "Conta para suprimento não informada!"
		$prompt = CD_USUARIO.ADM_USUARIO
		return(-1)
	endif

	if (NR_CTA.SIS_DUMMY = NR_CTAPES.SIS_DUMMY)
		message/info "Não é possível transferir para a mesma conta!"
		$prompt = CD_USUARIO.ADM_USUARIO
		return (-1)
	endif

	if (VL_ABERTURA.SIS_DUMMY <= 0)
		message/info "Valor de suprimento incorreto!"
		$prompt = VL_ABERTURA.SIS_DUMMY
		return (-1)
	endif

	if (NR_CAIXA.SIS_DUMMY = 1)
		viParams = ""
		activate "FCXSVCO001".buscaCaixa($$gParamGlb, viParams, voParams, $xCdErro$, $xCtxErro$)
		if ($procerror)
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			return(-1)
		elseif ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
			return(-1)
		endif

		vCdTerminal = $item("CD_TERMINAL", voParams)
		vDtAbertura = $item("DT_ABERTURA", voParams)
		vNrSeq = $item("NR_SEQ", voParams)
	endif

	viParams = ""
	putitem/id viParams, "CD_OPERCX", CD_USUARIO.ADM_USUARIO
	putitem/id viParams, "CD_TERMINAL", CD_TERMINAL.GER_TERMINAL
	activate "FCXSVCO001".buscaCaixaOper($$gParamGlb, viParams, voParams, $xCdErro$, $xCtxErro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
		return(-1)
	endif

	vCdTerminalDest = $item("CD_TERMINAL", voParams)
	vDtAberturaDest = $item("DT_ABERTURA", voParams)
	vNrSeqDest = $item("NR_SEQ", voParams)

	viParams = ""
	putitem/id viParams, "NR_CTAORIGEM", NR_CTA.SIS_DUMMY
	putitem/id viParams, "NR_CTADESTINO", NR_CTAPES.SIS_DUMMY
	putitem/id viParams, "VL_SUPRIMENTO", VL_ABERTURA.SIS_DUMMY
	putitem/id viParams, "CD_TERMINALDEST", vCdTerminalDest
	putitem/id viParams, "DT_ABERTURADEST", vDtAberturaDest
	putitem/id viParams, "NR_SEQDEST", vNrSeqDest
	putitem/id viParams, "DS_OBS", DS_OBS.SIS_DUMMY

	if (NR_CAIXA.SIS_DUMMY = 1)
		putitem/id viParams, "IN_CXUSUARIO", <TRUE>
		putitem/id viParams, "CD_TERMINAL", vCdTerminal
		putitem/id viParams, "DT_ABERTURA", vDtAbertura
		putitem/id viParams, "NR_SEQ", vNrSeq
	else
		putitem/id viParams, "IN_CXUSUARIO", <FALSE>
	endif
	;Projeto 078 - Tarefa 3602 - Aloisio Gargantini - 09/08/2010
	putitem/id viParams, "TP_PROCESSO", 3 ;Suprimento de caixa
	putitem/id viParams, "IN_MANUAL", <TRUE>
	;
	activate "FCXSVCO002".supreCaixa($$gParamGlb, viParams, voParams, $xCdErro$, $xCtxErro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
		return(-1)
	endif

	commit
	if ($status < 0)
		rollback
		return (-1) 
	else
		message/info "Suprimento efetuado com sucesso!"
	endif	
	
	;MTF(01/03/2006) - Projeto 049, tarefa 006.
	if($inPdvOtimizado$ != <TRUE>);Oda - no lojao nao vai perguntar, ira imprmir o recibo direto.
		askmess "Deseja imprimir recibo?", "Sim,Não"
		if ($status = 1)
			$instancehandle->imprimeReciboSuprimento()
			if ($procerror)
				$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			endif
		endif
	else
		$instancehandle->imprimeReciboSuprimento()
		if ($procerror)
			$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		endif
	endif
	
	
	return(0)
end ;confirma

;----------------------------
partner operation validaConta
;----------------------------
	variables
		string viParams, voParams, vEmpresa, lstConta
	endvariables

	if (CD_USUARIO.ADM_USUARIO > 0) | (CD_TERMINAL.GER_TERMINAL > 0)
		viParams = ""
		putitem/id viParams, "CD_EMPRESA", $item("CD_EMPRESA", $$gParamGlb)
		putitem/id viParams, "CD_OPERADOR", CD_USUARIO.ADM_USUARIO
		putitem/id viParams, "CD_TERMINAL", CD_TERMINAL.GER_TERMINAL
		activate "FCCSVCO002".buscaContaOperador($$gParamGlb, viParams, voParams, $xCdErro$, $xCtxErro$)
		if ($procerror)       
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			return (-1)
		elseif ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
			return (-1)
		endif

		NR_CTAPES.SIS_DUMMY = $item("NR_CTAPES", voParams)
	else
		NR_CTAPES.SIS_DUMMY = ""
	endif

	return (0)
end ;validaConta


;----------------------------------------------;
partner operation imprimeReciboSuprimento      ;
;----------------------------------------------;
; MTF    : 01/03/2006  Projeto 049, tarefa 006 ;
; Funcao : Imprimir recibo de suprimento.      ;
; Chamada: operation - confirma()              ;
;----------------------------------------------;
;
	variables
		string vpiParams, vpoParams, vDsConteudo
	endvariables
	
	if ($item("NR_MODREC_SUPRIMENTO", $xlplemp$) = "")
		$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Número do modelo de recibo de suprimento não informado no parametro empresa.%%^ Parametro -> NR_MODREC_SUPRIMENTO", "")
		return(-1)
	endif
	
	clear/e "GER_MODFINC"
	nr_modfinc.ger_modfinc/init = $item("NR_MODREC_SUPRIMENTO", $xlplemp$)
	retrieve/e "GER_MODFINC"
	if ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Modelo de recibo de suprimento não cadastrado.", "")
		return(-1)
	endif
	
	vpiParams = ""
	putitem/id vpiParams, "NR_CTAPES_SANGRIA_SUPRIMENTO", nr_ctapes.sis_dummy
	putitem/id vpiParams, "DT_SANGRIA_SUPRIMENTO",        $item("DT_SISTEMA", $$gParamGlb)
	putitem/id vpiParams, "VL_SANGRIA_SUPRIMENTO",        vl_abertura.sis_dummy
	if ($cdusuario$ = "")
		$cdusuario$ = $item("CD_USUARIO", $$gParamGlb)
	endif
	putitem/id vpiParams, "CD_USU_ESCOLHIDO",             $cdUsuario$
	putitem/id vpiParams, "CD_USU_LOGADO",                $item("CD_USUARIO", $$gParamGlb)
	putitem/id vpiParams, "CD_EMPRESA",                   cd_empresa.ger_empresa
	putitem/id vpiParams, "NR_REC_SANGRIA_SUPRIMENTO",    $item("NR_MODREC_SUPRIMENTO", $xlplemp$)
	activate "GERSVCO028".geraReciboSangriaSuprimento($$gParamGlb, vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Recibo de sangria/suprimento não gerado.", "")
		return(-1)
	endif
	
	vDsConteudo = $item("DS_CONTEUDO", vpoParams)
	if (nm_job.ger_modfinc = "P_LPT1") | (nm_job.ger_modfinc = "P_LN03")
		filedump vDsConteudo, "LPT1"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Erro %%$status ao imprimir recibo na LPT1!", "")
			return(-1)
		endif
	elseif (nm_job.ger_modfinc = "P_LPT2") | (nm_job.ger_modfinc = "P_PORT2")
		filedump vDsConteudo, "LPT2"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Erro %%$status ao imprimir recibo na LPT2!", "")
			return(-1)
		endif
	elseif (nm_job.ger_modfinc = "P_LPT3")
		filedump vDsConteudo, "LPT3"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Erro %%$status ao imprimir recibo na LPT3!", "")
			return(-1)
		endif
	elseif (nm_job.ger_modfinc = "P_LPT4")
		filedump vDsConteudo, "LPT4"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Erro %%$status ao imprimir recibo na LPT4!", "")
			return(-1)
		endif
	elseif (nm_job.ger_modfinc = "P_LPT5")
		filedump vDsConteudo, "LPT5"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Erro %%$status ao imprimir recibo na LPT5!", "")
			return(-1)
		endif
	elseif (nm_job.ger_modfinc = "P_LPT6")
		filedump vDsConteudo, "LPT6"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Erro %%$status ao imprimir recibo na LPT6!", "")
			return(-1)
		endif
	elseif (nm_job.ger_modfinc = "P_LPT7")
		filedump vDsConteudo, "LPT7"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Erro %%$status ao imprimir recibo na LPT7!", "")
			return(-1)
		endif
	elseif (nm_job.ger_modfinc = "P_LPT8")
		filedump vDsConteudo, "LPT8"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Erro %%$status ao imprimir recibo na LPT8!", "")
			return(-1)
		endif
	elseif (nm_job.ger_modfinc = "P_LPT9")
		filedump vDsConteudo, "LPT9"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Erro %%$status ao imprimir recibo na LPT9!", "")
			return(-1)
		endif
	else
		vpiParams = ""
		putitem/id vpiParams, "DS_CHEQUE",  vDsConteudo
		putitem/id vpiParams, "NM_JOB",     nm_job.ger_modfinc
		putitem/id vpiParams, "IN_PREVIEW", <TRUE>
		activate "FCXR007".exec($$gParamGlb, vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
		if ($procerror)       
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			return (-1)
		elseif ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
			return (-1)
		endif
	endif
	
	return(0)
End ;operation imprimeReciboSuprimento.

;----------------------------------
partner operation carregaLista
;Marcelo Oda 09/03/05
;carrega lista conforme usuario logado.
;----------------------------------
	variables
		string vCaixa
		numeric vTpUsuario, vTpFilial, vTpMatriz, vTpBanco, vInCxTerminal
		numeric vNrCtaUsuario, vNrCtaFilial, vNrCtaMatriz
	endvariables

	vTpUsuario = $item("CD_TPMANUT_CXUSUARIO", $xlpl$)
	vTpFilial = $item("CD_TPMANUT_CXFILIAL", $xlpl$)
	vTpMatriz = $item("CD_TPMANUT_CXMATRIZ", $xlpl$)
	vInCxTerminal = $item("IN_CAIXA_TERMINAL", $xlplemp$)

	vCaixa = ""

	if (vInCxTerminal = 0)
		clear/e "FCC_TPMANUTUSU"
		CD_EMPRESA.FCC_TPMANUTUSU = $item("CD_EMPRESA", $$gParamGlb)
		CD_USULIBERADO.FCC_TPMANUTUSU = $item("CD_USUARIO", $$gParamGlb)
		TP_MANUTENCAO.FCC_TPMANUTUSU = vTpUsuario
		retrieve/e "FCC_TPMANUTUSU"
		if ($status >= 0)
			putitem/id vCaixa, "1", "Usuário"

			clear/e "FCC_CTAPES"
			CD_EMPRESA.FCC_CTAPES = $item("CD_EMPRESA", $$gParamGlb)
			CD_OPERCAIXA.FCC_CTAPES = $item("CD_USUARIO", $$gParamGlb)
			retrieve/e "FCC_CTAPES"
			if ($status >= 0)
				$nrCtaPes$ = NR_CTAPES.FCC_CTAPES
			endif

			$vlInicial$ = 1
			NR_CTA.SIS_DUMMY = $nrCtaPes$
		endif
	endif

	clear/e "FCC_TPMANUTUSU"
	CD_EMPRESA.FCC_TPMANUTUSU = $item("CD_EMPRESA", $$gParamGlb)
	CD_USULIBERADO.FCC_TPMANUTUSU = $item("CD_USUARIO", $$gParamGlb)
	TP_MANUTENCAO.FCC_TPMANUTUSU = vTpFilial
	retrieve/e "FCC_TPMANUTUSU"
	if ($status >= 0)
		putitem/id vCaixa, "2", "Filial"
		if ($vlInicial$ = 0)
			$vlInicial$ = 2
			NR_CTA.SIS_DUMMY = $item("CD_CTAPES_CXFILIAL", $xlplemp$)
		endif
	endif

	clear/e "FCC_TPMANUTUSU"
	CD_EMPRESA.FCC_TPMANUTUSU = $item("CD_EMPRESA", $$gParamGlb)
	CD_USULIBERADO.FCC_TPMANUTUSU = $item("CD_USUARIO", $$gParamGlb)
	TP_MANUTENCAO.FCC_TPMANUTUSU = vTpMatriz
	retrieve/e "FCC_TPMANUTUSU"
	if ($status >= 0)
		putitem/id vCaixa, "3", "Matriz"
		if ($vlInicial$ = 0)
			$vlInicial$ = 3
			NR_CTA.SIS_DUMMY = $item("CD_CTAPES_CXMATRIZ", $xlplemp$)
		endif
	endif

	$valrep (NR_CAIXA.SIS_DUMMY) = vCaixa
	NR_CAIXA.SIS_DUMMY = $vlInicial$

	return(0)
end;carregaLista

;------------------------------------------
partner operation carregaListaOutroUsuario
;Marcelo Oda 09/03/06 55-3
;carrega lista de do usuario logado, após digitar usuario e senha de um usuario que tenha permissao
;feito para o lojao
;------------------------------------------
	variables
		string viParams, voParams, vCaixa
		numeric vTpMatriz, vTpBanco, vCdUsuario, vCdNivel
		numeric vNrCtaUsuario, vNrCtaFilial, vNrCtaMatriz
	endvariables

	viParams = ""
	putitem/id viParams, "IN_USULOGADO", <TRUE>
	putitem/id viParams, "DS_COMPONENTE", $componentname
	putitem/id viParams, "DS_TITULO", ""
	activate "ADMFM020".exec(viParams, voParams, $xCdErro$, $xCtxerro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")     
		exit (-1)
	endif
	if ($status < 0)
		exit (-1)
	endif
	
	$cdUsuario$ = $item("CD_USUARIO", voParams)
	vCdUsuario = $item("CD_USUARIO", voParams)
	vCdNivel = $item("CD_NIVEL", voParams)

	vTpMatriz = $item("CD_TPMANUT_CXMATRIZ", $xlpl$)

	vCaixa = ""

	clear/e "FCC_TPMANUTUSU"
	CD_EMPRESA.FCC_TPMANUTUSU = $item("CD_EMPRESA", $$gParamGlb)
	CD_USULIBERADO.FCC_TPMANUTUSU = vCdUsuario
	TP_MANUTENCAO.FCC_TPMANUTUSU = vTpMatriz
	retrieve/e "FCC_TPMANUTUSU"
	if ($status >= 0)
		putitem/id vCaixa, "3", "Matriz"
		if ($vlInicial$ = 0)
			$vlInicial$ = 3
			NR_CTA.SIS_DUMMY = $item("CD_CTAPES_CXMATRIZ", $xlplemp$)
		endif
	else
		message/error "Usuário sem nível no caixa Matriz"
		exit(0)
	endif

	$valrep (NR_CAIXA.SIS_DUMMY) = vCaixa
	NR_CAIXA.SIS_DUMMY = $vlInicial$

	return(0)
end;carregaListaOutroUsuario

;----------------------------------------
partner operation inicializaOutroUsuario
;----------------------------------------
	variables
		numeric vInCxTerminal
	endvariables

	vInCxTerminal = $item("IN_CAIXA_TERMINAL", $xlplemp$)

	fieldsyntax NR_CAIXA.SIS_DUMMY, "DIM"
	if (vInCxTerminal = 0)
		clear/e"ADM_USUARIO"
		CD_USUARIO.ADM_USUARIO/init = $item("CD_USUARIO", $$gParamGlb)
		retrieve/e "ADM_USUARIO"
		fieldsyntax CD_TERMINAL.GER_TERMINAL, "DIM"
		fieldsyntax CD_USUARIO.ADM_USUARIO, "DIM"
		fieldsyntax BT_LTERMINAL.GER_TERMINAL, "DIM"
		fieldsyntax BT_LUSUARIO.ADM_USUARIO, "DIM"
	else
		clear/e "GER_TERMINAL"
		CD_TERMINAL.GER_TERMINAL/init = $item("CD_TERMINAL", $$gParamGlb)
		retrieve/e "GER_TERMINAL"
		fieldsyntax CD_USUARIO.ADM_USUARIO, "DIM"
		fieldsyntax CD_TERMINAL.GER_TERMINAL, "DIM"
		fieldsyntax BT_LUSUARIO.ADM_USUARIO, "DIM"
		fieldsyntax BT_LTERMINAL.GER_TERMINAL, "DIM"
	endif
	
	clear/e "GER_EMPRESA"
	CD_EMPRESA.GER_EMPRESA = $item("CD_EMPRESA", $$gParamGlb)
	retrieve/e "GER_EMPRESA"
	if ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Empresa não encontrada!", "")
		$prompt = VL_ABERTURA.SIS_DUMMY
		exit(-1)
	endif

	$instancehandle->validaConta()
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	endif
	if($status < 0)
		exit(-1)
	endif

	return(0)
end