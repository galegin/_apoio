;------------------------
entry preEDIT 
;------------------------
	;Hugo em 08/04/11 Tarefa 182-39
	;$cdUsuarioDesc$ = ""
	$cdUsuarioDesc$ = $item("CD_USUARIODESC", $xlpi$)
	;
	$cdEmpresa$ = $item("CD_EMPRESA", $xlpi$)
	$nrTransacao$ = $item("NR_TRANSACAO", $xlpi$)
	$dtTransacao$ = $item("DT_TRANSACAO", $xlpi$)
	$prDescMaximo$ = $item("PR_DESCMAXIMO", $xlpi$)

	if ($cdEmpresa$ = 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Empresa não informada!", "")
		exit(-1)
	endif  

	if ($nrTransacao$ = 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Transação não informada!", "")
		exit(-1)
	endif  

	if ($dtTransacao$ = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Data da transação não informada!", "")
		exit(-1)
	endif  

	if ($prDescMaximo$ = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Desconto máximo não informado!", "")
		exit(-1)
	endif 

	if ($prDescMaximo$ > 100)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Desconto máximo maior que 100%!", "")
		exit(-1)
	endif       

	$instancehandle->valorPadrao()
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		exit(-1)
	elseif ($status < 0)
		exit(-1)
	endif

	if (PR_TOTALDESC.SIS_DESCONTO <= PR_DESCMAXIMO.SIS_DESCONTO)
		macro "^ACCEPT"
	endif

	$cdUsuarioDesc$ = "" ;Hugo em 08/04/11 Tarefa 182-39
	
	return(0)
End ; operation preEDIT

;------------------------
entry posEDIT 
;------------------------
	$xlpo$ = ""
	putitem/id $xlpo$, "CD_USUARIODESC", $cdUsuarioDesc$

	return(0)
End ; operation posEDIT

#include <LIB_PADRAO>:DEF_TEMPLATE
;---------------------
partner operation INIT 
	;---------------------
	variables
		string viParams, voParams
		numeric vCdEmpresa
	endvariables

	vCdEmpresa = $item("CD_EMPRESA", $$gParamGlb)
	
	;Retire o comentário das linhas abaixo para carregar os parâmetros locais
	;Parametros gerais
	;$xlpl$ = ""
	;putitem $xlpl$, -1, "NOME PARAMETRO"
	;activate "ADMSVCO001".GetLstParametro($xlpl$, $xlpl$, $xcderro$, $xctxerro$)
	;if ($procerror)
	;	$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	;elseif ($xcdErro$)
	;	$instancehandle->SetErroApl($xCtxErro$, $xCdErro$, $xCtxErro$)
	;endif
	
	;;Parametros por empresa
	viParams = ""
	putitem viParams, -1, "DESCRICAO_DESC_MAX"
	putitem viParams, -1, "IN_UTILIZA_BIOMETRIA_FAT"	
	putitem viParams, -1, "IN_UTILIZA_DESC_MAX_CLI"		
	activate "ADMSVCO001".GetParamEmpresa(vCdEmpresa, viParams, voParams, $xcderro$, $xctxerro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	elseif ($xcdErro$)
		$instancehandle->SetErroApl($xCtxErro$, $xCdErro$, $xCtxErro$)
	endif
	$descricaoDescMax$ = $item("DESCRICAO_DESC_MAX", voParams)	
	$inUtilizaBiometria$ = $item("IN_UTILIZA_BIOMETRIA_FAT", voParams)
	$inUtilizaDescCli$ = $item("IN_UTILIZA_DESC_MAX_CLI", voParams)		

	$instancehandle->convPriLetraMaiuscula($descricaoDescMax$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	endif	

	$formtitle = "%%$componentname - Valida Desconto da Transação"
End ; operation INIT

;------------------------
partner operation CLEANUP 
	;------------------------
	
End ; operation CLEANUP

;------------------------
partner operation valorPadrao 
	;------------------------

	clear/e "SIS_DESCONTO"

	clear/e "V_TRA_TOTITEM"
	CD_EMPRESA.V_TRA_TOTITEM/init = $cdEmpresa$
	NR_TRANSACAO.V_TRA_TOTITEM/init = $nrTransacao$
	DT_TRANSACAO.V_TRA_TOTITEM/init = $dtTransacao$
	retrieve/e "V_TRA_TOTITEM"
	if ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Transação %%$cdEmpresa$ / %%$nrTransacao$ / %%$dtTransacao$ não encontrada!", "")
		return(-1)
	endif

	DS_DESCMAXIMO.SIS_DESCONTO/init = $descricaoDescMax$
	VL_TOTALBRUTO.SIS_DESCONTO/init = VL_TOTALBRUTO.V_TRA_TOTITEM
	VL_TOTALLIQUIDO.SIS_DESCONTO/init = VL_TOTALLIQUIDO.V_TRA_TOTITEM
	PR_DESCMAXIMO.SIS_DESCONTO/init = $prDescMaximo$
	VL_DESCCAPA.SIS_DESCONTO/init = VL_TOTALDESCCAB.V_TRA_TOTITEM
	PR_DESCCAPA.SIS_DESCONTO/init = (VL_TOTALDESCCAB.V_TRA_TOTITEM / VL_TOTALBRUTO.V_TRA_TOTITEM) * 100
	VL_DESCITEM.SIS_DESCONTO/init = VL_TOTALDESC.V_TRA_TOTITEM
	PR_DESCITEM.SIS_DESCONTO/init = (VL_TOTALDESC.V_TRA_TOTITEM / VL_TOTALBRUTO.V_TRA_TOTITEM) * 100
	VL_TOTALDESC.SIS_DESCONTO/init = VL_TOTALDESCCAB.V_TRA_TOTITEM + VL_TOTALDESC.V_TRA_TOTITEM 
	PR_TOTALDESC.SIS_DESCONTO/init = ((VL_TOTALDESCCAB.V_TRA_TOTITEM + VL_TOTALDESC.V_TRA_TOTITEM) / VL_TOTALBRUTO.V_TRA_TOTITEM) * 100		

	if ($inUtilizaDescCli$) 
		fieldsyntax "BT_ALTERARDESCONTO.SIS_BOTOES", "DIM"
	else
		fieldsyntax "BT_ALTERARDESCONTO.SIS_BOTOES", ""
	endif

	return(0)
End ; operation valorPadrao

;------------------------
partner operation convPriLetraMaiuscula
	;------------------------
	params
		string piDsCampo :INOUT
	endparams
	variables
		string vDescricao, vPriLetra, vPriLetraM
	endvariables

	if (piDsCampo = "")
		return(-1)
	endif

	lowercase piDsCampo, vDescricao
	vPriLetra = vDescricao[1,1]
	uppercase vPriLetra, vPriLetraM

	piDsCampo = $replace(vDescricao, 1, vPriLetra, vPriLetraM)

	return(0)
End ; operation convPriLetraMaiuscula

;---------------------------------
partner operation alteraDescMaximo
	;---------------------------------
	variables
		string viParams, voParams, vPrDescMaximoAnt
	endvariables	

	viParams = ""
	putitem/id viParams, "IN_USULOGADO", <TRUE>
	putitem/id viParams, "DS_COMPONENTE", "TRAFM062"
	putitem/id viParams, "IN_UTILIZA_BIOMETRIA", $InUtilizaBiometria$
	activate "ADMFM020".exec(viParams, voParams, $xCdErro$, $xCtxerro$)
	if ($procerror)  
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")     
		return (-1)	
	elseif ($status < 0)
		return (-1)
	endif
	
	$cdUsuarioDesc$ = $item("CD_USUARIO", voParams)
	
	clear/e "TRA_LIMDESCONTO"
	CD_OPERACAO.TRA_LIMDESCONTO/init = CD_OPERACAO.V_TRA_TOTITEM
	CD_USUARIO.TRA_LIMDESCONTO/init = $cdUsuarioDesc$
	retrieve/e "TRA_LIMDESCONTO"
	if ($status >= 0)
		$prDescMaximo$ = PR_DESCMAX.TRA_LIMDESCONTO
		PR_DESCMAXIMO.SIS_DESCONTO = PR_DESCMAX.TRA_LIMDESCONTO
	else
		$prDescMaximo$ = ""
		PR_DESCMAXIMO.SIS_DESCONTO = ""
		clear/e "TRA_LIMDESCONTO"
	endif
	
	return(0)
End ; operation alteraDescMaximo

;------------------------
partner operation confirmar 
	;------------------------
	if (PR_TOTALDESC.SIS_DESCONTO < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Desconto não pode ser negativo!", "")
		return(-1)
	endif

	if (PR_TOTALDESC.SIS_DESCONTO > PR_DESCMAXIMO.SIS_DESCONTO)
		if (!$inUtilizaDescCli$)
			$instancehandle->alteraDescMaximo()
			if ($procerror)
				$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
				return(-1)
			elseif ($status < 0)
				return(-1)
			endif
		endif

		if (PR_TOTALDESC.SIS_DESCONTO > PR_DESCMAXIMO.SIS_DESCONTO)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Valor maior que o desconto máximo permitido para o cliente!", "")
			return(-1)
		endif		
	endif

	macro "^ACCEPT"
	
	return(0)
End ; operation confirmar