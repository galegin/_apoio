;------------------------
entry preEDIT 
	;------------------------
	$nrSeqHistRelSub$ = $item("NR_SEQHISTRELSUB", $xlpi$)
	$nrDocumento$ = $item("NR_DOCUMENTO", $xlpi$)
	$inDireto$ = $item("IN_DIRETO", $xlpi$)

	if ($nrDocumento$ = 0)
		$instancehandle->numerar()
		if ($procerror)
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		endif
	endif
	
	$instancehandle->carregarCartao()
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	endif
	
	$instancehandle->valorPadrao()
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	endif
	
	return(0)
End ; operation preEDIT

;-------------------------------
partner operation carregarCartao
;-------------------------------
	variables
		string viParams, voParams
	endvariables
	
	viParams = ""
	putitem/id viParams, "TP_DOCUMENTO", 5
	
	activate "GERSVCO007".ListHistRelSub(viParams, voParams, $xCdErro$, $xCtxErro$)
	if ($procerror)       
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
		return(-1)
	endif
	
	$valrep(NR_SEQHISTRELSUB.SIS_DUMMY) = voParams
	
	return(0)
end ;getEmpresa

;----------------------------
partner operation valorPadrao
;----------------------------
	variables
		string vDsLstDocumento
		numeric vNrDocumento
	endvariables

	if ($inDireto$ = <TRUE>)
		if ($nrSeqHistRelSub$ = 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Sequência de cartão de débito não informada!", "")
			return(-1)
		endif
		fieldsyntax NR_SEQHISTRELSUB.SIS_DUMMY, "DIM"
	endif

	NR_SEQHISTRELSUB.SIS_DUMMY/init = $nrSeqHistRelSub$
	NR_DOCUMENTO.SIS_DUMMY/init = $nrDocumento$

	return(0)
end

;------------------------
partner operation numerar
;------------------------
	variables
		numeric vNrFat
		date vDtSistema
		string viParams,voParams
	endvariables
	
	viParams = ""
	putitem/id viParams, "NM_ENTIDADE", "FCR_FATURAI"
	;    newinstance "GERSVCO031", "GERSVCO031", "TRANSACTION=TRUE"
	activate "GERSVCO031".getNumSeq($xlpg$, viParams, voParams, $xCdErro$, $xCtxerro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)  
	elseif ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
		return(-1)  
	endif    
	
	$nrDocumento$ = $item("NR_SEQUENCIA", voParams)
	
	return(0)
end

operation validaCampos
  variables
    date vDtSistema, vDtVencimento
  endvariables

  if (NR_SEQHISTRELSUB.SIS_DUMMY = 0)
     $instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Operadora não informada!", "")
    $prompt = NR_SEQHISTRELSUB.SIS_DUMMY
    return(-1)
  endif

  if (NR_DOCUMENTO.SIS_DUMMY = 0)
     $instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Documento não informado!", "")
    $prompt = NR_DOCUMENTO.SIS_DUMMY
    return(-1)
  endif

  if (DT_VENCIMENTO.SIS_DUMMY != "")
    vDtSistema = $item("DT_SISTEMA", $$gParamGlb)
    vDtVencimento = vDtSistema + NR_DIASMAXPRE.SIS_DUMMY

    if (DT_VENCIMENTO > vDtVencimento)
      $instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Data de vencimento maior que a permitida pelo cartão!", "")
      $prompt = DT_VENCIMENTO.SIS_DUMMY
      return(-1)
    endif
  endif

  $xlpo$ = ""
  putitem/id $xlpo$, "NR_SEQHISTRELSUB", NR_SEQHISTRELSUB.SIS_DUMMY
  putitem/id $xlpo$, "NR_DOCUMENTO", NR_DOCUMENTO.SIS_DUMMY
  if (DT_VENCIMENTO.SIS_DUMMY = "")
    putitem/id $xlpo$, "DT_VENCIMENTO", $item("DT_SISTEMA", $$gParamGlb)
    putitem/id $xlpo$, "IN_PREDATADO", <FALSE>
  else
    putitem/id $xlpo$, "DT_VENCIMENTO", DT_VENCIMENTO.SIS_DUMMY
    putitem/id $xlpo$, "IN_PREDATADO", <TRUE>
  endif

  putitem/id $xlpo$, "CD_AUTORIZACAO", NR_AUTORIZACAO.SIS_DUMMY

  exit(0)
end

;------------------------
operation buscaDiasMaxPre
;------------------------

NR_DIASMAXPRE.SIS_DUMMY/init = ""

clear/e "FCX_HISTRELSUB"
TP_DOCUMENTO.FCX_HISTRELSUB/init = 5
NR_SEQHISTRELSUB.FCX_HISTRELSUB/init = NR_SEQHISTRELSUB.SIS_DUMMY
retrieve/e "FCX_HISTRELSUB"
if ($status >= 0)
	clear/e "FGR_FORMULACAREMP"
	CD_EMPRESA.FGR_FORMULACAREMP/init = $item("CD_EMPRESA", $$gParamGlb)
	CD_FORMULACARTAO.FGR_FORMULACAREMP/init = CD_FORMULACARTAO.FCX_HISTRELSUB
	retrieve/e "FGR_FORMULACAREMP"
	if ($status >= 0) & (IN_PREDATADO.FGR_FORMULACAREMP = <TRUE>)
		fieldsyntax DT_VENCIMENTO.SIS_DUMMY, ""
		NR_DIASMAXPRE.SIS_DUMMY/init = NR_DIASMAXPRE.FGR_FORMULACAREMP
	else
		clear/e "FGR_FORMULACARTAO"
		CD_FORMULACARTAO.FGR_FORMULACARTAO/init = CD_FORMULACARTAO.FCX_HISTRELSUB
		retrieve/e "FGR_FORMULACARTAO"
		if ($status >= 0) & (IN_PREDATADO.FGR_FORMULACARTAO = <TRUE>)
			fieldsyntax DT_VENCIMENTO.SIS_DUMMY, ""
			NR_DIASMAXPRE.SIS_DUMMY/init = NR_DIASMAXPRE.FGR_FORMULACARTAO
		else
			DT_VENCIMENTO.SIS_DUMMY/init = ""
			fieldsyntax DT_VENCIMENTO.SIS_DUMMY, "DIM"
		endif
	endif
else
	DT_VENCIMENTO.SIS_DUMMY/init = ""
	fieldsyntax DT_VENCIMENTO.SIS_DUMMY, "DIM"
endif

return(0)

end