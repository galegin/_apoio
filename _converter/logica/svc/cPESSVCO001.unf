;---------------------------------
Public Operation GravaConsultaSci
;---------------------------------
	;Fornaroli - 02/06/04.
	;Grava Informacoes da consulta sci.
	params
		numeric piCdPessoa   :IN
		string  piDsConsulta :IN
		string  poCdErro     :OUT
		string  poCtxErro    :OUT
	endparams
	variables
		numeric vNrSequencia
	endvariables

	clear/e "PES_REFERENCISVC"
	selectdb max(nr_sequencia) from "PES_REFERENCISVC" u_where                %\
									(cd_pessoa.pes_referencisvc = piCdPessoa) %\
									to (vNrSequencia)

	creocc "PES_REFERENCISVC",-1
	cd_pessoa.pes_referencisvc     = piCdPessoa
	nr_sequencia.pes_referencisvc  = vNrSequencia + 1
	tp_referencia.pes_referencisvc = "S"
	cd_operador.pes_referencisvc   = $item("CD_USUARIO",$$gParamGlb)
	dt_cadastro.pes_referencisvc   = $item("DT_SISTEMA",$$gParamGlb)

	validateocc "PES_REFERENCISVC"
	if ($status = 0)
		$collhandle ("PES_REFERENCISVC")->Salvar()
	endif


	clear/e "OBS_REFERENCISVC"
	creocc "OBS_REFERENCISVC",-1
	cd_pessoa.obs_referencisvc     = piCdPessoa
	nr_sequencia.obs_referencisvc  = vNrSequencia + 1
	dt_observacao.obs_referencisvc = $date
	hr_observacao.obs_referencisvc = $clock
	ds_observacao.obs_referencisvc = piDsConsulta
	cd_operador.obs_referencisvc   = $item("CD_USUARIO",$$gParamGlb)
	dt_cadastro.obs_referencisvc   = $item("DT_SISTEMA",$$gParamGlb)

	validateocc "OBS_REFERENCISVC"
	if ($status = 0)
		$collhandle ("OBS_REFERENCISVC")->Salvar()
	endif
	
	return(0)
End ;GravaConsultaSci.



public operation VldInEmail 
params 
   numeric poCdPessoa :in
   boolean poInPadrao :out
   numeric poCdErro   :out
   string  poCtxErro  :out
endparams 
poCtxErro="" ; inicializa vazio
poCdErro = "" ; inicializa zero
 
clear/e "pes_emailsvc"
cd_pessoa.pes_emailsvc/init=poCdPessoa ; move codigo da pessoa que se esta buscando
in_padrao.pes_emailsvc/init=<True> ; move valor do in que se esta buscando
retrieve/e  "pes_emailsvc" ; ler todos os email da pessoa do parametro, buscando in_padrao marcado 
if($status = -2) ; não encontrou
   poInPadrao=<false>
elseif ($status = 0) ; econtrou o registro marcado
   poInPadrao=<True>
else
   $instancehandle->SetErroOpr($procerrorcontext,poCdErro,poCtxErro) ; chamada de uma operation que retorno o erro diferente de -1 e -2
endif   
return (0); finaliza a execução da operation

;=========================================================================
;Autor: BAM
;
;Nome : VldInEmail		
;
;Data : 18/03/2002
;
;Versão : V1
;
;Descrição : Valida se ja existe um e-mail padrão
;
;
;Parametros Entrada : recebe a chave do e-mail
;
;Parametros Saída : indicador (0- nao 1 - sim existe)
;                   codigo do erro
;                   contexto do erro
;
;Histórico Manutenção :
;
;Exemplo :
; activate "PESSVCO001".VldInEmail(cd_chave,Indicador,$xcderro$,$xctxerro$) 
;Data	Operador	DetalhesEnd ; operation VldInPadraoEnd ; operation VldInPadrao

End ; operation VldInEmail

public operation VldInTelefone 
params 
   numeric poCdPessoa :in
   boolean poInPadrao :out
   numeric poCdErro   :out
   string  poCtxErro  :out
endparams 
poCtxErro="" ; inicializa vazio
poCdErro = "" ; inicializa zero
 
clear/e "pes_telefonesvc"
cd_pessoa.pes_telefonesvc/init=poCdPessoa ; move codigo da pessoa que se esta buscando
in_padrao.pes_telefonesvc/init=<True> ; move valor do in que se esta buscando
retrieve/e  "pes_telefonesvc" ; ler todos os telefones da pessoa do parametro, buscando in_padrao marcado 
if($status = -2) ; não encontrou
   poInPadrao=<false>
elseif ($status = 0) ; econtrou o registro marcado
   poInPadrao=<True>
else
   $instancehandle->SetErroOpr($procerrorcontext,poCdErro,poCtxErro) ; chamada de uma operation que retorno o erro diferente de -1 e -2
endif   
return (0); finaliza a execução da operation


;=========================================================================
;Autor: BAM
;
;Nome : VldInTelefone		
;
;Data : 18/03/2002
;
;Versão : V1
;
;Descrição : Valida se ja existe um telefone padrão
;
;
;Parametros Entrada : recebe a chave do e-mail
;
;Parametros Saída : indicador (0- nao 1 - sim existe)
;                   codigo do erro
;                   contexto do erro
;
;Histórico Manutenção :
;
;Exemplo :
; activate "PESSVCO001".VldInTelefone(cd_chave,Indicador,$xcderro$,$xctxerro$) 
;Data	Operador	DetalhesEnd ; operation VldInPadraoEnd ; operation VldInPadrao


End ; operation VldInTelefone

;#include U:zProcError  ; Report an unexpected failure in a proc statement.


;----------------------------------------------------------;
public operation validaGravaFornecedor                     ;
;----------------------------------------------------------;
; MTF    : 07/07/2006              Projeto 075, tarefa 004 ;
; Funcao : Verifica se o codigo informado ja é fornecedor. ;
;          Caso nao seja, torna-lo um fornecedor.          ;
; Chamada: FCRFP038 - geraDuplicata()                      ;
;----------------------------------------------------------;
;
	params
		string  piGlobal  :IN
		string  piParams  :IN
		string  poParams  :OUT
		numeric poCdErro  :OUT
		string  poCtxErro :OUT
	endparams
	variables
		numeric vCdFornecedor
	endvariables
	
	vCdFornecedor = $item("CD_FORNECEDOR", piParams)
	if (vCdFornecedor = "")
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Favor informar o código de fornecedor para efetuar cadastro.", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
	
	clear/e "PES_FORNECEDOSVC"
	cd_fornecedor.pes_fornecedosvc/init = vCdFornecedor
	retrieve/e "PES_FORNECEDOSVC"
	if ($status < 0)
		;Codigo nao é fornecedor, entao torna-lo um.
		clear/e "PES_FORNECEDOSVC"
		creocc "PES_FORNECEDOSVC", -1
		cd_fornecedor.pes_fornecedosvc = vCdFornecedor
		cd_operador.pes_fornecedosvc   = $item("CD_USUARIO", piGlobal)
		dt_cadastro.pes_fornecedosvc   = $datim
		
		$collhandle("PES_FORNECEDOSVC")->Salvar()
		if ($procerror)
			$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "ADICIONAL= PESSVCO001.gravaFornecedor()")
			poCdErro  = $xCdErro$
			poCtxErro = $xCtxErro$
			return(-1)
		elseif ($status < 0)
			$instancehandle->setStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "ADICIONAL= PESSVCO001.gravaFornecedor()")
			poCdErro  = $xCdErro$
			poCtxErro = $xCtxErro$
			return(-1)
		endif
	endif
	
	return(0)
End ;operation gravaFornecedor.