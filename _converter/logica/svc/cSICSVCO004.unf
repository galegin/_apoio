;-----------------------------
public operation filtraEmpresa
;-----------------------------
	params
		string  $xLpg$    :IN
		string  piParams  :IN
		string  poParams  :OUT
		numeric poCdErro  :OUT
		string  poCtxErro :OUT 
	endparams
	variables
		numeric vN
		string  vLstCdEmpresa, vLstCdGrupoEmpresa, vLstCdGrupoEmp, vLstAux, vpiParams, vpoParams
	endvariables
	
	vLstCdEmpresa      = ""
	vLstCdGrupoEmpresa = ""
	vN = 0
	
;   Verifica se o usuário é Administrador
	clear/e "ADM_USUARIOSVC"
	cd_usuario.adm_usuariosvc/init = $item("CD_USUARIO", piParams)
	retrieve/e "ADM_USUARIOSVC"
	if (!$procerror)      
		clear/e "GER_GRUPOEMPRSVC"
		cd_grupoempresa.ger_grupoemprsvc/init = $item("CD_GRUPOEMPRESA", piParams)
		retrieve/e "GER_GRUPOEMPRSVC"
		if ($status >= 0)
			
			repeat
				clear/e "GER_EMPRESASVC"
				cd_grupoempresa.ger_empresasvc/init = $item("CD_GRUPOEMPRESA", piParams)
				retrieve/e "GER_EMPRESASVC"
				if (!$procerror)
					
					setocc "GER_EMPRESASVC",1
					while ($status >=0)
						if (tp_privilegio.adm_usuariosvc = "1") | (tp_privilegio.adm_usuariosvc = "4")
							clear/e "adm_usucmpempsvc"
							cd_usuario.adm_usucmpempsvc/init    = $item("CD_USUARIO", piParams)
							cd_empresa.adm_usucmpempsvc/init    = cd_empresa.ger_empresasvc
							cd_componente.adm_usucmpempsvc/init = $item("CD_COMPONENTE", piParams)
							retrieve/e "adm_usucmpempsvc"
							if ($status = 0)
								vN = vN + 1
								if (cd_ccusto.ger_empresasvc = "")
									putitem vLstCdEmpresa, -1, "%%cd_empresa.adm_usucmpempsvc"
								else
									putitem vLstCdGrupoEmpresa, -1, "%%cd_empresa.adm_usucmpempsvc"
								endif
							endif
						else
							vN = vN + 1
							if (cd_ccusto.ger_empresasvc = "")
								putitem vLstCdEmpresa, -1, "%%cd_empresa.ger_empresasvc"
							else
								putitem vLstCdGrupoEmpresa, -1, "%%cd_empresa.ger_empresasvc"
							endif			
						endif
						setocc "GER_EMPRESASVC",$curocc(GER_EMPRESASVC) + 1
					endwhile
				endif
				
				setocc "GER_GRUPOEMPRSVC", $curocc(GER_GRUPOEMPRSVC) + 1
			until($status < 0)
			$status = 0
		endif
	endif

	sort/list vLstCdEmpresa,"U"
	sort/list vLstCdGrupoEmpresa,"U"
	
	vLstCdGrupoEmp = vLstCdGrupoEmpresa
	
	clear/e "GER_EMPRESASVC"
	cd_empresa.ger_empresasvc/init = $item("CD_EMPRESA", $xLpg$)
	retrieve/e "GER_EMPRESASVC"
	if (cd_ccusto.ger_empresasvc = "")
		vLstCdGrupoEmpresa = ""
	endif
 
	;Utilizado p/ o processo de recebimento de faturas. Busca o codigo da empresa correspondente a empresa logada.
	if ($item("IN_RECEBIMENTO", piParams) = <TRUE>)
		if (cd_ccusto.ger_empresasvc = "")
			if ($item("IN_UTILIZA_AUDITORIA", piParams) = <TRUE>)
				;Esta logado na empresa principal, entao pega a lista das empresas encargos dos grupos 
				;empresas informados no filtro do FCRFP002 p/ efetuar a consulta na rotina de gravacao 
				;da auditoria do SICSVCO009 -> gravaLog.
				;vLstCdEmpresa      = ""
				;vLstCdEmpresa      = vLstCdGrupoEmp
				;vLstCdGrupoEmpresa = ""
				vLstCdGrupoEmpresa  = vLstCdGrupoEmp
				
				vpiParams = ""
				activate "SICSVCO009".validaCentroCusto($xLpg$, vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
				if ($procerror)
					$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "ADICIONAL=  / SICSVCO004 -> filtraEmpresa() chamando SICSVCO009 -> validaCentroCusto()")
					poCdErro  = $xCdErro$
					poCtxErro = $xCtxErro$
					return(-1)
				elseif ($status < 0)
					$instancehandle->setStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "ADICIONAL=  / SICSVCO004 -> filtraEmpresa() chamando SICSVCO009 -> validaCentroCusto()")
					poCdErro  = $xCdErro$
					poCtxErro = $xCtxErro$
					return(-1)
				else
					if ($item("CD_CENTROCUSTO", vpoParams) = "")
						vLstCdEmpresa = ""
					endif
				endif
			else
				vLstCdGrupoEmpresa = ""
			endif
		else
			if ($item("IN_UTILIZA_AUDITORIA", piParams) = <TRUE>)
				;Esta logado na empresa encargos, entao pega a lista das empresas principal dos grupos 
				;empresas informados no filtro do FCRFP002 p/ efetuar a consulta na rotina de gravacao 
				;da auditoria do SICSVCO009 -> gravaLog.
				
				if ($item("IN_VALIDAEMPRESA", piParams) = <TRUE>) ;Utilizado no recebimento pra selecionar as empresas fiscal e encargos na selecao das faturas a receber.
					vLstCdEmpresa = "%%vLstCdEmpresa·;%%vLstCdGrupoEmpresa"
				else	
					;vLstCdGrupoEmpresa = ""
					vpiParams = ""
					activate "SICSVCO009".validaCentroCusto($xLpg$, vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
					if ($procerror)
						$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "ADICIONAL=  / SICSVCO004 -> filtraEmpresa() chamando SICSVCO009 -> validaCentroCusto()")
						poCdErro  = $xCdErro$
						poCtxErro = $xCtxErro$
						return(-1)
					elseif ($status < 0)
						$instancehandle->setStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "ADICIONAL=  / SICSVCO004 -> filtraEmpresa() chamando SICSVCO009 -> validaCentroCusto()")
						poCdErro  = $xCdErro$
						poCtxErro = $xCtxErro$
						return(-1)
					else
						if ($item("CD_EMPRESA", vpoParams) = "")
							vLstCdEmpresa = ""
						endif
					endif
					if ($item("IN_FLAG", piParams) = <TRUE>)
						vLstCdGrupoEmpresa = vLstCdEmpresa
					endif
				endif
			else
				vLstCdGrupoEmpresa = ""
				vLstCdEmpresa      = ""
				vLstCdEmpresa      = vLstCdGrupoEmp
			endif
		endif
	endif
	
	putitem/id poParams, "LST_CDEMPRESA",    vLstCdEmpresa
	putitem/id poParams, "LST_GRUPOEMPRESA", vLstCdGrupoEmpresa
	
	return (0)
End ; operation filtraEmpresa.


;-------------------------------------------------------------;
public operation validaEmpresa                                ;
;-------------------------------------------------------------;
; MTF - 18/07/2005                                            ;
; Funcao : Valida as empresas de acordo com a empresa logada. ;
;          Se for encargos, considera todas empresas          ;
;          informadas, caso seja principal somente filtra as  ;
;          empresas do principal.                             ;
; Chamada: FCRFP002 - leave field - cd_empresa.sis_dummy      ;
;          FCRFP016 - leave field - cd_empresa.dummy          ;
;-------------------------------------------------------------;
;
	params
		string  piGlobal  :IN
		string  piParams  :IN
		string  poParams  :OUT
		numeric poCdErro  :OUT
		string  poCtxErro :OUT
	endparams

	variables
		string  vLstCdEmpresa, vLstCdGrupoEmpresa, vLstEmpresa
		numeric vCdEmpresaLog, vCdPoolEmpresa, vCdUsuario
		boolean vInValidaPool
	endvariables
	
;	if ($item("CD_EMPRESA", piParams) = "")
;		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Não há empresas informada para recebimento", "")
;		poCdErro  = $xCdErro$
;		poCtxErro = $xCtxErro$
;		return(-1)
;	endif

	vCdEmpresaLog = $item("CD_EMPRESALOG", piParams)
	vCdUsuario = $item("CD_USUARIO", piParams)
	if (vCdUsuario = 0)
		vCdUsuario = $item("CD_USUARIO", piGlobal)
	endif
	vInValidaPool = $item("IN_VALIDAPOOL", piParams)
	if (vInValidaPool = "")
		vInValidaPool = <TRUE>
	endif
	
;   Verifica se o usuário é Administrador
	clear/e "ADM_USUARIOSVC"
	cd_usuario.adm_usuariosvc/init = vCdUsuario
	retrieve/e "ADM_USUARIOSVC"
	if ($status >= 0)      
		
		clear/e "GER_EMPRESASVC"
		cd_empresa.ger_empresasvc/init = $item("CD_EMPRESA", piParams)
		retrieve/e "GER_EMPRESASVC"
		if ($status >= 0)
			
			setocc "GER_EMPRESASVC",1
			repeat
				if (tp_privilegio.adm_usuariosvc = "1") | (tp_privilegio.adm_usuariosvc = "4")
					clear/e "adm_usucmpempsvc"
					cd_usuario.adm_usucmpempsvc/init    = vCdUsuario
					cd_empresa.adm_usucmpempsvc/init    = cd_empresa.ger_empresasvc
					cd_componente.adm_usucmpempsvc/init = $item("CD_COMPONENTE", piParams)
					retrieve/e "adm_usucmpempsvc"
					if ($status >= 0)
						if (cd_ccusto.ger_empresasvc = "")
							putitem vLstCdEmpresa, -1, "%%cd_empresa.adm_usucmpempsvc"
						else
							putitem vLstCdGrupoEmpresa, -1, "%%cd_empresa.adm_usucmpempsvc"
						endif
					endif
				else
					;;;DBB | PRJ 094/0570 | 23/05/2008
					if (tp_privilegio.adm_usuariosvc = "5");Admin por Empresa
						clear/e "ADM_USUEMPSVC"
						CD_USUARIO.ADM_USUEMPSVC/init = vCdUsuario
						CD_EMPRESA.ADM_USUEMPSVC/init = cd_empresa.ger_empresasvc
						retrieve/e "ADM_USUEMPSVC"
						if ($status >= 0)
							;== BY BIANCHINI [PRJ/TAREFA 78/4124] 03/03/2011==;
							;putitem vLstCdEmpresa, -1, "%%cd_empresa.ger_empresasvc"
							if (cd_ccusto.ger_empresasvc = "")
								putitem vLstCdEmpresa, -1, "%%cd_empresa.ger_empresasvc"
							else
								putitem vLstCdGrupoEmpresa, -1, "%%cd_empresa.ger_empresasvc"
							endif
							;==
						endif
					;;;
					else
						if (cd_ccusto.ger_empresasvc = "")
							putitem vLstCdEmpresa, -1, "%%cd_empresa.ger_empresasvc"
						else
							putitem vLstCdGrupoEmpresa, -1, "%%cd_empresa.ger_empresasvc"
						endif			
					endif
				endif
				setocc "GER_EMPRESASVC",$curocc(GER_EMPRESASVC) + 1
			until($status < 0)
			$status = 0
		endif
	endif
	
	if ($item("CD_POOLEMPRESA", piParams) != "")
		vCdPoolEmpresa = $item("CD_POOLEMPRESA", piParams)
	else
		vCdPoolEmpresa = $item("CD_POOLEMPRESA", piGlobal)
	endif
	if (vCdPoolEmpresa > 0) & (vInValidaPool = <TRUE>)
		;VALIDA vLstCdEmpresa
		clear/e "TMP_NR09SVC"
		getlistitems vLstCdEmpresa, NR_GERAL.TMP_NR09SVC
		setocc "TMP_NR09SVC"
		while ($status >= 0)
			clear/e "GER_EMPRESASVC"
			CD_EMPRESA.GER_EMPRESASVC = NR_GERAL.TMP_NR09SVC
			retrieve/e "GER_EMPRESASVC"
			if ($status >= 0)
				clear/e "GER_POOLGRUPOSVC"
				CD_POOLEMPRESA.GER_POOLGRUPOSVC = vCdPoolEmpresa
				CD_GRUPOEMPRESA.GER_POOLGRUPOSVC = CD_GRUPOEMPRESA.GER_EMPRESASVC
				retrieve/e "GER_POOLGRUPOSVC"
				if ($status >= 0)
					setocc "TMP_NR09SVC", $curocc("TMP_NR09SVC") + 1
				else
					discard "TMP_NR09SVC"
					if ($status = 0)
						$status = -1
					endif
				endif
			else
				discard "TMP_NR09SVC"
				if ($status = 0)
					$status = -1
				endif
			endif
		endwhile

		sort/e "TMP_NR09SVC", "NR_GERAL.TMP_NR09SVC"
		putlistitems vLstCdEmpresa, NR_GERAL.TMP_NR09SVC	


		;VALIDA vLstCdGrupoEmpresa
		clear/e "TMP_NR09SVC"
		getlistitems vLstCdGrupoEmpresa, NR_GERAL.TMP_NR09SVC
		setocc "TMP_NR09SVC"
		while ($status >= 0)
			clear/e "GER_EMPRESASVC"
			CD_EMPRESA.GER_EMPRESASVC = NR_GERAL.TMP_NR09SVC
			retrieve/e "GER_EMPRESASVC"
			if ($status >= 0)
				clear/e "GER_POOLGRUPOSVC"
				CD_POOLEMPRESA.GER_POOLGRUPOSVC = vCdPoolEmpresa
				CD_GRUPOEMPRESA.GER_POOLGRUPOSVC = CD_GRUPOEMPRESA.GER_EMPRESASVC
				retrieve/e "GER_POOLGRUPOSVC"
				if ($status >= 0)
					setocc "TMP_NR09SVC", $curocc("TMP_NR09SVC") + 1
				else
					discard "TMP_NR09SVC"
					if ($status = 0)
						$status = -1
					endif
				endif
			else
				discard "TMP_NR09SVC"
				if ($status = 0)
					$status = -1
				endif
			endif
		endwhile

		sort/e "TMP_NR09SVC", "NR_GERAL.TMP_NR09SVC"
		putlistitems vLstCdGrupoEmpresa, NR_GERAL.TMP_NR09SVC	
	endif

	poParams    = ""
	vLstEmpresa = ""
	
	if ($item("IN_AUDITORIA", piParams) = <TRUE>) ;Utilizado no processo de recebimento.
		;25/04/2006-----------------------------------------------------;
		clear/e "GER_EMPRESASVC"						;
		cd_empresa.ger_empresasvc/init = $item("CD_EMPRESA", piGlobal)	;
		retrieve/e "GER_EMPRESASVC"						;
		if (cd_ccusto.ger_empresasvc = "")				;
			putitem/id vLstEmpresa, "CD_EMPRESA",     vLstCdEmpresa	;
			putitem/id poParams,    "LISTA_EMPRESA",  vLstEmpresa	;
			putitem/id poParams,    "LST_EMPRESA",    "%%vLstCdEmpresa"	;
			putitem/id poParams,    "CD_EMPRESA",     vLstCdEmpresa	;
		else											;
			putitem/id vLstEmpresa, "CD_EMPRESA",     vLstCdEmpresa
			putitem/id vLstEmpresa, "CD_CENTROCUSTO", vLstCdGrupoEmpresa
			putitem/id poParams,    "LISTA_EMPRESA",  vLstEmpresa
			putitem/id poParams,    "LST_EMPRESA",    "%%vLstCdEmpresa·;%%vLstCdGrupoEmpresa"
			putitem/id poParams,    "CD_EMPRESA",     vLstCdEmpresa
			putitem/id poParams,    "CD_CENTROCUSTO", vLstCdGrupoEmpresa
		endif											;
		;25/04/2006-----------------------------------------------------;
	else
		clear/e "GER_EMPRESASVC"
		if (vCdEmpresaLog = "")
			cd_empresa.ger_empresasvc/init = $item("CD_EMPRESA", piGlobal)
		else
			cd_empresa.ger_empresasvc/init = vCdEmpresaLog
		endif
		retrieve/e "GER_EMPRESASVC"
		if ($status >= 0)
			if (cd_ccusto.ger_empresasvc = "")
				if ($item("IN_EMP_PAGTO_AUTO", piParams) = <TRUE>)
					poParams = ""
					putitem/id poParams,    "CD_EMPRESA",     vLstCdEmpresa
				else
					putitem/id vLstEmpresa, "CD_EMPRESA",     vLstCdEmpresa
					putitem/id poParams,    "LISTA_EMPRESA",  vLstEmpresa
					putitem/id poParams,    "LST_EMPRESA",    "%%vLstCdEmpresa"
					putitem/id poParams,    "CD_EMPRESA",     vLstCdEmpresa
				endif
			else
				;25/04/2006-------------------------------------------------------------;
				if ($item("IN_RECEBIMENTO", piParams) = <TRUE>)				;
					putitem/id vLstEmpresa, "CD_CENTROCUSTO", vLstCdGrupoEmpresa	;
					putitem/id poParams,    "LISTA_EMPRESA",  vLstEmpresa		;
					putitem/id poParams,    "LST_EMPRESA",    "%%vLstCdGrupoEmpresa"	;
					putitem/id poParams,    "CD_EMPRESA",     vLstCdGrupoEmpresa	;
				else
					if ($item("IN_EMP_PAGTO_AUTO", piParams) = <TRUE>)
						poParams = ""
						putitem/id poParams, "CD_EMPRESA", vLstCdGrupoEmpresa
					else
						;
						if ($item("IN_CCUSTO", piParams) = <TRUE>) ;Somente pega encargos.
							putitem/id poParams,     "LST_EMPRESA",   vLstCdGrupoEmpresa
							if ($item("IN_MATRIZ", piParams) = <TRUE>)
								vLstEmpresa = ""
								putitem/id vLstEmpresa, "CD_EMPRESA", vLstCdGrupoEmpresa
								putitem/id poParams,    "LISTA_EMPRESA",  vLstEmpresa
								putitem/id poParams,    "CD_EMPRESA",     vLstCdEmpresa
							endif
						else
							putitem/id vLstEmpresa, "CD_EMPRESA",     vLstCdEmpresa
							putitem/id vLstEmpresa, "CD_CENTROCUSTO", vLstCdGrupoEmpresa
							putitem/id poParams,    "LISTA_EMPRESA",  vLstEmpresa
							putitem/id poParams,    "CD_EMPRESA",     vLstCdEmpresa
							putitem/id poParams,    "CD_CENTROCUSTO", vLstCdGrupoEmpresa
							if (vLstCdEmpresa != "") & (vLstCdGrupoEmpresa != "")
								putitem/id poParams, "LST_EMPRESA", "%%vLstCdEmpresa·;%%vLstCdGrupoEmpresa"
							elseif (vLstCdEmpresa != "")
								putitem/id poParams, "LST_EMPRESA", vLstCdEmpresa
							elseif (vLstCdGrupoEmpresa != "")
								putitem/id poParams, "LST_EMPRESA", vLstCdGrupoEmpresa
							endif
						endif
					endif
				endif											;
				;25/04/2006-------------------------------------------------------------;
			endif
		endif
	endif
	
	return(0)
End ;operation validaEmpresa.

;---------------------------
public operation PoolEmpresa
;--------------------------------------------------------------
; EVJ - 20/02/2006                                            ;
; Funcao : Buscar Todas as Empresa que pertece ao Mesmo Pool  ;
;          Se for encargos, considera todas empresas          ;
;          informadas, caso seja principal somente filtra as  ;
;          empresas do principal.                             ;
;-------------------------------------------------------------;
;
	params
		string  piParams  :IN
		string  poParams  :OUT
		numeric poCdErro  :OUT
		string  poCtxErro :OUT
	endparams

	variables
		Numeric vCdPoolEmpresa
		string  vLstEmpresa,vLstGrupoEmpresa,vLstCdEmpresa,vLstCdGrupoEmpresa,vCdEmpresa,vDsLinha
		boolean vInCCusto,vInSomenteCC
	endvariables

	vCdPoolEmpresa = $item("CD_POOLEMPRESA",piParams)
	vCdEmpresa     = $item("CD_EMPRESA",piParams)
	vInSomenteCC   = $item("IN_CCUSTO",piParams)

	if (vCdEmpresa = "")
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Empresa não informada", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
	
	if (vCdPoolEmpresa = "")
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Pool empresa não informado", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

;	Teste se existe centro de custo
	clear/e "GER_EMPRESASVC"
	CD_EMPRESA.GER_EMPRESASVC/init = vCdEmpresa
	retrieve/e "GER_EMPRESASVC"
	if ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Empresa não existe", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	vInCCusto = <False>
	if (CD_CCUSTO.GER_EMPRESASVC != "")
		vInCCusto = <True>
	endif

	if (vInSomenteCC = <True>) & (vInCCusto = <False>)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Login de empresa não confere com Centro de Custo", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	clear/e "GER_POOLGRUPOSVC"
	CD_POOLEMPRESA.GER_POOLGRUPOSVC/init = vCdPoolEmpresa
	retrieve/e "GER_POOLGRUPOSVC"
	if ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Grupo de pooll empresa não existe", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
		
	sort/e "GER_POOLGRUPOSVC","CD_GRUPOEMPRESA.GER_POOLGRUPOSVC"

	setocc "GER_POOLGRUPOSVC", 1
	while ($status >= 0)
		clear/e "GER_EMPRESASVC"
		CD_GRUPOEMPRESA.GER_EMPRESASVC/init = CD_GRUPOEMPRESA.GER_POOLGRUPOSVC
		retrieve/e "GER_EMPRESASVC"
		if ($status >= 0)
			setocc "GER_EMPRESASVC", 1
			while ($status >= 0)
				if (CD_CCUSTO.GER_EMPRESASVC = "");Nao tem centro de custo
					if (vInSomenteCC != <True>) ;Listar somente com centro de Custo
						if (vLstCdEmpresa = "")
							vLstCdEmpresa = CD_EMPRESA.GER_EMPRESASVC
						else
							vLstCdEmpresa = "%%vLstCdEmpresa%%%·;%%CD_EMPRESA.GER_EMPRESASVC"
						endif
						vDsLinha = ""
						putitem/id vDsLinha,"CD_EMPRESA",CD_EMPRESA.GER_EMPRESASVC
						putitem vLstEmpresa, -1, vDsLinha
					endif
				else
					if (vInCCusto = <True>) ; Estou logado em uma empresa com centro de custo
						if (vLstCdEmpresa = "")
							vLstCdEmpresa = CD_EMPRESA.GER_EMPRESASVC
						else
							vLstCdEmpresa = "%%vLstCdEmpresa%%%·;%%CD_EMPRESA.GER_EMPRESASVC"
						endif
						vDsLinha = ""
						putitem/id vDsLinha,"CD_EMPRESA",CD_EMPRESA.GER_EMPRESASVC
						putitem vLstEmpresa, -1, vDsLinha
					endif
				endif
			
				setocc "GER_EMPRESASVC",$curocc(GER_EMPRESASVC)+1
			endwhile
			if (vLstCdGrupoEmpresa = "")
				vLstCdGrupoEmpresa = CD_GRUPOEMPRESA.GER_EMPRESASVC
			else
				vLstCdGrupoEmpresa = "%%vLstCdGrupoEmpresa%%%·;%%CD_GRUPOEMPRESA.GER_EMPRESASVC"
			endif

			vDsLinha = ""
			putitem/id vDsLinha,"CD_GRUPOEMPRESA",CD_GRUPOEMPRESA.GER_EMPRESASVC
			putitem vLstGrupoEmpresa, -1, vDsLinha

		endif
		setocc "GER_POOLGRUPOSVC",$curocc(GER_POOLGRUPOSVC)+1
	endwhile

	putitem/id poParams, "LS_CDEMPRESA", vLstCdEmpresa
	putitem/id poParams, "LS_CDGRUPOEMPRESA", vLstCdGrupoEmpresa
	putitem/id poParams, "LS_EMPRESA", vLstEmpresa
	putitem/id poParams, "LS_GRUPOEMPRESA", vLstGrupoEmpresa

	return (0)
end; PoolEmpresa


;--------------------------------------------------------------------;
public operation validaEmpConta                                      ;
;--------------------------------------------------------------------;
; MTF    : 02/03/2006                        Projeto 049, tarefa 013 ;
; Funcao : Valida as empresas de acordo com a conta banco informada. ;
; Chamada: FCRFP002 - consulta()                                     ;
;--------------------------------------------------------------------;
;
	params
		string  piGlobal  :IN
		string  piParams  :IN
		string  poParams  :OUT
		numeric poCdErro  :OUT
		string  poCtxErro :OUT
	endparams
	variables
		string  vpiParams, vpoParams, vLstCdEmpresa, vLstCdGrupoEmpresa
	endvariables
	
	if ($item("LST_EMPRESA", piParams) = "")
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Não há empresas informada para recebimento", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
	if ($item("CD_EMPRESA_CONTA", piParams) = "")
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Empresa da conta banco não informada.", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
	
	;Verifica se o usuário é Administrador
	clear/e "ADM_USUARIOSVC"
	cd_usuario.adm_usuariosvc/init = $item("CD_USUARIO", piGlobal)
	retrieve/e "ADM_USUARIOSVC"
	if ($status >= 0)      
		
		clear/e "GER_EMPRESASVC"
		cd_empresa.ger_empresasvc/init = $item("LST_EMPRESA", piParams)
		retrieve/e "GER_EMPRESASVC"
		if ($status >= 0)
			
			setocc "GER_EMPRESASVC",1
			repeat
				if (tp_privilegio.adm_usuariosvc = "1") | (tp_privilegio.adm_usuariosvc = "4")
					clear/e "adm_usucmpempsvc"
					cd_usuario.adm_usucmpempsvc/init    = $item("CD_USUARIO", piParams)
					cd_empresa.adm_usucmpempsvc/init    = cd_empresa.ger_empresasvc
					cd_componente.adm_usucmpempsvc/init = $item("CD_COMPONENTE", piParams)
					retrieve/e "adm_usucmpempsvc"
					if ($status >= 0)
						if (cd_ccusto.ger_empresasvc = "")
							putitem vLstCdEmpresa, -1, "%%cd_empresa.adm_usucmpempsvc"
						else
							putitem vLstCdGrupoEmpresa, -1, "%%cd_empresa.adm_usucmpempsvc"
						endif
					endif
				else
					if (cd_ccusto.ger_empresasvc = "")
						putitem vLstCdEmpresa, -1, "%%cd_empresa.ger_empresasvc"
					else
						putitem vLstCdGrupoEmpresa, -1, "%%cd_empresa.ger_empresasvc"
					endif			
				endif
				setocc "GER_EMPRESASVC",$curocc(GER_EMPRESASVC) + 1
			until($status < 0)
			$status = 0
		endif
	endif
	
	vpiParams = ""
	putitem/id vpiParams, "CD_EMPRESA", $item("CD_EMPRESA_CONTA", piParams)
	activate "SICSVCO009".validaCentroCusto(piGlobal, vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
	if ($procerror)
		$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	elseif ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	elseif (vpoParams != "")
		poParams    = ""
		if ($item("IN_MATRIZ", vpoParams) = <TRUE>) ;Empresa principal. Pegar empresas da lista que sao da principal, senao pega somente encargos.
			putitem/id poParams, "LST_EMPRESA", vLstCdEmpresa
			putitem/id poParams, "CD_EMPRESA",  vLstCdEmpresa
		else
			putitem/id poParams, "LST_EMPRESA",    vLstCdGrupoEmpresa
			putitem/id poParams, "CD_CENTROCUSTO", vLstCdGrupoEmpresa
		endif
	endif
	
	return(0)
End ;operation validaEmpConta.

;--------------------------
public operation LstEmpresa
;--------------------------
	params
		string	piParams:In
		string	poParams:Out
		numeric	poCdErro:Out
		string	poCtxErro:Out
	endParams

	variables
		Numeric vCdEmpresa
		string  vLstEmpresa,vLstGrupoEmpresa,vLstCdEmpresa,vLstCdGrupoEmpresa
		string  vDsLinha
		boolean vInCCusto,vInSomenteCC
	endvariables

	vCdEmpresa     = $item("CD_EMPRESA",piParams)
	vInSomenteCC   = $item("IN_CCUSTO",piParams)

	if (vCdEmpresa = "")
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Empresa não informada", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
	
;	Teste se existe centro de custo
	clear/e "GER_EMPRESASVC"
	CD_EMPRESA.GER_EMPRESASVC/init = vCdEmpresa
	retrieve/e "GER_EMPRESASVC"
	if ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Empresa não existe", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	vInCCusto = <False>
	if (CD_CCUSTO.GER_EMPRESASVC != "")
		vInCCusto = <True>
	endif

	if (vInSomenteCC = <True>) & (vInCCusto = <False>)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Login de empresa não confere com Centro de Custo", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	clear/e "GER_EMPRESASVC"
	retrieve/e "GER_EMPRESASVC"
	if ($status >= 0)
		setocc "GER_EMPRESASVC", 1
		while ($status >= 0)
			if (CD_CCUSTO.GER_EMPRESASVC = "");Nao tem centro de custo
				if (vInSomenteCC != <True>) ;Listar somente com centro de Custo
					if (vLstCdEmpresa = "")
						vLstCdEmpresa = CD_EMPRESA.GER_EMPRESASVC
					else
						vLstCdEmpresa = "%%vLstCdEmpresa%%%·;%%CD_EMPRESA.GER_EMPRESASVC"
					endif
					putitem/id vDsLinha,"CD_EMPRESA",CD_EMPRESA.GER_EMPRESASVC
					putitem vLstEmpresa, -1, vDsLinha
				endif
			else
				if (vInCCusto = <True>) ; Estou logado em uma empresa com centro de custo
					if (vLstCdEmpresa = "")
						vLstCdEmpresa = CD_EMPRESA.GER_EMPRESASVC
					else
						vLstCdEmpresa = "%%vLstCdEmpresa%%%·|%%CD_EMPRESA.GER_EMPRESASVC"
					endif
					putitem/id vDsLinha,"CD_EMPRESA",CD_EMPRESA.GER_EMPRESASVC
					putitem vLstEmpresa, -1, vDsLinha
				endif
			endif
			
			vDsLinha = ""
			if (vLstCdGrupoEmpresa = "")
				vLstCdGrupoEmpresa = CD_GRUPOEMPRESA.GER_EMPRESASVC
			else
				vLstCdGrupoEmpresa = "%%vLstCdGrupoEmpresa%%%·|%%CD_GRUPOEMPRESA.GER_EMPRESASVC"
			endif

			putitem/id vDsLinha,"CD_GRUPOEMPRESA",CD_GRUPOEMPRESA.GER_EMPRESASVC
			putitem vLstGrupoEmpresa, -1, vDsLinha
			setocc "GER_EMPRESASVC",$curocc(GER_EMPRESASVC)+1
		endwhile
	endif

	putitem/id poParams, "LS_CDEMPRESA", vLstCdEmpresa
	putitem/id poParams, "LS_CDGRUPOEMPRESA", vLstCdGrupoEmpresa
	putitem/id poParams, "LS_EMPRESA", vLstEmpresa
	putitem/id poParams, "LS_GRUPOEMPRESA", vLstGrupoEmpresa

	return (0)
	
end; LstEmpresa

;----------------------------------
public operation LstEmpresaOrdenada
;----------------------------------
	params
		string $xlpg$ : IN
		string piParams : IN
		string poParams : OUT
		numeric	poCdErro : OUT
		string poCtxErro : OUT
	endParams

	variables
		Numeric vCdEmpresa
		string vLstCdEmpresa, vLstCdGrupoEmpresa, vDsLinha, vNulo
		boolean vInCCusto, vInSomenteCC
	endvariables

	vCdEmpresa = $item("CD_EMPRESA",piParams)
	vInSomenteCC = $item("IN_CCUSTO",piParams)

	if (vCdEmpresa = "")
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Empresa não informada", "")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
	
;	Teste se existe centro de custo
	clear/e "GER_EMPRESASVC"
	CD_EMPRESA.GER_EMPRESASVC/init = vCdEmpresa
	retrieve/e "GER_EMPRESASVC"
	if ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Empresa não existe", "")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	vInCCusto = <False>
	if (CD_CCUSTO.GER_EMPRESASVC != "")
		vInCCusto = <True>
	endif

	if (vInSomenteCC = <True>) & (vInCCusto = <False>)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Login de empresa não confere com Centro de Custo", "")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
	
	vNulo = ""
	
	clear/e "GER_EMPRESASVC"
	if (vInCCusto = <TRUE>)
		CD_CCUSTO.GER_EMPRESASVC/init = "·!·=%%vNulo%%%"
	else
		CD_CCUSTO.GER_EMPRESASVC/init = "·=%%vNulo%%%"
	endif
	retrieve/e "GER_EMPRESASVC"
	if ($status >= 0)
		if (vInCCusto = <TRUE>)
			sort/e "GER_EMPRESASVC", "CD_CCUSTO.GER_EMPRESASVC"
		else
			sort/e "GER_EMPRESASVC", "CD_EMPRESA.GER_EMPRESASVC"
		endif
		
		setocc "GER_EMPRESASVC", 1
		while ($status >= 0)
			if (vInCCusto = <TRUE>)
				if (vInSomenteCC != <TRUE>)
					if (vLstCdEmpresa = "")
						vLstCdEmpresa = CD_CCUSTO.GER_EMPRESASVC
					else
						vLstCdEmpresa = "%%vLstCdEmpresa%%%·;%%CD_CCUSTO.GER_EMPRESASVC"
					endif
				endif
				
				if (vLstCdEmpresa = "")
					vLstCdEmpresa = CD_EMPRESA.GER_EMPRESASVC
				else
					vLstCdEmpresa = "%%vLstCdEmpresa%%%·;%%CD_EMPRESA.GER_EMPRESASVC"
				endif
			else
				if (vLstCdEmpresa = "")
					vLstCdEmpresa = CD_EMPRESA.GER_EMPRESASVC
				else
					vLstCdEmpresa = "%%vLstCdEmpresa%%%·;%%CD_EMPRESA.GER_EMPRESASVC"
				endif
			endif
			
			vDsLinha = ""
			if (vLstCdGrupoEmpresa = "")
				vLstCdGrupoEmpresa = CD_GRUPOEMPRESA.GER_EMPRESASVC
			else
				vLstCdGrupoEmpresa = "%%vLstCdGrupoEmpresa%%%·|%%CD_GRUPOEMPRESA.GER_EMPRESASVC"
			endif
			
			setocc "GER_EMPRESASVC",$curocc(GER_EMPRESASVC)+1
		endwhile
	endif

	putitem/id poParams, "LS_CDEMPRESA", vLstCdEmpresa
	putitem/id poParams, "LS_CDGRUPOEMPRESA", vLstCdGrupoEmpresa
	putitem/id poParams, "IN_CCUSTO", vInCCusto
	
	return (0)
	
end; LstEmpresa

;-------------------------------------------------------------;
public operation validaMovimentoPool                          ;
;-------------------------------------------------------------;
; MODA - 19/02/07                                             ;
; Funcao : Verifica o parametro  CD_EMP_BAIXA_SALDO_PED       ;
;          Atualiza o estoque do produto nessa empresa de     ;
;          e deixa o saldo zerado do prodto na empresa        ;
;          corrente.                                          ;
; Chamada: GERSVCO008 - atualizaSaldoOperacao                 ;
;                                                             ;
;-------------------------------------------------------------;
;
	params
		string  piGlobal  :IN
		string  piParams  :IN
		string  viValores  :IN
		string  poParams  :OUT
		numeric poCdErro  :OUT
		string  poCtxErro :OUT
	endparams

	variables
		string viParams, voParams
		numeric vCdEmpBase, vCdGrupoEmpBase, vCdPoolEmpBase, vCdEmpCCusto
		numeric vCdGrupoEmpMatriz, vCdEmpMatCCusto
	endvariables

	vCdEmpBase = $item("CD_EMPRESA", piParams)
	if(vCdEmpBase = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO= %%vCdEmpBase empresa base não informada!", "ADICIONAL=Operação->SICSVCO004.validaMovimentoPool")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1) 
	endif

	clear/e "GER_EMPRESASVC"
	CD_EMPRESA.GER_EMPRESASVC/init = vCdEmpBase;empresa onde baixou a transacao de pedido
	retrieve/e "GER_EMPRESASVC"
	if($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO= %%vCdEmpBase empresa base não cadastrada!", "ADICIONAL=Operação->SICSVCO004.validaMovimentoPool")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1) 
	endif
	vCdGrupoEmpBase = CD_GRUPOEMPRESA.GER_EMPRESASVC;grupo empresa onde baixou a transacao de pedido

	clear/e "GER_EMPRESASVC"
	CD_GRUPOEMPRESA.GER_EMPRESASVC/init = vCdGrupoEmpBase
	retrieve/e "GER_EMPRESASVC"
	if($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO= %%vCdGrupoEmpBase grupo empresa não inválido!", "ADICIONAL=Operação->SICSVCO004.validaMovimentoPool")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1) 
	endif		
	
	setocc "GER_EMPRESASVC", -1 ;buscar empresa encargos de onde baixou o pedido
	setocc "GER_EMPRESASVC", 1 	
	while($status >= 0)
		if(CD_CCUSTO.GER_EMPRESASVC > 0)
			vCdEmpCCusto = CD_EMPRESA.GER_EMPRESASVC
		endif
		setocc "GER_EMPRESASVC", $curocc("GER_EMPRESASVC") + 1 
	endwhile

	clear/e "GER_POOLGRUPOSVC"
	CD_GRUPOEMPRESA.GER_POOLGRUPOSVC/init = vCdGrupoEmpBase ;verificar qual o pool esta a empresa onde foi baixado o pedido
	retrieve/e "GER_POOLGRUPOSVC"
	if($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO= %%vCdGrupoEmpBase grupo empresa não cadastrada no pool!", "ADICIONAL=Operação->SICSVCO004.validaMovimentoPool")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1) 
	endif
	vCdPoolEmpBase = CD_POOLEMPRESA.GER_POOLGRUPOSVC
	
	clear/e "GER_POOLEMPRESVC"
	CD_POOLEMPRESA.GER_POOLEMPRESVC/init = vCdPoolEmpBase ;busca a empresa matriz do pool
	retrieve/e "GER_POOLEMPRESVC"
	if($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO= %%vCdPoolEmpBase pool não cadastrado!", "ADICIONAL=Operação->SICSVCO004.validaMovimentoPool")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1) 
	endif
	vCdGrupoEmpMatriz = CD_GRUPOEMPMATRIZ.GER_POOLEMPRESVC
	if(vCdGrupoEmpMatriz = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO= Grupo empresa matriz não cadastrado!", "ADICIONAL=Operação->SICSVCO004.validaMovimentoPool")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1) 
	endif

	clear/e "GER_EMPRESASVC"
	CD_GRUPOEMPRESA.GER_EMPRESASVC/init = vCdGrupoEmpMatriz; busca a empresa encargos do grupoempresa matriz
	retrieve/e "GER_EMPRESASVC"
	if($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=NF %%vCdGrupoEmpBase grupo empresa não inválido!", "ADICIONAL=Operação->SICSVCO004.validaMovimentoPool")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1) 
	endif		
	setocc "GER_EMPRESASVC", -1 
	setocc "GER_EMPRESASVC", 1 	
	while($status >= 0)
		if(CD_CCUSTO.GER_EMPRESASVC > 0)
			vCdEmpMatCCusto = CD_EMPRESA.GER_EMPRESASVC
		endif
		setocc "GER_EMPRESASVC", $curocc("GER_EMPRESASVC") + 1 
	endwhile

	if(vCdGrupoEmpMatriz != vCdGrupoEmpBase);se o pedido estiver sendo baixado no grupo empresa matriz nao faz nada
		viParams = ""
		viParams = piParams
		putitem/id viParams, "CD_EMPRESA", vCdEmpCCusto ; estorna estoque da empresa encargos que baixou o pedido
		putitem/id viParams, "IN_ESTORNO", <TRUE>
		activate "PRDSVCO006".atualizaSaldo(piGlobal, viParams, viValores, voParams, $xCdErro$, $xCtxErro$)
		if ($procerror)
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		elseif ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		endif
		viParams = ""
		viParams = piParams
		putitem/id viParams, "CD_EMPRESA", vCdEmpMatCCusto ; Lanca estoque na encargos da matriz
		activate "PRDSVCO006".atualizaSaldo(piGlobal, viParams, viValores, voParams, $xCdErro$, $xCtxErro$)
		if ($procerror)
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		elseif ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		endif
	endif

	return(0)
End ;operation validaMovimentoPool
