entry getParam
	params
		numeric piCdEmpresa:IN
	endparams
	
	variables
		string viParams, voParams, vTpQuebraCFOP, vInOptSimples
	endvariables

	if (piCdEmpresa = 0)
		piCdEmpresa = $item("CD_EMPRESA", $xlpg$)
	endif

	viParams = ""
	putitem viParams, -1, "CD_EMPVALOR"
	putitem viParams, -1, "CD_MOTIVO_ALTVALOR_CMP"
	activate "ADMSVCO001".GetLstParametro(viParams, voParams, $xCdErro$, $xCtxerro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($xCdErro$)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=%%$item("DESCRICAO", $xCtxErro$)", "ADICIONAL=Operação->FISSVCI004")
		return(-1)
	elseif ($status < 0)
		return(-1)
	endif
	
	$cdEmpresaValorSis$ = $item("CD_EMPVALOR", voParams)
	$cdMotivoAltValor$ = $item("CD_MOTIVO_ALTVALOR_CMP", voParams)

	viParams = ""
	putitem viParams, -1, "CD_EMPRESA_VALOR"
	putitem viParams, -1, "CD_SALDOPADRAO"
	putitem viParams, -1, "CD_SALDO_CALC_VLR_MEDIO"
	putitem viParams, -1, "IN_PDV_OTIMIZADO"
	putitem viParams, -1, "CD_CUSTO_S_IMPOSTO"
	putitem viParams, -1, "CD_CUSTO_MEDIO_S_IMPOSTO"
	putitem viParams, -1, "CD_OPER_ENT_EST_TRANS"
	putitem viParams, -1, "CD_OPER_ENT_INSPECAO" 
	putitem viParams, -1, "DS_LST_OPER_EST_TERCEIRO" 
	putitem viParams, -1, "DS_CUSTO_SUBST_TRIBUTARIA" 
	putitem viParams, -1, "DS_CUSTO_VALOR_RETIDO"
	putitem viParams, -1, "CD_SALDO_EST_TERCEIRO"
	putitem viParams, -1, "CD_SALDO_EST_DE_TERC" 
	putitem viParams, -1, "DS_LST_OPER_EST_DE_TERC"
	putitem viParams, -1, "IN_RATEIO_CUSTO_OP" ;---Elia Proj. 148/225 15/07/09
	putitem viParams, -1, "TP_VALIDA_CUSTO_MEDIO" ;>-- MAC - PRJ 102 TAR 769 - 07/01/2010
	;putitem viParams, -1, "TP_CONTR_INSP_SALDO_LOTE";--->MNT - Prj 130/1227 - 30/08/2010
	putitem viParams, -1, "TIN_TINTURARIA" ;-- ALX - 156/451 - 26/10/2010 --;
	putitem viParams, -1, "TP_SIT_LOTEC_DUP_TRANSF" ;Hugo em 10/03/2011 Tarefa 156-536
	putitem viParams, -1, "TP_SIT_LOTEI_DUP_TRANSF" ;Hugo em 10/03/2011 Tarefa 156-536
	activate "ADMSVCO001".GetParamEmpresa(piCdEmpresa, viParams, voParams, $xCdErro$, $xCtxerro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($xCdErro$)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=%%$item("DESCRICAO", $xCtxErro$)", "ADICIONAL=Operação->FISSVCO004")
		return(-1)
	elseif ($status < 0)
		return(-1)
	endif

	;$tpContrInspSaldoLote$ = $item("TP_CONTR_INSP_SALDO_LOTE", voParams);--->MNT - Prj 130/1227 - 30/08/2010
	$cdEmpresaValorEmp$ = $item("CD_EMPRESA_VALOR", voParams)
	$cdSaldoPadrao$ = $item("CD_SALDOPADRAO", voParams)
	$cdSaldoCalcVlrMedio$ = $item("CD_SALDO_CALC_VLR_MEDIO", voParams)
	$inPDVOtimizado$ = $item("IN_PDV_OTIMIZADO", voParams)
	$cdCustoSemImp$ = $item("CD_CUSTO_S_IMPOSTO", voParams)
	$cdCustoMedioSemImp$ = $item("CD_CUSTO_MEDIO_S_IMPOSTO", voParams)
	$cdOperEntEstTrans$ = $item("CD_OPER_ENT_EST_TRANS", voParams)
	$cdOperEntInspecao$ = $item("CD_OPER_ENT_INSPECAO", voParams) 
	$dsLstOperEstTerceiro$	= $item("DS_LST_OPER_EST_TERCEIRO", voParams)
	$dsLstOperEstTerceiro$ = $replace($dsLstOperEstTerceiro$, 1, ";", "·|", -1)
	$dsCustoSubstTributaria$ = $item("DS_CUSTO_SUBST_TRIBUTARIA", voParams)
	$dsCustoValorRetido$ = $item("DS_CUSTO_VALOR_RETIDO", voParams)
	$cdSaldoEstTerceiro$ = $item("CD_SALDO_EST_TERCEIRO", voParams)
	$cdSaldoEstDeTerc$ = $item("CD_SALDO_EST_DE_TERC", voParams)
	$dsLstOperEstDeTerc$ = $item("DS_LST_OPER_EST_DE_TERC", voParams)
	$dsLstOperEstDeTerc$ = $replace($dsLstOperEstDeTerc$, 1, ";", "·|", -1)
	$inRateioCustoOp$ = $item("IN_RATEIO_CUSTO_OP", voParams) ;---Elia Proj. 148/225 15/07/09
	$tpValidaCustoMedio$ = $item("TP_VALIDA_CUSTO_MEDIO", voParams) ;>-- MAC - PRJ 102 TAR 769 - 07/01/2010
	$tinTinturaria$ = $item("TIN_TINTURARIA", voParams) ;-- ALX - 156/451 - 26/10/2010 --;
	$tpSitLoteCTransf$ = $item("TP_SIT_LOTEC_DUP_TRANSF", voParams) ;Hugo em 10/03/2011 Tarefa 156-536
	$tpSitLoteITransf$ = $item("TP_SIT_LOTEC_DUP_TRANSF", voParams) ;Hugo em 10/03/2011 Tarefa 156-536
	
	return(0)

End ; entry getParam

;---------------------------------
public operation atualizaEstoqueNF
;---------------------------------
	params
		string  $xlpg$    :IN    
		string  $xlpi$    :IN
		string  poParams  :OUT
		string  poCdErro  :OUT
		string  poCtxErro :OUT
	endparams
	
	variables
		string viParams, voParams, viListas, vDsRegistro, vDsLstNF, vCdcomponente, vDsLstNFAux, vDsLstValor, vDsRegistroNF
		numeric vCdEmpresa, vNrFatura, vVlCalc, vVlUnitario, vVlIPI, vQtFaturado, vCdEmpresaBase, vCdOperacaoBase
		numeric vVlProdutoAtu, vVlAtual, vQtSaldo, vQtProdutoAtu, vTpLote, vNrMesCorrente, vCdOperSaldo
		numeric vNrMesNF, vNrAnoNF, vNrAnoCorrente, vTpInspecao, vCdCST, vQtProcessado, vTpContrInspSaldoLote
		date vDtFatura, vDtNf, vDtCorrente, vDtSistema, vDtKardex
		boolean vInEstorno, vInTransferencia, vInValidaPool, vInEstoqueTerceiro, vInKardex, vInOperRec, vInMatriz
		boolean vInEstoqueDeTerceiro, vInSoTerceiro, vInRemessaRetornoMP, vInProdAcabado, vInProdPropria, vInAchouLote
		boolean vInSemInspecao, vInAchou, vInInspecao
	endvariables

	vDsLstNF = $item("DS_LSTNF", $xlpi$)
	vInEstorno = $item("IN_ESTORNO", $xlpi$)
	vCdComponente = $item("CD_COMPONENTE", $xlpi$)
	vCdEmpresaBase = $item("CD_EMPRESABASE", $xlpi$)
	vCdOperacaoBase = $item("CD_OPERACAOBASE", $xlpi$)
	vInTransferencia = $item("IN_TRANSFERENCIA", $xlpi$)
	vInSoTerceiro = $item("IN_SOTERCEIRO", $xlpi$)
	vDtSistema = $item("DT_SISTEMA", $xlpg$)
	vInSemInspecao = $item("IN_SEMINSPECAO", $xlpi$) ;* Claudemir - Prj/Tarefa: 156/158 - 28/01/2010
	vTpContrInspSaldoLote = $item("TP_CONTR_INSP_SALDO_LOTE", $$gParamGlb) ;-- ALX - 130/1328 - 22/11/2010 --;

	vDtKardex =  $item("DT_KARDEX", $xlpi$) ;>-- MAC - PRJ 182 / 192 - 21/11/2011 

	if(vCdOperacaoBase > 0) ;flag para verificar se recebeu uma operacao base. oda
		vInOperRec = <TRUE>
	endif

	if (vDsLstNF = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Lista de NF não informada!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1)
	endif

	call getParam(0)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1)
	endif

	if ($status < 0)
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		exit(-1)
	endif

	;---Elia Proj. 130/26 25/07/08
	clear/e "TMP_NR09SVC"
	if ($dsLstOperEstTerceiro$ != "")
		clear/e "GER_OPERACAOSVC"
		CD_OPERACAO.GER_OPERACAOSVC/init = $dsLstOperEstTerceiro$
		retrieve/e "GER_OPERACAOSVC"
		if ($status >= 0)
			setocc "GER_OPERACAOSVC", 1
			while ($status >= 0)
				creocc "TMP_NR09SVC", -1
				NR_GERAL.TMP_NR09SVC = CD_OPERACAO.GER_OPERACAOSVC
				retrieve/o "TMP_NR09SVC"
				setocc "GER_OPERACAOSVC", $curocc("GER_OPERACAOSVC") + 1 
			endwhile
		endif
	endif
	;

	;---Elia Proj. 130/155 10/11/08
	clear/e "F_TMP_NR09SVC"
	if ($dsLstOperEstDeTerc$ != "")
		clear/e "GER_OPERACAOSVC"
		CD_OPERACAO.GER_OPERACAOSVC/init = $dsLstOperEstDeTerc$
		retrieve/e "GER_OPERACAOSVC"
		if ($status >= 0)
			setocc "GER_OPERACAOSVC", 1
			while ($status >= 0)
				creocc "F_TMP_NR09SVC", -1
				NR_GERAL.F_TMP_NR09SVC = CD_OPERACAO.GER_OPERACAOSVC
				retrieve/o "F_TMP_NR09SVC"
				setocc "GER_OPERACAOSVC", $curocc("GER_OPERACAOSVC") + 1 
			endwhile
		endif
	endif
	;
	
	repeat
		getitem vDsRegistro, vDsLstNF, 1
		vCdEmpresa = $item("CD_EMPRESA", vDsRegistro)
		vNrFatura = $item("NR_FATURA", vDsRegistro)
		vDtFatura = $item("DT_FATURA", vDsRegistro)

        vDsRegistroNF = vDsRegistro ;---Elia 29/04/10 
		
		if (vCdEmpresa = 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Empresa não informada!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		endif
		
		if (vNrFatura = 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Fatura não informado!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1) 
		endif
		
		if (vDtFatura = "")
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Data NF não informada!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		endif

		;---Elia Proj. 148/225 15/07/09
		if ($inRateioCustoOp$ = <TRUE>)
			vNrMesNF = vDtFatura[M]
			vNrAnoNF = vDtFatura[Y]
			vNrMesCorrente = vDtSistema[M]
			vNrAnoCorrente = vDtSistema[Y]
			vDtNf = "01/%%vNrMesNF%%%/%%vNrAnoNF"
			vDtCorrente = "01/%%vNrMesCorrente%%%/%%vNrAnoCorrente"

			if (vDtNf != vDtCorrente)
				$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Permitido atualizar estoque da NF apenas no mês corrente!%%^Parâmetro empresa IN_RATEIO_CUSTO_OP configurado!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
				poCdErro = $xCdErro$
				poCtxErro = $xCtxErro$
				exit (-1)	
			endif
		endif
		;
		
		clear/e "FIS_NFSVC"
		CD_EMPRESA.FIS_NFSVC/init = vCdEmpresa
		NR_FATURA.FIS_NFSVC/init = vNrFatura
		DT_FATURA.FIS_NFSVC/init = vDtFatura
		retrieve/e "FIS_NFSVC"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=NF %%vNrFatura não cadastrada!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1) 
		endif

		viParams = ""
		putitem/id viParams, "CD_EMPRESA", CD_EMPFAT.FIS_NFSVC
		activate "SICSVCO009".validaCentroCusto($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
		if ($procerror)       
			$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		elseif ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		endif
		vInMatriz = $item("IN_MATRIZ", voParams)
		if (vInMatriz = "")
			vInMatriz = <TRUE>
		endif

; amfrederico 25/10/2010
; Não pode ser utilizada uma empresa passada como parâmetro como base para laçamento de kadex porque ele trabalha com uma lista de NF
; e nesta pode haver empresas diferentes, como as de centro de custo
;		if (vCdEmpresaBase = 0)
			vCdEmpresaBase = CD_EMPFAT.FIS_NFSVC
;		endif

		if (vCdOperacaoBase = 0 | vInOperRec != <TRUE>)
			vCdOperacaoBase = CD_OPERACAO.FIS_NFSVC
		endif

		clear/e "GER_OPERACAOSVC"
		CD_OPERACAO.GER_OPERACAOSVC/init = vCdOperacaoBase
		retrieve/e "GER_OPERACAOSVC"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Operaçao %%CD_OPERACAO.GER_OPERACAOSVC não cadastrada!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		else
			;Hugo em 10/02/11 Tarefa 130-1418
			clear/e "GER_OPERSALDOSVC"
			CD_OPERACAO.GER_OPERSALDOSVC/init = CD_OPERACAO.GER_OPERACAOSVC
			IN_PADRAO.GER_OPERSALDOSVC/init = <TRUE>
			retrieve/e "GER_OPERSALDOSVC"
			if ($status >= 0)
				vCdOperSaldo = CD_SALDO.GER_OPERSALDOSVC
			endif
			;
			
			;---------------------------DBB |PRJ 130/0349| 02/03/09
			;if ((vInEstorno) & (IN_KARDEX.GER_OPERACAOSVC = <TRUE>))
			if (vInEstorno) & (IN_KARDEX.GER_OPERACAOSVC = <TRUE>) & (TP_OPERACAO.GER_OPERACAOSVC = "E") ;<ANO Pjt156 Trf192 - 18/02/2010>
				viParams = ""
				putitem/id viParams, "CD_EMPRESA", CD_EMPRESA.FIS_NFSVC
				putitem/id viParams, "NR_FATURA", NR_FATURA.FIS_NFSVC
				putitem/id viParams, "DT_FATURA", DT_FATURA.FIS_NFSVC
				activate "FISSVCO014".validaInspecaoNF($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
				if ($procerror)       
					$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
					poCdErro = $xCdErro$
					poCtxErro = $xCtxErro$
					exit(-1)
				elseif ($status < 0)
					$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
					poCdErro = $xCdErro$
					poCtxErro = $xCtxErro$
					exit(-1)
				endif
			endif	
			;-----------------------------------------------------
		endif

		vInEstoqueTerceiro = <FALSE>
		vInEstoqueDeTerceiro = <FALSE>

		if (!$empty("TMP_NR09SVC"))
			creocc "TMP_NR09SVC", -1
			NR_GERAL.TMP_NR09SVC = CD_OPERACAO.GER_OPERACAOSVC
			retrieve/o "TMP_NR09SVC"
			if ($status = 4)
				vInEstoqueTerceiro = <TRUE>
			else
				discard "TMP_NR09SVC"
			endif
		endif

		if (TP_MODALIDADE.GER_OPERACAOSVC = "F") ; F - Remessa/Retorno avulso
			vInEstoqueTerceiro = <TRUE>
		endif
		;

		if (vInEstoqueTerceiro = <FALSE>)
			if (!$empty("F_TMP_NR09SVC"))
				creocc "F_TMP_NR09SVC", -1
				NR_GERAL.F_TMP_NR09SVC = CD_OPERACAO.GER_OPERACAOSVC
				retrieve/o "F_TMP_NR09SVC"
				if ($status = 4)
					vInEstoqueDeTerceiro = <TRUE>
				else
					discard "F_TMP_NR09SVC"
				endif
			endif
		endif
		
		vDsLstValor = ""

		if ($empty("FIS_NFITEMSVC") = 0)
			setocc "FIS_NFITEMSVC",-1 ;<ANO Pjt130 Trf1087 - 18/05/2010>
			setocc "FIS_NFITEMSVC", 1
			while ($status >=0)
				;<ANO Pjt130 Trf1087 - 18/05/2010>
				$instancehandle->setDisplay("Itens processados - %%$curocc("FIS_NFITEMSVC")%%%/%%$totocc("FIS_NFITEMSVC")", "", "") 
				if ($procerror)
					$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")		
				endif
				;</ANO Pjt130 Trf1087 - 18/05/2010>

				if ($empty("FIS_NFITEMPROSVC") = 0)
					setocc "FIS_NFITEMPROSVC", 1
					while ($status >=0)
						vDsLstValor = "";oda teste de performace

						if (IN_KARDEX.GER_OPERACAOSVC = <TRUE>)
							if ($empty("FIS_NFITEMUNSVC") = 0) & (QT_CONVERSAO.FIS_NFITEMUNSVC <= 0)
								$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Produto %%CD_PRODUTO.FIS_NFITEMPROSVC da NF %%NR_FATURA.FIS_NFITEMPROSVC possui quantidade de conversão inválida!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
								poCdErro = $xCdErro$
								poCtxErro = $xCtxErro$
								exit(-1) 
							endif
						endif		

						if ($empty("FIS_NFITEMVLSVC") = 0) & (IN_KARDEX.GER_OPERACAOSVC = <TRUE>)
							setocc "FIS_NFITEMVLSVC", 1
							while ($status >=0)
								vInAchou = <FALSE>
								
								if (TP_OPERACAO.FIS_NFSVC = "E") | (TP_MODALIDADE.GER_OPERACAOSVC = 3 & TP_OPERACAO.GER_OPERACAOSVC = "S")
									if (TP_ATUALIZACAO.FIS_NFITEMVLSVC = 1 | TP_ATUALIZACAO.FIS_NFITEMVLSVC = 2) ;Último valor/Valor médio
										viParams = ""
										putitem/id viParams, "CD_EMPRESA", CD_EMPRESA.FIS_NFITEMVLSVC
										putitem/id viParams, "NR_FATURA", NR_FATURA.FIS_NFITEMVLSVC
										putitem/id viParams, "DT_FATURA", DT_FATURA.FIS_NFITEMVLSVC
										putitem/id viParams, "NR_ITEM", NR_ITEM.FIS_NFITEMVLSVC
										putitem/id viParams, "CD_PRODUTO", CD_PRODUTO.FIS_NFITEMPROSVC
										putitem/id viParams, "TP_VALOR", TP_VALOR.FIS_NFITEMVLSVC
										putitem/id viParams, "CD_VALOR", CD_VALOR.FIS_NFITEMVLSVC
										putitem/id viParams, "VL_PRODUTOATU", vVlCalc
										putitem/id viParams, "CD_MOTIVO", $cdMotivoAltValor$
										putitem/id viParams, "CD_CUSTO_S_IMPOSTO", $cdCustoSemImp$
										putitem/id viParams, "CD_CUSTO_MEDIO_S_IMPOSTO", $cdCustoMedioSemImp$
										putitem/id viParams, "IN_PDV_OTIMIZADO", $inPDVOtimizado$
										;---Elia Proj. 102/357 09/10/08
										putitem/id viParams, "DS_CUSTO_SUBST_TRIBUTARIA", $dsCustoSubstTributaria$
										putitem/id viParams, "DS_CUSTO_VALOR_RETIDO", $dsCustoValorRetido$
										;
										activate "SICSVCO005".arredondaCusto($xlpg$, viParams, voParams, $xCdErro$, $xCtxerro$)
										if ($procerror)       
											$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
											poCdErro = $xCdErro$
											poCtxErro = $xCtxErro$
											exit(-1)
										elseif ($status < 0)
											$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
											poCdErro = $xCdErro$
											poCtxErro = $xCtxErro$
											exit(-1)
										endif
										vVlUnitario = $item("VL_UNITARIO", voParams)				
										vVlIPI = $item("VL_IPI", voParams)
										vQtFaturado = $item("QT_FATURADO", voParams)
	
										vVlCalc = vVlUnitario - (vVlUnitario * PR_DESCONTO.FIS_NFITEMVLSVC / 100) 
										vVlCalc = vVlCalc[round,6]
										vVlCalc = vVlCalc - (vVlCalc * PR_DESCONTOCAB.FIS_NFITEMVLSVC / 100)
										vVlCalc = vVlCalc[round,6]
										
										;---Elia Proj. 148/318 14/08/09									
										vCdCST = CD_CST.FIS_NFITEMSVC[1 : 1]	
										if (vCdCST = 1) ; 1 - Importacao direta
											vVlProdutoAtu = vVlCalc		
										else
											vVlProdutoAtu = vVlCalc + vVlIPI
										endif
										;
										
										if (TP_ATUALIZACAO.FIS_NFITEMVLSVC = 2) ;Valor médio
											viParams = ""
											putitem/id viParams, "CD_EMPRESA", vCdEmpresaBase
											putitem/id viParams, "CD_PRODUTO", CD_PRODUTO.FIS_NFITEMPROSVC
											putitem/id viParams, "TP_VALOR", TP_VALOR.FIS_NFITEMVLSVC
											putitem/id viParams, "CD_VALOR", CD_VALOR.FIS_NFITEMVLSVC
											putitem/id viParams, "CD_EMPRESA_VALOR", $cdEmpresaValorEmp$
											putitem/id viParams, "CD_EMPVALOR", $cdEmpresaValorSis$
											putitem/id viParams, "IN_SOEMPRESA", <TRUE>
											viListas = ""    
											activate "PRDSVCO007".buscaValorData($xlpg$, viParams, viListas, voParams, $xCdErro$, $xCtxErro$)
											if ($procerror)
												$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
												poCdErro = $xCdErro$
												poCtxErro = $xCtxErro$
												exit(-1)
											elseif ($xCdErro$)
												$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=%%$item("DESCRICAO", $xCtxErro$)", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
												poCdErro = $xCdErro$
												poCtxErro = $xCtxErro$
												exit(-1)
											endif
											
											vVlAtual = $item("VL_VALOR", voParams)
											viParams = ""
											putitem/id viParams, "CD_GRUPOEMPRESA", CD_GRUPOEMPRESA.FIS_NFITEMPROSVC
											putitem/id viParams, "CD_PRODUTO", CD_PRODUTO.FIS_NFITEMPROSVC
											if ($cdSaldoCalcVlrMedio$ > 0)
												putitem/id viParams, "CD_SALDO", $cdSaldoCalcVlrMedio$
											else
												;>-- MAC - PRJ 101 TAR 511 - 19/01/2010
												;putitem/id viParams, "CD_SALDO", $cdSaldoPadrao$
												putitem/id viParams, "IN_SALDOCONSOLIDADO", <TRUE>
												;--<
											endif
											putitem/id viParams, "IN_VERIFICAPRODUTOPADRAO", <TRUE>
											viListas = ""    
											activate "PRDSVCO006".buscaSaldoData($xlpg$, viParams, viListas, voParams, $xCdErro$, $xCtxErro$)
											if ($procerror)
												$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
												poCdErro = $xCdErro$
												poCtxErro = $xCtxErro$
												exit(-1)
											elseif ($xCdErro$)
												$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=%%$item("DESCRICAO", $xCtxErro$)", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
												poCdErro = $xCdErro$
												poCtxErro = $xCtxErro$
												exit(-1)
											endif
											vQtSaldo = $item("QT_SALDO", voParams)
											;>-- MAC - PRJ 102 TAR 769 - 07/01/2010										
											if ($tpValidaCustoMedio$ = 1) & (vQtSaldo != 0) & (vVlAtual = 0)
												$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=PRODUTO %%CD_PRODUTO.FIS_NFITEMPROSVC com saldo em estoque e sem custo médio %%CD_VALOR.FIS_NFITEMVLSVC!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
												poCdErro = $xCdErro$
												poCtxErro = $xCtxErro$
												exit(-5)
											endif  
											;--<
											
											if ($empty("FIS_NFITEMUNSVC") = 0)
												if (TP_OPERACAO.FIS_NFITEMUNSVC = "M")
													vVlProdutoAtu = vVlProdutoAtu / QT_CONVERSAO.FIS_NFITEMUNSVC
													vQtFaturado = vQtFaturado * QT_CONVERSAO.FIS_NFITEMUNSVC
													vQtFaturado = vQtFaturado[round,3] ;-- ALX - 182/18 - 26/01/2011 --;
												else
													vVlProdutoAtu = vVlProdutoAtu * QT_CONVERSAO.FIS_NFITEMUNSVC
													vQtFaturado = vQtFaturado / QT_CONVERSAO.FIS_NFITEMUNSVC
													vQtFaturado = vQtFaturado[round,3] ;-- ALX - 182/18 - 26/01/2011 --;
												endif
											endif
											if (vInMatriz = <FALSE>)
												vQtSaldo = vQtSaldo - vQtFaturado
											endif
											if (vQtSaldo < 0)
												vQtSaldo = 0
											endif
											if (vInEstorno = <TRUE> & (TP_OPERACAO.FIS_NFSVC = "E")) | (TP_OPERACAO.FIS_NFSVC = "S")
												if ((vQtSaldo - vQtFaturado) = 0)
													vVlCalc = 0
												else
													vVlCalc = ((vQtSaldo * vVlAtual) - (vVlProdutoAtu * vQtFaturado)) / (vQtSaldo - vQtFaturado)
												endif
											else
												vVlCalc = ((vVlProdutoAtu * vQtFaturado) + (vQtSaldo * vVlAtual)) / (vQtFaturado + vQtSaldo)
											endif
											vVlProdutoAtu = vVlCalc[round, 6]
										else
											if ($empty("FIS_NFITEMUNSVC") = 0)
												if (TP_OPERACAO.FIS_NFITEMUNSVC = "M")
													vVlProdutoAtu = vVlProdutoAtu / QT_CONVERSAO.FIS_NFITEMUNSVC
												else
													vVlProdutoAtu = vVlProdutoAtu * QT_CONVERSAO.FIS_NFITEMUNSVC
												endif
											endif
										endif									
										
										if (TP_ATUALIZACAO.FIS_NFITEMVLSVC = 1) & ((vInEstorno = <TRUE> & (TP_OPERACAO.FIS_NFSVC = "E")) | (TP_OPERACAO.FIS_NFSVC = "S"))
										else
											viParams = ""
											putitem/id viParams, "CD_EMPRESA", vCdEmpresaBase
											putitem/id viParams, "CD_PRODUTO", CD_PRODUTO.FIS_NFITEMPROSVC
											putitem/id viParams, "TP_VALOR", TP_VALOR.FIS_NFITEMVLSVC
											putitem/id viParams, "CD_VALOR", CD_VALOR.FIS_NFITEMVLSVC
											putitem/id viParams, "VL_PRODUTOATU", vVlProdutoAtu
											putitem/id viParams, "CD_MOTIVO", $cdMotivoAltValor$
											activate "PRDSVCO007".atualizaValor($xlpg$, viParams, voParams, $xCdErro$, $xCtxerro$)
											if ($procerror)       
												$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
												poCdErro = $xCdErro$
												poCtxErro = $xCtxErro$
												exit(-1)
											elseif ($xCdErro$)
												$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=%%$item("DESCRICAO", $xCtxErro$)", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
												poCdErro = $xCdErro$
												poCtxErro = $xCtxErro$
												exit(-1)
											endif
											
											vDsRegistro = ""
											putitem/id vDsRegistro, "TP_VALOR", TP_VALOR.FIS_NFITEMVLSVC
											putitem/id vDsRegistro, "CD_VALOR", CD_VALOR.FIS_NFITEMVLSVC
											putitem/id vDsRegistro, "VL_PRODUTO", vVlProdutoAtu
											putitem vDsLstValor, -1, vDsRegistro
											vInAchou = <TRUE>
										endif        
									endif
								endif
								
								if (vInAchou = <FALSE>)
										vDsRegistro = ""
										putitem/id vDsRegistro, "TP_VALOR", TP_VALOR.FIS_NFITEMVLSVC
										putitem/id vDsRegistro, "CD_VALOR", CD_VALOR.FIS_NFITEMVLSVC
										putitem/id vDsRegistro, "VL_PRODUTO", VL_UNITARIO.FIS_NFITEMVLSVC
										putitem vDsLstValor, -1, vDsRegistro
								endif
								
								setocc "FIS_NFITEMVLSVC" , $curocc("FIS_NFITEMVLSVC") + 1
							endwhile			
						endif
						
						if ($empty("FIS_NFITEMUNSVC") = 0)
							if (TP_OPERACAO.FIS_NFITEMUNSVC = "M")
								vQtProdutoAtu = QT_FATURADO.FIS_NFITEMPROSVC * QT_CONVERSAO.FIS_NFITEMUNSVC
								vVlProdutoAtu = VL_UNITLIQUIDO.FIS_NFITEMPROSVC / QT_CONVERSAO.FIS_NFITEMUNSVC
							else
								vQtProdutoAtu = QT_FATURADO.FIS_NFITEMPROSVC / QT_CONVERSAO.FIS_NFITEMUNSVC
								vVlProdutoAtu = VL_UNITLIQUIDO.FIS_NFITEMPROSVC * QT_CONVERSAO.FIS_NFITEMUNSVC
							endif
						else
							vQtProdutoAtu = QT_FATURADO.FIS_NFITEMPROSVC
							vVlProdutoAtu = VL_UNITLIQUIDO.FIS_NFITEMPROSVC
						endif
						
						;---Elia Proj.66/105 07/12/07
						viParams = ""
						putitem/id viParams, "CD_EMPRESA", vCdEmpresaBase
						putitem/id viParams, "CD_PRODUTO", CD_PRODUTO.FIS_NFITEMPROSVC
						activate "PRDSVCO008".buscaDadosFilial($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
						if ($procerror)       
							$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
							poCdErro = $xCdErro$
							poCtxErro = $xCtxErro$
							exit(-1)
						elseif ($status < 0)
							$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
							poCdErro = $xCdErro$
							poCtxErro = $xCtxErro$
							exit(-1)
						endif
						vTpLote = $item("TP_LOTE", voParams)
						vTpInspecao = $item("TP_INSPECAO", voParams)
						;---Elia Proj. 130/505 30/06/09
						vInProdAcabado = $item("IN_PRODACABADO", voParams)
						vInProdPropria = $item("IN_PRODPROPRIA", voParams)

						;--->MNT - Prj 130/1227 - 30/08/2010
						if (vTpContrInspSaldoLote = 1) & ($cdOperEntInspecao$ = 0) & (vTpLote > 0)
							$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Nenhuma operação informada no parametro CD_OPER_ENT_INSPECAO. Verifique!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
							poCdErro = $xCdErro$
							poCtxErro = $xCtxErro$
							exit(-1)
						endif
						;<---	

						if (vInProdAcabado = <TRUE>) & (vInProdPropria = <TRUE>)
						else
							if ($cdCustoMedioSemImp$ > 0) & ($tpValidaCustoMedio$ = 1) 
								clear/e "FIS_NFITEMVLSVC"
								TP_VALOR.FIS_NFITEMVLSVC/init = "C"
								CD_VALOR.FIS_NFITEMVLSVC/init = $cdCustoMedioSemImp$
								retrieve/e "FIS_NFITEMVLSVC"
								if ($status >= 0)
									if (VL_UNITARIO.FIS_NFITEMVLSVC = 0)
										$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Produto %%CD_PRODUTO.FIS_NFITEMPROSVC da NF %%CD_EMPFAT.FIS_NFITEMPROSVC / %%NR_FATURA.FIS_NFITEMPROSVC / %%DT_FATURA.FIS_NFITEMPROSVC possui valor de custo médio sem imposto(CD_CUSTO_MEDIO_S_IMPOSTO) zerado!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
										poCdErro = $xCdErro$
										poCtxErro = $xCtxErro$
										exit(-1)
									endif
								endif
								clear/e "FIS_NFITEMVLSVC"
								retrieve/e "FIS_NFITEMVLSVC"
							endif
						endif
						;
						;---Elia Proj. 130/16 04/07/08
						viParams = "" 
						putitem/id viParams, "CD_EMPRESA", CD_EMPRESA.FIS_NFITEMPROSVC
						putitem/id viParams, "NR_FATURA", NR_FATURA.FIS_NFITEMPROSVC
						putitem/id viParams, "DT_FATURA", DT_FATURA.FIS_NFITEMPROSVC
						putitem/id viParams, "NR_ITEM", NR_ITEM.FIS_NFITEMPROSVC
						activate "GERSVCO058".buscaDadosGerOperCfopNf($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
						if ($procerror)
							$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
							poCdErro = $xCdErro$
							poCtxErro = $xCtxErro$
							exit(-1)
						elseif ($xCdErro$)
							$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=%%$item("DESCRICAO", $xCtxErro$)", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
							poCdErro = $xCdErro$
							poCtxErro = $xCtxErro$
							exit(-1)
						endif
						vInKardex = $item("IN_KARDEX", voParams)

						if (vInKardex = <FALSE>)
						else
							if (vInSoTerceiro != <TRUE>)
								;--->MNT - Prj 130/1227 - 30/08/2010
								vInInspecao = <FALSE>

								if (vInSemInspecao) | ($empty("BAL_BALANCOTRSVC") = 0)
								else
									if (vTpContrInspSaldoLote = 1)
										if (vTpLote > 0) & (TP_OPERACAO.FIS_NFSVC = "E") 
											vInInspecao = <TRUE>
										endif
									else
										if (vTpLote > 0) & (vTpInspecao > 0) & ($cdOperEntInspecao$ != "") & (TP_OPERACAO.FIS_NFSVC = "E")
											;-->> ALX - 156/451 - 26/10/2010 --;
											if ($tinTinturaria$ = 1 & TP_MODALIDADE.GER_OPERACAOSVC = 3)
												vInInspecao = <FALSE>
											;--<<
											else
												vInInspecao = <TRUE>
											endif
										endif

										if (vInInspecao = <TRUE>)
											vInAchouLote = <False>
											clear/e "TRA_ITEMLOTESVC"
											CD_EMPRESA.TRA_ITEMLOTESVC   = CD_EMPRESAORI.FIS_NFSVC
											NR_TRANSACAO.TRA_ITEMLOTESVC = NR_TRANSACAOORI.FIS_NFSVC
											DT_TRANSACAO.TRA_ITEMLOTESVC = DT_TRANSACAOORI.FIS_NFSVC
											retrieve/e "TRA_ITEMLOTESVC"
											if($status >= 0)
												setocc "TRA_ITEMLOTESVC", 1
													while($status >= 0)
													clear/e "PRD_LOTEISVC"
													CD_EMPRESA.PRD_LOTEISVC/init = CD_EMPLOTE.TRA_ITEMLOTESVC
													NR_LOTE.PRD_LOTEISVC/init    = NR_LOTE.TRA_ITEMLOTESVC
													CD_PRODUTO.PRD_LOTEISVC/init = CD_PRODUTO.FIS_NFITEMPROSVC
													retrieve/e "PRD_LOTEISVC"
													if($status >= 0)
														vInAchouLote = <True>
														break
													endif
													setocc "TRA_ITEMLOTESVC", $curocc("TRA_ITEMLOTESVC")+1
												endwhile
											endif
											if (vInAchouLote)
												vInInspecao = <FALSE>
											endif
										endif
									endif
								endif

								;;<ANO Pjt130 Trf1087 - 18/05/2010>
								if(vQtProcessado % 50 = 0)
									deleteinstance "GERSVCO008I"
									newinstance "GERSVCO008", "GERSVCO008I", "TRANSACTION=FALSE"
								endif
								;/;<ANO Pjt130 Trf1087 - 18/05/2010>

								viParams = "" 
								putitem/id viParams, "CD_EMPRESA", vCdEmpresaBase
								putitem/id viParams, "CD_PRODUTO", CD_PRODUTO.FIS_NFITEMPROSVC
								putitem/id viParams, "NR_ITEM", NR_ITEM.FIS_NFITEMPROSVC
								putitem/id viParams, "IN_ESTORNO", vInEstorno
								if (vInInspecao) & (IN_KARDEX.GER_OPERACAOSVC = <TRUE>)
									putitem/id viParams, "CD_OPERACAO", $cdOperEntInspecao$
								else
									putitem/id viParams, "CD_OPERACAO", vCdOperacaoBase
								endif
								putitem/id viParams, "TP_DCTOORIGEM", 2 ;Faturamento
								putitem/id viParams, "NR_DCTOORIGEM", NR_FATURA.FIS_NFSVC
								putitem/id viParams, "DT_DCTOORIGEM", DT_FATURA.FIS_NFSVC
								putitem/id viParams, "QT_MOVIMENTO", vQtProdutoAtu
								putitem/id viParams, "VL_UNITLIQUIDO" , vVlProdutoAtu
								putitem/id viParams, "VL_UNITSEMIMPOSTO" , vVlProdutoAtu
								putitem/id viParams, "CD_COMPONENTE", vCdComponente
								putitem/id viParams, "DS_LSTVALOR", vDsLstValor
								;putitem/id viParams, "IN_VALIDA_POOL", vInValidaPool
								putitem/id viParams, "DT_KARDEX", vDtKardex ;>-- MAC - PRJ 182 / 192 - 21/11/2011
								activate "GERSVCO008".atualizaSaldoOperacao($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
								if ($procerror)
									$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
									deleteinstance "GERSVCO008I" ;;<ANO Pjt130 Trf1087 - 18/05/2010>
									poCdErro = $xCdErro$
									poCtxErro = $xCtxErro$
									exit(-1)
								elseif ($xCdErro$)
									$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=%%$item("DESCRICAO", $xCtxErro$)", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
									deleteinstance "GERSVCO008I" ;;<ANO Pjt130 Trf1087 - 18/05/2010>
									poCdErro = $xCdErro$
									poCtxErro = $xCtxErro$
									exit(-1)
								elseif ($status < 0)
									$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Problema ao atualizar o estoque!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
									deleteinstance "GERSVCO008I" ;;<ANO Pjt130 Trf1087 - 18/05/2010>
									poCdErro = $xCdErro$
									poCtxErro = $xCtxErro$
									exit(-1)
								endif
							endif
							
							;-->> ALX - 130/1328 - 22/11/2010 --;
							if (vTpContrInspSaldoLote = 1) & (vTpLote > 0) & (TP_OPERACAO.FIS_NFSVC = "S")
								if($empty("FIS_NFITEMPLOSVC") = 0)
									setocc "FIS_NFITEMPLOSVC", 1
									while ($status >= 0)
										;Hugo em 10/02/11 Tarefa 130-1418
										clear/e "PRD_LOTEISVC"
										CD_EMPRESA.PRD_LOTEISVC/init = CD_EMPLOTE.FIS_NFITEMPLOSVC
										NR_LOTE.PRD_LOTEISVC/init = NR_LOTE.FIS_NFITEMPLOSVC
										NR_ITEM.PRD_LOTEISVC/init = NR_ITEMLOTE.FIS_NFITEMPLOSVC
										retrieve/e "PRD_LOTEISVC"
										if ($status >= 0)
											if (vCdOperSaldo != 0) & (vCdOperSaldo != CD_SALDO.PRD_LOTEISVC)
												$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Saldo %%CD_SALDO.PRD_LOTEISVC do item de lote %%CD_EMPRESA.PRD_LOTEISVC / %%NR_LOTE.PRD_LOTEISVC / %%NR_ITEM.PRD_LOTEISVC diferente do saldo %%vCdOperSaldo que é padrão da operação %%vCdOperacaoBase%%%!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
												poCdErro = $xCdErro$
												poCtxErro = $xCtxErro$
												return(-1)
											endif
										endif
										;
									
										viParams = ""
										putitem/id viParams, "CD_EMPRESA", CD_EMPLOTE.FIS_NFITEMPLOSVC
										putitem/id viParams, "NR_LOTE", NR_LOTE.FIS_NFITEMPLOSVC
										putitem/id viParams, "NR_ITEM", NR_ITEMLOTE.FIS_NFITEMPLOSVC
										putitem/id viParams, "QT_MOVIMENTO", QT_LOTE.FIS_NFITEMPLOSVC
										putitem/id viParams, "TP_MOVIMENTO", "B" ; B - Baixa
										putitem/id viParams, "IN_ESTORNO", vInEstorno
										if (TP_MODALIDADE.GER_OPERACAOSVC = 3) ;Devolução
											putitem/id viParams, "IN_VALIDASITUACAO", <FALSE> 
										endif
										activate "PRDSVCO020".movimentaQtLoteI($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
										if ($procerror)       
											$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
											poCdErro = $xCdErro$
											poCtxErro = $xCtxErro$
											return(-1)
										elseif ($status < 0)
											$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
											poCdErro = $xCdErro$
											poCtxErro = $xCtxErro$
											return(-1)
										endif	
										
										setocc "FIS_NFITEMPLOSVC", $curocc("FIS_NFITEMPLOSVC") + 1
									endwhile
								endif
							endif
							;--<<
							
							;Hugo em 10/03/2011 Tarefa 156-536
							if ($tinTinturaria$ = 1) & (TP_OPERACAO.FIS_NFSVC = "E") & (TP_MODALIDADE.GER_OPERACAOSVC = 2) & ($empty("FIS_NFITEMPLOSVC") = 0)
								setocc "FIS_NFITEMPLOSVC", 1
								while ($status >= 0)
									if ($tpSitLoteITransf$ > 0)
										viParams = ""
										putitem/id viParams, "CD_EMPRESA", CD_EMPLOTE.FIS_NFITEMPLOSVC
										putitem/id viParams, "NR_LOTE", NR_LOTE.FIS_NFITEMPLOSVC
										putitem/id viParams, "NR_ITEM", NR_ITEMLOTE.FIS_NFITEMPLOSVC
										putitem/id viParams, "TP_SITUACAO", 1 ;Aprovado
										activate "PRDSVCO020".alteraSituacaoLoteI($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
										if ($procerror)       
											$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
											poCdErro = $xCdErro$
											poCtxErro = $xCtxErro$
											return(-1)
										elseif ($status < 0)
											$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
											poCdErro = $xCdErro$
											poCtxErro = $xCtxErro$
											return(-1)
										endif
									endif	
									
									if ($tpSitLoteCTransf$ > 0)
										viParams = ""
										putitem/id viParams, "CD_EMPRESA", CD_EMPLOTE.FIS_NFITEMPLOSVC
										putitem/id viParams, "NR_LOTE", NR_LOTE.FIS_NFITEMPLOSVC
										putitem/id viParams, "TP_SITUACAO", 3 ;Liberado
										activate "PRDSVCO020".alteraSituacaoLoteC($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
										if ($procerror)       
											$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
											poCdErro = $xCdErro$
											poCtxErro = $xCtxErro$
											return(-1)
										elseif ($status < 0)
											$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
											poCdErro = $xCdErro$
											poCtxErro = $xCtxErro$
											return(-1)
										endif
									endif	
									
									setocc "FIS_NFITEMPLOSVC", $curocc("FIS_NFITEMPLOSVC") + 1
								endwhile
							endif
							;
						endif
						
						if (vInEstoqueTerceiro = <TRUE>) | (vInEstoqueDeTerceiro = <TRUE>)
							viParams = "" 
							putitem/id viParams, "CD_EMPRESA", vCdEmpresaBase
							putitem/id viParams, "CD_PRODUTO", CD_PRODUTO.FIS_NFITEMPROSVC
							putitem/id viParams, "CD_PESSOA", CD_PESSOA.FIS_NFSVC
							putitem/id viParams, "CD_OPERACAO", vCdOperacaoBase
							putitem/id viParams, "IN_ESTORNO", vInEstorno
							putitem/id viParams, "TP_DCTOORIGEM", 2 ;Faturamento
							putitem/id viParams, "NR_DCTOORIGEM", NR_FATURA.FIS_NFSVC
							putitem/id viParams, "DT_DCTOORIGEM", DT_FATURA.FIS_NFSVC
							putitem/id viParams, "QT_MOVIMENTO", vQtProdutoAtu
							putitem/id viParams, "CD_COMPONENTE", vCdComponente
							;---Elia Proj. 130/155 10/11/08
							if (vInEstoqueTerceiro = <TRUE>)
								if ($cdSaldoEstTerceiro$ != 0)
									putitem/id viParams, "CD_SALDO", $cdSaldoEstTerceiro$
								endif
								putitem/id viParams, "IN_INVERTIDO", <TRUE>
							else
								if ($cdSaldoEstDeTerc$ = 0)
									$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Valor do parametro empresa CD_SALDO_EST_DE_TERC não cadatrado!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
									poCdErro = $xCdErro$
									poCtxErro = $xCtxErro$
									exit(-1)
								endif
								putitem/id viParams, "CD_SALDO", $cdSaldoEstDeTerc$
								putitem/id viParams, "IN_INVERTIDO", <FALSE>
							endif
							;
							activate "PRDSVCO022".gravaMovimento($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
							if ($procerror)
								$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
								poCdErro = $xCdErro$
								poCtxErro = $xCtxErro$
								exit(-1)
							elseif ($xCdErro$)
								$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=%%$item("DESCRICAO", $xCtxErro$)", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
								poCdErro = $xCdErro$
								poCtxErro = $xCtxErro$
								exit(-1)
							endif
						endif

						setocc "FIS_NFITEMPROSVC" , $curocc("FIS_NFITEMPROSVC") + 1
					endwhile
				endif
				setocc "FIS_NFITEMSVC" , $curocc("FIS_NFITEMSVC") + 1
			endwhile
			deleteinstance "GERSVCO008I" ;;<ANO Pjt130 Trf1087 - 18/05/2010>
		endif 

		clear/e "GER_OPERACAOSVC"
		CD_OPERACAO.GER_OPERACAOSVC/init = vCdOperacaoBase
		retrieve/e "GER_OPERACAOSVC"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Operaçao %%CD_OPERACAO.GER_OPERACAOSVC não cadastrada!", "ADICIONAL=Operação->FISSVCO038.atualizaEstoqueNF")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			exit(-1)
		endif

		if (vInTransferencia != <TRUE>) & (vInSoTerceiro != <TRUE>)
			if (TP_OPERACAO.FIS_NFSVC = "E") & (TP_MODALIDADE.GER_OPERACAOSVC = 2) & (CD_OPERFAT.GER_OPERACAOSVC > 0) ;Transferencia de entrada
				clear/e "GER_EMPRESASVC"
				CD_PESSOA.GER_EMPRESASVC/init = CD_PESSOA.FIS_NFSVC
				retrieve/e "GER_EMPRESASVC"
				if ($status >= 0)
					viParams = ""
					vDsLstNFAux = ""
                    ;---Elia 29/04/10
					;putitem vDsLstNFAux, -1, vDsRegistro
					putitem vDsLstNFAux, -1, vDsRegistroNF
					;	
					putitem/id viParams, "DS_LSTNF", vDsLstNFAux
					putitem/id viParams, "IN_ESTORNO", <FALSE>
					putitem/id viParams, "CD_EMPRESABASE", CD_EMPRESA.GER_EMPRESASVC
					putitem/id viParams, "CD_OPERACAOBASE", CD_OPERFAT.GER_OPERACAOSVC
					putitem/id viParams, "IN_TRANSFERENCIA", <TRUE>
					newinstance "FISSVCO038", "FISSVCO038I", "TRANSACTION=FALSE"
					activate "FISSVCO038I".atualizaEstoqueNF($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
					if ($procerror)
						$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
						poCdErro = $xCdErro$
						poCtxErro = $xCtxErro$
						exit(-1)
					elseif ($status < 0)
						$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
						poCdErro = $xCdErro$
						poCtxErro = $xCtxErro$
						exit(-1)
					endif
				endif
			endif
		
			if (TP_OPERACAO.FIS_NFSVC = "S") & (TP_MODALIDADE.GER_OPERACAOSVC = 2) & ($cdOperEntEstTrans$ > 0) ;Transferência de saída      
				clear/e "GER_EMPRESASVC"
				CD_PESSOA.GER_EMPRESASVC/init = CD_PESSOA.FIS_NFSVC
				retrieve/e "GER_EMPRESASVC"
				if ($status >= 0)
					viParams = ""
					vDsLstNFAux = ""
					;---Elia 29/04/10
					;putitem vDsLstNFAux, -1, vDsRegistro
					putitem vDsLstNFAux, -1, vDsRegistroNF
					;
					putitem/id viParams, "DS_LSTNF", vDsLstNFAux
					putitem/id viParams, "IN_ESTORNO", <FALSE>
					putitem/id viParams, "CD_EMPRESABASE", CD_EMPRESA.GER_EMPRESASVC
					putitem/id viParams, "CD_OPERACAOBASE", $cdOperEntEstTrans$
					putitem/id viParams, "IN_TRANSFERENCIA", <TRUE>
					newinstance "FISSVCO038", "FISSVCO038I", "TRANSACTION=FALSE"
					activate "FISSVCO038I".atualizaEstoqueNF($xlpg$, viParams, voParams, $xCdErro$, $xCtxErro$)
					if ($procerror)
						$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
						poCdErro = $xCdErro$
						poCtxErro = $xCtxErro$
						exit(-1)
					elseif ($status < 0)
						$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
						poCdErro = $xCdErro$
						poCtxErro = $xCtxErro$
						exit(-1)
					endif
				endif
			endif
		endif
		
		delitem vDsLstNF, 1
	until (vDsLstNF = "")

	exit(0)

End ; operation atualizaEstoqueNF
