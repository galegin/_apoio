;-------------------------------------
public operation buscaDadosRegraFiscal
;-------------------------------------

	params
		string  $xlpg$      :IN
		string  piParams    :IN
		string  poParams    :OUT
		string  poCdErro    :OUT
		string  poCtxErro   :OUT
	endparams

	variables
		numeric vCdRegraFiscal
	endvariables

	vCdRegraFiscal = $item("CD_REGRAFISCAL", piParams)

	if (vCdRegraFiscal = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Regra fiscal não informada!", "ADICIONAL=Operação->FISSVCO033.buscaDadosRegraFiscal")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	clear/e "FIS_REGRAFISCSVC"
	CD_REGRAFISCAL.FIS_REGRAFISCSVC/init = vCdRegraFiscal
	retrieve/e "FIS_REGRAFISCSVC"
	if ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Regra fiscal inválida!", "ADICIONAL=Operação->FISSVCO033.buscaDadosRegraFiscal")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	poParams = ""
	putlistitems/occ poParams, "FIS_REGRAFISCSVC"
	putitem/id poParams, "CD_CFOPSERVICO", CD_CFOP.FIS_REGRASRVSVC
	putitem/id poParams, "CD_CSTSERVICO", CD_CST.FIS_REGRASRVSVC

	return (0)
end


;------------------------------------------
;-- MAD [Proj/Tar.170/011] - 16/10/2010
public operation buscaRegraFiscalProduto
;------------------------------------------

	params
		string  $xlpg$      :IN
		string  piParams    :IN
		string  poParams    :OUT
		string  poCdErro    :OUT
		string  poCtxErro   :OUT
	endparams

	variables
		numeric vCdProduto, vCdOperacao
	endvariables

	vCdProduto  = $item("CD_PRODUTO" , piParams)
	vCdOperacao = $item("CD_OPERACAO", piParams)

	if (vCdProduto = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Código do produto não informado!", "ADICIONAL=Operação->FISSVCO033.buscaRegraFiscalProduto")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	if (vCdOperacao = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Código da operação não informado!", "ADICIONAL=Operação->FISSVCO033.buscaRegraFiscalProduto")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	clear/e "PRD_PRDREGRAFSVC"
	CD_PRODUTO.PRD_PRDREGRAFSVC/init  = vCdProduto
	CD_OPERACAO.PRD_PRDREGRAFSVC/init = vCdOperacao
	retrieve/e "PRD_PRDREGRAFSVC"
	if ($status < 0)
		clear/e "PRD_PRDREGRAFSVC"
	endif

	poParams = ""
	putlistitems/occ poParams, "PRD_PRDREGRAFSVC"

	return(0)

End ; operation buscaRegraFiscalProduto
;;

;------------------------------------------
;-- MAD [Proj/Tar.175/243] - 20/10/2011
public operation buscaCfopExterior
;------------------------------------------

	params
		string  $xlpg$      :IN
		string  piParams    :IN
		string  poParams    :OUT
		string  poCdErro    :OUT
		string  poCtxErro   :OUT
	endparams

	variables
		numeric vCdRegraFiscal
	endvariables

	vCdRegraFiscal = $item("CD_REGRAFISCAL", piParams)

	if (vCdRegraFiscal = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Regra fiscal não informada!", "ADICIONAL=Operação->FISSVCO033.buscaCfopExterior")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	clear/e "FIS_REGRAADICSVC"
	CD_REGRAFISCAL.FIS_REGRAADICSVC/init = vCdRegraFiscal
	retrieve/e "FIS_REGRAADICSVC"
	if ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=CFOP para o exterior não configurada para a regra fiscal!", "ADICIONAL=Operação->FISSVCO033.buscaCfopExterior")
		poCdErro  = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	poParams = ""
	putlistitems/occ poParams, "FIS_REGRAADICSVC"

	return(0)

End ; operation buscaCfopExterior