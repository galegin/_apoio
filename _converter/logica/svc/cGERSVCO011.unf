;-------------------------
public operation getNumSeq
;-------------------------
    params
        string  piGlobal     :IN 
        string  piNmEntidade :IN
        string  piNmAtributo :IN
        Date    piData       :IN
        numeric piNrFinal    :IN
        numeric poNrSeq     :OUT
        string  poCdErro    :OUT
        string  poCtxErro   :OUT 
    endparams

    poCdErro = ""
    poCtxErro = "" 

    clear/e "GER_NRDTSEQSVC" 
    cd_empresa.ger_nrdtseqsvc/init = $item("CD_GRUPOEMPRESA",piGlobal)
    nm_entidade.ger_nrdtseqsvc/init = piNmEntidade 
    nm_atributo.ger_nrdtseqsvc/init = piNmAtributo
    dt_sequencia.ger_nrdtseqsvc/init = piData
    retrieve/e "GER_NRDTSEQSVC" 
    if ($status = 0) ;Ocorrencia encontrada 
        if (piData = "")
            piData = $date
        endif
        if (dt_sequencia.ger_nrdtseqsvc = piData)
            if (nr_atual.ger_nrdtseqsvc = nr_final.ger_nrdtseqsvc)
                if (in_reiniciar.ger_nrdtseqsvc)
                    nr_atual.ger_nrdtseqsvc = 0
                endif
            endif
            nr_atual.ger_nrdtseqsvc = nr_atual.ger_nrdtseqsvc + 1 
        else
          dt_sequencia.ger_nrdtseqsvc = piData
          nr_atual.ger_nrdtseqsvc = 1
        endif
    elseif ($status = -2) ; Ocorrencia nao encontrada 
           $status    = 0
           $xCdErro$  = ""
           $xCtxErro$ = "" 

           creocc "GER_NRDTSEQSVC", -1 
           cd_empresa.ger_nrdtseqsvc 			= $item("CD_GRUPOEMPRESA",piGlobal)
           nm_entidade.ger_nrdtseqsvc 			= piNmEntidade 
           nm_atributo.ger_nrdtseqsvc 			= piNmAtributo
           dt_sequencia.ger_nrdtseqsvc 			= piData
           nr_incremento.ger_nrdtseqsvc 		= 1
           nr_atual.ger_nrdtseqsvc 				= 1
           nr_inicial.ger_nrdtseqsvc 			= 1
           nr_final.ger_nrdtseqsvc 				= piNrFinal
           in_reiniciar.ger_nrdtseqsvc 			= <true>
           cd_grupoempresa.ger_nrdtseqsvc 		= $item("CD_GRUPOEMPRESA",piGlobal)
           cd_operador.ger_nrdtseqsvc 			= $item("CD_USUARIO",piGlobal)
           dt_cadastro.ger_nrdtseqsvc 			= $datim
    else
       $instancehandle->SetErroProc($procerrorcontext,poCdErro,poCtxErro)
       return (-1); finaliza a execução da operation
    endif
    if ($status >=0)
        $collhandle("GER_NRDTSEQSVC")->Salvar()
        if ($procerror)
            $instancehandle->SetErroProc($procerrorcontext,poCdErro,poCtxErro)
            return (-1)
        elseif ($status < 0)
            $instancehandle->SetErroApl($xctxerro$,poCdErro,poCtxErro)
            return (-1)
        else
            poNrSeq = nr_atual.ger_nrdtseqsvc ; atribui-se ao parametro de saida o valor do atributo
        endif
    else
        $instancehandle->SetErroProc($procerrorcontext,poCdErro,poCtxErro)
        return (-1)
    endif
    return (0)
;=========================================================================
;Autor: BAM
;
;Nome:GetNumSeq
;
;Data: 10/06/2002
;
;Versão:
;
;Descrição : Numeracao Sequencial com Data		
;
;
;Parametros Entrada
;			Nome Entidade
;			Nome do Campo 
;			Numero final da sequencia	
;                       Data do registro  
;Parametros Saída
;                       Numero da sequencia 
;			Código de Erro
;			Contexto do erro
;Exemplo Chamada
;	activate "GERSVCO011".GetNumSeq($entname,"NR_TRANSACAO",999999999,$$GDtSistema,NR_TRANSACAO.CMP_TRANSACAO,$xCdErro$,$xCtxErro$)
;Histórico Manutenção:
;
;Data	Operador	Detalhes
end ;getNumseq  fecha a operation