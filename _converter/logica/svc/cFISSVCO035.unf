;---------------------------------------
public operation buscaDadosFiscalProduto
;---------------------------------------

	params
		string  $xlpg$      :IN    
		string  piParams    :IN
		string  poParams    :OUT
		string  poCdErro    :OUT
		string  poCtxErro   :OUT
	endparams

	variables
		numeric vCdProduto
		string vCdUfOrigem, vCdUFDestino, vTpOperacao
		date   vDtSistema
	endvariables

	vCdProduto   = $item("CD_PRODUTO", piParams)
	vCdUfOrigem  = $item("UF_ORIGEM", piParams)
	vCdUFDestino = $item("UF_DESTINO", piParams)
	vTpOperacao  = $item("TP_OPERACAO", piParams)	
	vDtSistema     = $item("DT_SISTEMA", $xlpg$) ;thamati 24/11/2009 [Proj. 0061 - Tar. 0713]

	if (vCdUfOrigem = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=UF origem não informada!", "ADICIONAL=Operação->FISSVCO035.buscaDadosFiscalProduto")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	if (vCdUfDestino = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=UF destino não informada!", "ADICIONAL=Operação->FISSVCO035.buscaDadosFiscalProduto")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
	
	if (vTpOperacao = "")
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Tipo de operação não informada!", "ADICIONAL=Operação->FISSVCO035.buscaDadosFiscalProduto")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	if (vCdProduto != "")
		clear/e "PRD_PRODUTOSVC"
		CD_PRODUTO.PRD_PRODUTOSVC/init = vCdProduto
		retrieve/e "PRD_PRODUTOSVC"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Produto inválido!", "ADICIONAL=Operação->FISSVCO035.buscaDadosFiscalProduto")
			poCdErro = $xCdErro$
			poCtxErro = $xCtxErro$
			return(-1)
		endif

		if (CD_TIPI.PRD_PRODUTOSVC != "")
			clear/e "FIS_INFOFISCASVC"
			TP_OPERACAO.FIS_INFOFISCASVC/init = vTpOperacao
			CD_NCM.FIS_INFOFISCASVC/init = CD_TIPI.PRD_PRODUTOSVC
			CD_UFORIGEM.FIS_INFOFISCASVC/init = vCDUfOrigem
			CD_UFDESTINO.FIS_INFOFISCASVC/init = vCdUfDestino
			retrieve/e "FIS_INFOFISCASVC"
			if ($status >= 0)
				;thamati 24/11/2009 [Proj. 0061 - Tar. 0713]
				if (DT_INIVIGENCIA.FIS_INFOFISCASVC != "") & (vDtSistema < DT_INIVIGENCIA.FIS_INFOFISCASVC) | %\
				   (DT_FIMVIGENCIA.FIS_INFOFISCASVC != "") & (vDtSistema > DT_FIMVIGENCIA.FIS_INFOFISCASVC)
					return(0)
				endif
				;
				putlistitems/occ poParams, "FIS_INFOFISCASVC"
			endif
		endif
	endif
	
	return (0)
End ; operation buscaDadosFiscalProduto