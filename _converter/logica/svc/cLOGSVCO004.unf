;------------------------
public operation gravaUltComponente
;------------------------
	params
		string  $xlpg$      :IN    
		string  piParams    :IN
		string  poParams    :OUT
		string  poCdErro    :OUT
		string  poCtxErro   :OUT
	endparams
	
	variables
		string vCdComponente
		numeric vCdUsuario, vNrSessao
	endvariables
	
	vCdUsuario = $item("CD_USUARIO", piParams)
	vCdComponente = $item("CD_COMPONENTE", piParams)
	vNrSessao = $item("NR_SESSAO", $$gParamUsr)

	if (vCdUsuario  = 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Usuário não informado!", "ADICIONAL=Operação->LOGSVCO004.gravaUltComponente")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	if (vCdComponente  = 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Componente não informado!", "ADICIONAL=Operação->LOGSVCO004.gravaUltComponente")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	if (vNrSessao = 0)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=Sessão não informada!", "ADICIONAL=Operação->LOGSVCO004.gravaUltComponente")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif
	
	clear/e "SIS_SESSAOSVC"
	creocc "SIS_SESSAOSVC", -1
	NR_SESSAO.SIS_SESSAOSVC = vNrSessao 
	CD_OPERADOR.SIS_SESSAOSVC = vCdUsuario 
	DT_CADASTRO.SIS_SESSAOSVC = $date
	retrieve/o "SIS_SESSAOSVC"
	if ($status = -7)
		retrieve/x "SIS_SESSAOSVC"
	endif
	CD_ULTIMOCOMPONENTE.SIS_SESSAOSVC = vCdComponente
	
	$collhandle("SIS_SESSAOSVC")->Salvar()
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	elseif ($status < 0)
		$instancehandle->SetStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")    
		poCdErro = $xCdErro$
		poCtxErro = $xCtxErro$
		return(-1)
	endif

	commit
	if ($status < 0)
		rollback
		exit (-1) 
	else
		$instancehandle->SetHint("ID=APLINFO002")
	endif
		
	return(0)
end;  gravaUltComponente