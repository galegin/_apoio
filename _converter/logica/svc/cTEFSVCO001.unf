;------------------
entry BuscaOperador
;------------------
params
   string  piGlobal :IN
   string  piParams :IN
   string  poParams :OUT
   string  poCdErro  :OUT
   string  poCtxErro :OUT
endparams
variables
	Numeric vCdRedeTEF,vTpTransacao
	String vNmRedeTEF,vdsoperacao
endvariables


	$instancehandle->SetStatus(<STS_LOG>,"CFI0002","","ADICIONAL=%%$COMPONENTNAME%%%.BUSCAOPERADOR·;LOG=%%piParams")

	vCdRedeTEF 		= $item("010-001",piParams)
	vTpTransacao 	= $item("011-000",piParams)
	vNmRedeTEF 		= $item("010-000",piParams)
	if (vTpTransacao="")
		$instancehandle->SetStatus(<STS_ERRO>,"TEF0006","","ADICIONAL=BuscaOperador")
	else	
		clear/e "tef_tipotranssvc"
		tp_tef.tef_tipotranssvc = $item("TEF_TIPO",piGlobal)
		tp_transacao.tef_tipotranssvc = vTpTransacao
		retrieve/e "tef_tipotranssvc"
		if ($status < 0)
			putitem/id poParams,"TP_OPERCREDITO",3
		else
			putitem/id poParams,"TP_OPERCREDITO", tp_opercredito.tef_tipotranssvc
			putitem/id poParams,"NR_VIAS",nr_vias.tef_tipotranssvc
		endif

		if (vCdRedeTEF = "")
			clear/e "tef_redetefsvc"
			tp_tef.tef_redetefsvc = $item("TEF_TIPO",piGlobal)
			nm_redetef.tef_redetefsvc = vNmRedeTEF
			retrieve/e "tef_redetefsvc"
			if ($status < 0)
				$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=")
				return($status)
			else
				vCdRedeTEF = cd_redetef.tef_redetefsvc
			endif
		endif
		clear/e "tef_relopersvc"
		tp_tef.tef_relopersvc = $item("TEF_TIPO",piGlobal)
		cd_redetef.tef_relopersvc = vCdRedeTEF
		tp_transacao.tef_relopersvc = vTpTransacao
		retrieve/e "tef_relopersvc"
		if ($status < 0)
			$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=")
		else	
			putitem/id poParams,"CD_OPERCREDITO",cd_opercredito.pes_opercredisvc			
			if (tp_opercredito.pes_opercredisvc != "")
				vDsOperacao = $item(tp_opercredito.pes_opercredisvc,$valrep(tp_opercredito.pes_opercredisvc))
				putitem/id poParams,"DS_OPERCREDITO",vDsOperacao
			endif
		endif
	endif
	return ($status)
end ; BuscaOperador

;--------------------------
public operation EfetuarTEF 
;--------------------------
params
   string  piGlobal :IN
   string  piParams :IN
   string  poParams :OUT
   string  $xcderro$ : OUT
   string  $xctxerro$ :OUT
endparams
variables
   string vDsMensagem,voParams
endvariables
;Verificar confirmações pendentes ou arquivo de retorno.
$instancehandle->SetStatus(<STS_LOG>,"CFI0002","","ADICIONAL=%%$COMPONENTNAME%%%.EFETUARTEF·;LOG=%%piParams")

clear/e "TEF_LANCAMENTSVC"
activate "ADMSVCO001".GetParamGrupoEmp($item("CD_EMPRESA",piGlobal),"TEF",$LSTPARAM$,$xcderro$,$xctxerro$)
if ($procerror)
    $instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=ADMSVCO001.GETPARAMGRUPOEMP")
elseif ($status < 0)
    $instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
else
	$instancehandle->GeraArquivoTef(piGlobal,piParams,poParams,$xcderro$,$xctxerro$)
	if ($procerror)
	    $instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=GERAARQUIVOTEF")
	elseif ($status < 0)
    	$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
	else
		putitem/id piParams,"000-000","RESPOSTA"
		putitem/id piParams,"001-000",$item("001-000",poParams)
		$instancehandle->LerArqRespostaTEF(piGlobal,piParams,poParams,$xcderro$,$xctxerro$)
		if ($procerror)
		    $instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=LERARQRESPOSTATEF")
		elseif ($status < 0)
    		$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
		else
			putitem/id poParams,"CD_LANCAMENTO", $item("CD_LANCAMENTO",piParams)
    	    vDsMensagem = $item("030-000",poParams)
        	if ($item("009-000",poParams) = 0) | ($item("009-000",poParams) = "P1")
				$collhandle("TEF_LANCAMENTSVC")->Adicionar(piGlobal,poParams)
				$collhandle("TEF_RESPOSTASVC")->Adicionar(piGlobal,poParams)
				$collhandle("TEF_LANCAMENTSVC")->Salvar()
	    	    if ($status < 0)
	        	    $instancehandle->SetStatus("",$xcderro$,$xctxerro$,"ADICIONAL=[TEF_LANCAMENTO]")
				endif
				if ($item("028-000",poParams) = 0) ;Sem cupom a imprimir
					message/info "%%vDsMensagem"
;        	   		$instancehandle->SetStatus(<STS_AVISO>,"TEF0009","DESCRICAO=%%vDsMensagem","")
            	endif
	        else
				message/info "%%vDsMensagem"
;    	       	$instancehandle->SetStatus(<STS_ERRO>,"TEF0008","DESCRICAO=%%vDsMensagem","")
				if ($item("IN_PROCESSATEF",piParams) = 0)
					putitem/id piParams,"IN_LANCAMENTO",1
					$instancehandle->RecusarTEF(piGlobal,piParams,poParams,$xcderro$,$xctxerro$)
					if ($procerror)
						$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=RECUSATEF")
					elseif ($status < 0)
						$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
					endif
				endif
				$status = <STS_ERRO>
        	endif
		endif
	endif
endif
exit($status)
End ; operation EfetuarTEF

;---------------------------
public operation FinalizaTEF 
;---------------------------
params
   string  piGlobal   :IN
   string  piParams   :IN
   string  poParams   :OUT
   string  $xcderro$  :OUT
   string  $xctxerro$ :OUT
endparams
variables
   string vDsMensagem,viParams,voParams,vRede,vNSU,vDsValor,vDsNsu
   numeric vNrCupons, vNrContCupom,vStatus
endvariables

	$instancehandle->SetStatus(<STS_LOG>,"CFI0002","","ADICIONAL=%%$COMPONENTNAME%%%.FINALIZATEF·;LOG=%%piParams")

	activate "ADMSVCO001".GetParamGrupoEmp($item("CD_EMPRESA",piGlobal),"TEF",$LSTPARAM$,$xcderro$,$xctxerro$)
	if ($procerror)
    	$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=ADMSVCO001.GETPARAMGRUPOEMP")
	    exit (-1) 
	elseif ($xcderro$)
	    $instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
	    exit (-1) 
	else
		; Envia confirmações pendentes
    	clear/e "TEF_LANCAMENTSVC"
	    cd_tefstatus.tef_lancamentsvc = "<STS_TEF_APROVADO>·;<STS_TEF_RECUSADO>"
    	retrieve/e "TEF_LANCAMENTSVC"
	    while ($dbocc(TEF_LANCAMENTSVC) > 0)
			if (nr_nsu.tef_lancamentsvc != "") | %\
				(((tp_transacao.tef_lancamentsvc = 1) | %\
				(tp_transacao.tef_lancamentsvc = 0) ) & ($item("TEF_TIPO",$LSTPARAM$)= <TEF_Discado> ))
	    	    if(cd_tefstatus.tef_lancamentsvc = <STS_TEF_APROVADO>) 
    	    	   putitem/id viParams,"000-000","CNF"
	        	else
		           putitem/id viParams,"000-000","NCN"
    		    endif  
				putitem/id viParams,"001-000",nr_identificacao.tef_lancamentsvc
				putitem/id viParams,"002-000",nr_docvinc.tef_lancamentsvc
				putitem/id viParams,"010-000",nm_redetef.tef_lancamentsvc
				putitem/id viParams,"012-000",nr_nsu.tef_lancamentsvc
				putitem/id viParams,"027-000",ds_finalizacao.tef_lancamentsvc
				if (nm_redetef.tef_lancamentsvc = "TECBAN")
					putitem/id viParams,"000-001","DISC"
				else
					putitem/id viParams,"000-001","DIAL"
				endif
				$instancehandle->GeraArquivoTef(piGlobal,viParams,voParams,$xcderro$,$xctxerro$)
				if ($procerror)
					$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=GERAARQUIVOTEF")
				elseif ($status < 0)
					$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
				else
    	    	    if  (cd_tefstatus.tef_lancamentsvc = <STS_TEF_APROVADO>) 
        	    	     cd_tefstatus.tef_lancamentsvc = <STS_TEF_SUCESSO>
	            	else
        	        	 cd_tefstatus.tef_lancamentsvc = <STS_TEF_FALHA>
		            endif  
				endif
			else
				; Caso nao exista o campo 12, nao envia confirmacao...
				if  (cd_tefstatus.tef_lancamentsvc = <STS_TEF_APROVADO>) 
       	    	     cd_tefstatus.tef_lancamentsvc = <STS_TEF_SUCESSO>
	           	else
    	           	 cd_tefstatus.tef_lancamentsvc = <STS_TEF_FALHA>
        	    endif
			endif
			$collhandle("TEF_LANCAMENTSVC")->Salvar()
			discard "TEF_LANCAMENTSVC"
		endwhile

 		; Impressao dos aprovados
	    clear/e "TEF_LANCAMENTSVC"
    	cd_tefstatus.tef_lancamentsvc = <STS_TEF_INICIADO>
	    retrieve/e "TEF_LANCAMENTSVC"
		if ($status < 0) ;Não há aprovados
    		$instancehandle->SetStatus(<STS_AVISO>,$procerror,$procerrorcontext,"ADICIONAL=[TEF_LANCAMENTO]")
		else
			vNrCupons = 0
			setocc "TEF_LANCAMENTSVC",-1
			setocc "TEF_LANCAMENTSVC",1
			repeat
				vNrCupons = vNrCupons + 1
				setocc "tef_lancamentsvc", $curocc("tef_lancamentsvc") + 1
			until ($status < 0)

			vNrContCupom = 0
			setocc "TEF_LANCAMENTSVC",1
			repeat
				vNrContCupom = vNrContCupom + 1
				if (vNrCupons > 0) 
					putitem/id viParams,"NR_CONTCUPOM",vNrContCupom
					putitem/id viParams,"NR_TOTCUPOM",vNrCupons
				else
					putitem/id viParams,"NR_CONTCUPOM",0
					putitem/id viParams,"NR_TOTCUPOM",0
				endif
				putitem/id viParams,"011-000",tp_transacao.tef_lancamentsvc
				putitem/id viParams,"010-000",nm_redetef.tef_lancamentsvc
				call BuscaOperador(piGlobal,viParams,voParams,$xcderro$,$xctxerro$)
				if ($status < 0)
					putmess "Erro buscando operador TEF: %%$xctxerro$"
				endif
				if ($item("TP_OPERCREDITO",voParams) = 3) | ($item("TP_OPERCREDITO",voParams) = "")
					putitem/id viParams,"DS_FORMAPGTO","Cartao credito"
				elseif ($item("TP_OPERCREDITO",voParams) = 4)
					putitem/id viParams,"DS_FORMAPGTO","Cartao debito"
				elseif ($item("TP_OPERCREDITO",voParams) = 2) | ($item("TP_OPERCREDITO",voParams) = 7)
					putitem/id viParams,"DS_FORMAPGTO","Cheque"
				else
					putitem/id viParams,"DS_FORMAPGTO",$item("DS_OPERCREDITO",voParams)
					putitem/id viParams,"NR_VIAS",$item("NR_VIAS",voparams)
				endif
		
				putitem/id viParams,"000-000",tp_arquivo.tef_lancamentsvc
				if (nm_redetef.tef_lancamentsvc = "TECBAN")
					putitem/id viParams,"000-001","DISC"
				else
					putitem/id viParams,"000-001","DIAL"
				endif
				putitem/id viParams,"028-000",nr_linhas.tef_respostasvc
				putitem/id viParams,"029-000",ds_cupom.tef_respostasvc
				putitem/id viParams,"030-000",ds_mensagem.tef_respostasvc

				$instancehandle->AvisoEmissaoCupomTEF(piGlobal,viParams,poParams,$xcderro$,$xctxerro$)
				if ($procerror)| ($status < 0)
					if (nr_nsu.tef_lancamentsvc != "") | %\
					   (((tp_transacao.tef_lancamentsvc = 1) | %\
					   (tp_transacao.tef_lancamentsvc = 0) ) & ($item("TEF_TIPO",$LSTPARAM$)= <TEF_Discado> ))
						$instancehandle->RecusarTEF(piGlobal,viParams,poParams,$xcderro$,$xctxerro$)
						if ($procerror)
							$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=RECUSATEF")
						elseif ($status < 0)
							$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
						else
							vStatus = <STS_ERRO>
							break
						endif
					else
						CD_TEFSTATUS.TEF_LANCAMENTSVC = <STS_TEF_FALHA>
						message/error "Transação não efetuada. %%^ Favor reter o cupom."
						$collhandle("TEF_RESPOSTASVC")->Excluir()
						$collhandle("TEF_LANCAMENTSVC")->Salvar()
					endif
					vStatus = <STS_ERRO>
				endif
				if ($status = 5)
					vNrContCupom = 0
					putitem/id viParams,"IN_PARADA",<TRUE>
					setocc "TEF_LANCAMENTSVC", 1
				else
					putitem/id viParams,"IN_PARADA",<FALSE>
					setocc "TEF_LANCAMENTSVC",$curocc("TEF_LANCAMENTSVC") + 1
				endif
			until ($status < 0)
			$status = vStatus
			if ($status >= 0)
				setocc "TEF_LANCAMENTSVC", 1
				while ($status >= 0)
					if (nr_nsu.tef_lancamentsvc != "") | %\
						(((tp_transacao.tef_lancamentsvc = 1) | %\
						(tp_transacao.tef_lancamentsvc = 0) ) & ($item("TEF_TIPO",$LSTPARAM$)= <TEF_Discado> ))
						
						cd_tefstatus.tef_lancamentsvc = <STS_TEF_APROVADO>
						$collhandle("TEF_LANCAMENTSVC")->Salvar()
	
						putitem/id viParams,"000-000","CNF"
						putitem/id viParams,"001-000",nr_identificacao.tef_lancamentsvc
						putitem/id viParams,"002-000",nr_docvinc.tef_lancamentsvc
						putitem/id viParams,"010-000",nm_redetef.tef_lancamentsvc
						putitem/id viParams,"012-000",nr_nsu.tef_lancamentsvc
						putitem/id viParams,"027-000",ds_finalizacao.tef_lancamentsvc
						$instancehandle->GeraArquivoTef(piGlobal,viParams,voParams,$xcderro$,$xctxerro$)
						if ($procerror)
							$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=GERAARQUIVOTEF")
						elseif ($status < 0)
							$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
						else
							cd_tefstatus.tef_lancamentsvc = <STS_TEF_SUCESSO>
						endif
					else
						cd_tefstatus.tef_lancamentsvc = <STS_TEF_SUCESSO>
					endif
					$collhandle("TEF_RESPOSTASVC")->Excluir()
					$collhandle("TEF_LANCAMENTSVC")->Salvar()
					setocc "TEF_LANCAMENTSVC" ,$curocc(TEF_LANCAMENTSVC)+1
				endwhile				
				$status = vStatus
			endif
		endif
endif
exit($status)
End ; operation FinalizaTEF

;--------------------------
public operation RecusarTEF
;--------------------------
	params
		string  piGlobal   :IN
		string  piParams   :IN
		string  poParams   :OUT
		string  $xcderro$  :OUT
		string  $xctxerro$ :OUT
	endparams

	variables
		string viParams,voParams,vDsValor,vDsNsu
	endvariables

	if ($item("IN_LANCAMENTO",piParams) = 1)
	    clear/e "TEF_LANCAMENTSVC"
    	cd_tefstatus.tef_lancamentsvc = <STS_TEF_INICIADO>
	    retrieve/e "TEF_LANCAMENTSVC"
		if ($status < 0)
			return (0)
		endif
	endif

	viParams = piParams
	setocc "TEF_LANCAMENTSVC", 1
	while ($status >= 0)
			cd_tefstatus.tef_lancamentsvc = <STS_TEF_RECUSADO> 
			$collhandle("TEF_LANCAMENTSVC")->Salvar()
		
			if (vl_transacao.tef_lancamentsvc > 0)
				vDsValor = "Valor: R$ %%vl_transacao.tef_lancamentsvc"
			endif
			if (nr_nsu.tef_lancamentsvc > 0) 
				vDsNsu = "NSU:%%nr_nsu.tef_lancamentsvc"
			endif
;;			message/error "Última transação TEF foi cancelada.%%^%%%Rede:%%nm_redetef.tef_lancamentsvc%%^%%vDsNsu%%^%%vDsValor %%^ Transação não efetuada. %%^ Favor reter o cupom."
;			message/error "Transação não efetuada. %%^ Favor reter o cupom."
;; Colocar o teste para Discado e dedicado, a mensagem do discado esta comentada. (William)
			putitem/id viParams,"000-000","NCN" 
			putitem/id viParams,"001-000",nr_identificacao.tef_lancamentsvc
			putitem/id viParams,"002-000",nr_docvinc.tef_lancamentsvc
			putitem/id viParams,"010-000",nm_redetef.tef_lancamentsvc
			putitem/id viParams,"012-000",nr_nsu.tef_lancamentsvc
			putitem/id viParams,"027-000",ds_finalizacao.tef_lancamentsvc
			$instancehandle->GeraArquivoTef(piGlobal,viParams,voParams,$xcderro$,$xctxerro$)
			if ($procerror)
				$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=")
			elseif ($status < 0)
				$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
			else
				CD_TEFSTATUS.TEF_LANCAMENTSVC = <STS_TEF_FALHA>
			endif
			$collhandle("TEF_RESPOSTASVC")->Excluir()
			$collhandle("TEF_LANCAMENTSVC")->Salvar()
			setocc "TEF_LANCAMENTSVC",$curocc(TEF_LANCAMENTSVC)+1
	endwhile

	message/error "Transação não efetuada. %%^ Favor reter o cupom."

	Return (0)
end; RECUSARTEF

;-------------------------------
public operation VerPendenciaTEF 
;-------------------------------
params
   string  piGlobal   :IN
   string  piParams   :IN
   string  poParams   :OUT	
   string  $xcderro$  :OUT
   string  $xctxerro$ :OUT
endparams
variables
   string viParams,voParams,vDsValor
endvariables

	$instancehandle->SetStatus(<STS_LOG>,"CFI0002","","ADICIONAL=%%$COMPONENTNAME%%%.VERPENDENCIATEF·;LOG=%%piParams")
	activate "ADMSVCO001".GetParamGrupoEmp($item("CD_EMPRESA",piGlobal),"TEF",$LSTPARAM$,$xcderro$,$xctxerro$)
	if ($procerror)
    	$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=ADMSVCO001.GETPARAMGRUPOEMP")
	elseif ($xcderro$)
    	$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
	else
		putitem/id piParams,"000-000","PENDENCIA"
		if ($item("TEF_TIPO",$LSTPARAM$) = 2) 
			putitem/id piParams,"000-001","DISC"
			$instancehandle->LerPendencias(piGlobal,piParams,poParams,$xcderro$,$xctxerro$)
			putitem/id piParams,"000-001","DIAL"
			$instancehandle->LerPendencias(piGlobal,piParams,poParams,$xcderro$,$xctxerro$) 
		else
			$instancehandle->LerPendencias(piGlobal,piParams,poParams,$xcderro$,$xctxerro$)
	   	endif
	    
	    clear/e "TEF_LANCAMENTSVC"
    	cd_tefstatus.tef_lancamentsvc = "<STS_TEF_INICIADO>·;<STS_TEF_APROVADO>"
	    retrieve/e "TEF_LANCAMENTSVC"
    	while ($dbocc(TEF_LANCAMENTSVC) > 0)
  		    if (cd_tefstatus.tef_lancamentsvc = <STS_TEF_INICIADO>)
				if (nr_nsu.tef_lancamentsvc != "") | %\
					(((tp_transacao.tef_lancamentsvc = 1) | %\
					(tp_transacao.tef_lancamentsvc = 0) ) & ($item("TEF_TIPO",$LSTPARAM$)= <TEF_Discado> ))
					cd_tefstatus.tef_lancamentsvc = <STS_TEF_RECUSADO>
					$collhandle("TEF_LANCAMENTSVC")->Salvar()
	
			        if (vl_transacao.tef_lancamentsvc != "")
    			        vDsValor = "Valor: R$ %%vl_transacao.tef_lancamentsvc"
        			endif
					message/error "Última transação TEF foi cancelada.%%^%%%Rede:%%nm_redetef.tef_lancamentsvc%%^%%%NSU:%%nr_nsu.tef_lancamentsvc%%^%%vDsValor"

		        	putitem/id viParams,"000-000","NCN"
					putitem/id viParams,"001-000",nr_identificacao.tef_lancamentsvc
					putitem/id viParams,"002-000",nr_docvinc.tef_lancamentsvc
					putitem/id viParams,"010-000",nm_redetef.tef_lancamentsvc
					putitem/id viParams,"012-000",nr_nsu.tef_lancamentsvc
					putitem/id viParams,"027-000",ds_finalizacao.tef_lancamentsvc
					if (nm_redetef.tef_lancamentsvc = "TECBAN")
						putitem/id viParams,"000-001","DISC"
					else
						putitem/id viParams,"000-001","DIAL"
					endif
					$instancehandle->GeraArquivoTef(piGlobal,viParams,voParams,$xcderro$,$xctxerro$)
					if ($procerror)
						$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=GERAARQUIVOTEF")
					elseif ($xcderro$)
						$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
					else
    	    		   cd_tefstatus.tef_lancamentsvc = <STS_TEF_FALHA>
					endif
				else
					cd_tefstatus.tef_lancamentsvc = <STS_TEF_FALHA>
				endif
		    else
				if (nr_nsu.tef_lancamentsvc != "") | %\
						(((tp_transacao.tef_lancamentsvc = 1) | %\
						(tp_transacao.tef_lancamentsvc = 0) ) & ($item("TEF_TIPO",$LSTPARAM$)= <TEF_Discado> ))
						
						cd_tefstatus.tef_lancamentsvc = <STS_TEF_APROVADO>
						$collhandle("TEF_LANCAMENTSVC")->Salvar()
	
						putitem/id viParams,"000-000","CNF" 
						putitem/id viParams,"001-000",nr_identificacao.tef_lancamentsvc
						putitem/id viParams,"002-000",nr_docvinc.tef_lancamentsvc
						putitem/id viParams,"010-000",nm_redetef.tef_lancamentsvc
						putitem/id viParams,"012-000",nr_nsu.tef_lancamentsvc
						putitem/id viParams,"027-000",ds_finalizacao.tef_lancamentsvc			
						$instancehandle->GeraArquivoTef(piGlobal,viParams,voParams,$xcderro$,$xctxerro$)
						if ($procerror)
							$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=GERAARQUIVOTEF")
						elseif ($status < 0)
							$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
						else
							cd_tefstatus.tef_lancamentsvc = <STS_TEF_SUCESSO>
						endif
				else
					cd_tefstatus.tef_lancamentsvc = <STS_TEF_SUCESSO>
				endif
			endif
			$collhandle("TEF_RESPOSTASVC")->Excluir()
			$collhandle("TEF_LANCAMENTSVC")->Salvar()
	        discard "TEF_LANCAMENTSVC"
		endwhile; 
	endif
	exit (0)
End ; operation VerPendenciaTEF

;------------------------------
public operation LerPendencias
;------------------------------ 
params
   string  piGlobal  :IN
   string  piParams  :IN
   string  poParams  :OUT	
   string $xcderro$  :OUT
   string  $xctxerro$ :OUT
endparams
	$instancehandle->LerArqRespostaTEF(piGlobal,piParams,poParams,$xcderro$,$xctxerro$)
	if ($procerror)
    	$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=")
	elseif ($status < 0)
		; Ignora erros de leitura, pois só vai existir arquivo se tiver problema.
    	$instancehandle->ResetErro()
	else
    	clear/e "TEF_LANCAMENTSVC"
	    nr_identificacao.tef_lancamentsvc = $item("001-000",poParams)
    	retrieve/e "TEF_LANCAMENTSVC"
	    if ($status = -2)
    	  	$collhandle("TEF_LANCAMENTSVC")->Adicionar(piGlobal,poParams)
			$collhandle("TEF_RESPOSTASVC")->Adicionar(piGlobal,poParams)
			$collhandle("TEF_LANCAMENTSVC")->Salvar()
    	endif
	endif
End ; operation LerPendencias

;-------------------------------
;partner operation GeraArquivoTef 
public operation GeraArquivoTef 
;-------------------------------
params
   string  piGlobal   :IN
   string  piParams   :IN
   string  poParams   :OUT
   string  $xcderro$  :OUT
   string  $xctxerro$ :OUT
endparams

variables
   string vDsLinha,vDsItem,vNmItem,vNmArq,vDirArq,vDsArq
   numeric vNrIdentificacao
   string vStatus
endvariables
	$instancehandle->SetStatus(<STS_LOG>,"CFI0002","","ADICIONAL=%%$COMPONENTNAME%%%.GERAARQUIVOTEF·;LOG=%%piParams")
	activate "ADMSVCO001".GetParamGrupoEmp($item("CD_EMPRESA",piGlobal),"TEF",$LSTPARAM$,$xcderro$,$xctxerro$)
	if ($procerror)
    	$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=")
	elseif ($xcderro$ < 0)
    	$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
	endif
    newinstance "GERSVCO001","GERSVCO001","TRANSACTION=TRUE"
	activate "GERSVCO001".GetNumSeqComm(piGlobal,"TEF_TRANSACAO","001-000",999999999,vNrIdentificacao,$xcderro$,$xctxerro$)
	if ($procerror)
    	$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=")
	elseif ($status < 0)
    	$instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
	else
    	putitem/id piParams,"001-000",vNrIdentificacao

	; 
	; Insercao de campo para teste de dominio da automacao
	;
	;putitem/id piParams,"888-888","teste"
	;
	;
	;

		clear/e "TEF_ARQUIVOSVC"
		cd_arquivo.tef_arquivosvc/init = $item("000-000",piParams)
		tp_tef.tef_arquivosvc/init     = $item("TEF_TIPO",$lstparam$)
		retrieve/e "TEF_ARQUIVOSVC"
		if ($status < 0)
    		$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=")
			return($status)
		else
	    	setocc "TEF_CAMPOARQSVC",-1
		    setocc "TEF_CAMPOARQSVC",1
    		vDsArq = ""
		    while ($dbocc("TEF_CAMPOARQSVC") > 0) 
    		     vDsLinha = ""  
        		 ;Monta a linha
	        	 vDsItem = $item(NR_CAMPO.TEF_CAMPOSVC,piParams)
								;
	    	     if (in_obrigatorio.tef_campoarqsvc) & ($item("TEF_TIPO",$LSTPARAM$) = <TEF_Dedicado>)
					 if (vDsItem = "")
						if (vl_padrao.tef_camposvc = "")
							$instancehandle->SetStatus(<STS_ERRO>,"PAR0001","","ADICIONAL=%%nr_campo.tef_camposvc - %%ds_campo.tef_camposvc")
                		   	return (-1)
	                	else
							vDsItem = vl_padrao.tef_camposvc
						endif
					else
						vDsLinha = "%%nr_campo.tef_campoarqsvc = %%vDsItem%%^"
            		endif
	    	     else
    	    	     if (vDsItem != "")
        	    	     vDsLinha = "%%nr_campo.tef_campoarqsvc = %%vDsItem%%^"
	        	     elseif (vl_padrao.tef_camposvc != "")
    	        	     vDsLinha = "%%nr_campo.tef_campoarqsvc = %%vl_padrao.tef_camposvc%%^"
	        	     endif
		         endif
    		     if (vDsLinha != "")
        		     vDsArq = "%%vDsArq%%vDsLinha"
		         endif
    		     discard "TEF_CAMPOARQSVC"
	    	endwhile 
	    	if ($item("TEF_TIPO",$LSTPARAM$) = 1) ;Dedicado
		        vDirArq = $item("TEF_ARQ_REQ_DEDICADO",$LSTPARAM$)
    		else
				if ($item("000-001",piParams) = "DISC")
		    	    vDirArq = $item("TEF_ARQ_REQ_DISC",$LSTPARAM$)
				else
					vDirArq = $item("TEF_ARQ_REQ_DIAL",$LSTPARAM$)
				endif
    		endif
	    	vNmArq = $item("TEF_ARQ_NOME",$LSTPARAM$)
	    	filedump vDsArq, "%%vDirArq%%vNmArq"
		    if ($procerror)
				$instancehandle->SetStatus(<STS_ERRO>,"TEF0001","","ADICIONAL=%%vDirArq%%vNmArq")
		    endif
		endif
		putitem/id piParams,"000-000","STATUS"
		putitem/id piParams,"001-000",vNrIdentificacao
		$instancehandle->LerArqRespostaTEF(piGlobal,piParams,poParams,$xcderro$,$xctxerro$)
		if ($procerror)
			$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"")
		elseif ($status < 0)
			vStatus=$status
			activate "winapi".deletefile("%%vDirArq%%vNmArq")
			if ($status<0)
				if ($item("TEF_TIPO",piGlobal) = 1) ;Dedicado
					$instancehandle->SetStatus(<STS_ERRO>,"TEF0011","","ADICIONAL=%%$text(TEF0003) %%vDirArq%%vNmArq%%%.")
				else
					$instancehandle->SetStatus(<STS_ERRO>,"TEF0002","","ADICIONAL=%%$text(TEF0003) %%vDirArq%%vNmArq%%%.")
				endif
	    	else
				if ($item("TEF_TIPO",piGlobal) = 1) ;Dedicado
					$instancehandle->SetStatus(<STS_ERRO>,"TEF0011","","ADICIONAL=")
				else
					$instancehandle->SetStatus(<STS_ERRO>,"TEF0002","","ADICIONAL=")
				endif
		    endif
			$status=vStatus
		endif
	endif
	return($status)
End ; operation GeraArquivoTef

;----------------------------------
public operation LerArqRespostaTEF 
;----------------------------------
params
   string  piGlobal :IN
   string  piParams :IN
   string  poParams :OUT
   string  $xcderro$  :OUT
   string  $xctxerro$ :OUT
endparams
variables
   string vDsLinha,vDsItem,vNmItem,vNmArq,vDirArq
   raw vDsArq
   numeric vNrIdentificacao,vQtTentativa,vStatus,vTIMEOUT
endvariables
	$instancehandle->SetStatus(<STS_LOG>,"CFI0002","","ADICIONAL=%%$COMPONENTNAME%%%.LERARQRESPOSTATEF·;LOG=%%piParams")
	if ($item("000-000",piParams) = "STATUS")
    	vNmArq = $item("TEF_ARQ_NOME_STATUS",$LSTPARAM$)
	else
    	vNmArq = $item("TEF_ARQ_NOME",$LSTPARAM$)
	endif
	if ($item("TEF_TIPO",$LSTPARAM$) = 1) ;Dedicado
    	vDirArq = $item("TEF_ARQ_RES_DEDICADO",$LSTPARAM$)
	else
		if ($item("000-001",piParams) = "DISC")
	        vDirArq = $item("TEF_ARQ_RES_DISC",$LSTPARAM$)
		else
			vDirArq = $item("TEF_ARQ_RES_DIAL",$LSTPARAM$)
		endif
	endif
	if ($item("TEF_TIMEOUT",piParams) != "")
		vTIMEOUT = $item("TEF_TIMEOUT",piParams)	if ($dsDirReq$ = "")
		$dsDirReq$ = "C:\CLIENT\REQ\"
	endif

	else
		vTIMEOUT = $item("TEF_TIMEOUT",$LSTPARAM$)
	endif
	vDirArq = "%%vDirArq%%vNmArq"
	vStatus = 0
	repeat
		; Faz a leitura do arquivo
		fileload/raw vDirarq,vDsArq
		if ($procerror) | ($status = 0 )
			vStatus = -1
			activate "KERNEL32".SLEEP(1000);
			if ($item("000-000",piParams) = "STATUS") 	| %\
			   ($item("000-000",piParams) = "PENDENCIA") ;| %\   ($item("000-000",piParams) = "RESPOSTA")
				vQtTentativa = vQtTentativa + 1
			endif
		else
			$instancehandle->ConverterArqTEF(piGlobal,vDsArq,poParams,$xcderro$,$xctxerro$)
		    if ($procerror)
		        $instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=")
	    	elseif($status < 0)
		        $instancehandle->SetStatus("",$xcderro$,$xctxerro$,"")
		    else
	    	    activate "winapi".deletefile(vDirArq)
	        	if ($procerror)
		            $instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"ADICIONAL=")
		        elseif($status <= 0)
					$instancehandle->SetStatus(<STS_ERRO>,"TEF0004","","ADICIONAL=%%vDirArq%%vNmArq")
		        endif
		    endif
	        if ($item("001-000",piParams) != $item("001-000",poParams)) & ($item("000-000",poParams) != "PENDENCIA")
    	        vStatus = -1
	        else
    			vStatus = 0
	        endif
	   endif
	until (vStatus = 0) | (vQtTentativa > vTIMEOUT)

	if ($item("000-000",piParams) = "PENDENCIA") & (vQtTentativa <= vTIMEOUT)
		activate "winapi".deletefile("%%vDirArq%%vNmArq")
	else 
		if (vQtTentativa > vTIMEOUT)
			$instancehandle->SetStatus(<STS_ERRO>,"TEF0005","","ADICIONAL=%%vDirArq")
		endif
	endif
End ; operation LerArqRespostaTEF

;----------------------------------
public operation ConverterArqTEF 
;----------------------------------
params
   string  piGlobal :IN
   string  piParams :IN
   string  poParams :OUT
   string  $xcderro$  :OUT
   string  $xctxerro$ :OUT
endparams
variables
   string vDsLinha,vDsItem,vNmItem,vNmArq,vDirArq,vDsArq
   numeric vNrIdentificacao,vNrLinha
endvariables
$instancehandle->SetStatus(<STS_LOG>,"CFI0002","","ADICIONAL=%%$COMPONENTNAME%%%.CONVERTERARQTEF·;LOG=%%piParams")

length piParams
while($result > 0)
	scan piParams,"{.{+"
	if ($result > 0)
		vDsLinha = piParams[1, $result - 1]                            
		piParams = piParams[$result + 4]            
	else
		vDsLinha = piParams
		piParams = ""        
	endif
    putitem/id poParams,vDsLinha[1,7],vDsLinha[11]
	vNrLinha = vNrLinha + 1
	length piParams
endwhile
return (0)
End ; operation ConverterArqTEF

;-------------------------------------
public operation AvisoEmissaoCupomTEF
;-------------------------------------
params
   string  piGlobal :IN
   string  piParams :IN
   string  poParams :OUT	
   string  $xcderro$  :OUT
   string  $xctxerro$ :OUT
endparams

activate "TEFFL001".exec(piGlobal,piParams,poParams,$xcderro$,$xctxerro$)
if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>,$procerror,$procerrorcontext,"")
endif
return ($status)
End ; operation AvisoEmissaoCupomTEF

;-----------------------------
public operation BuscaOperador 
;-----------------------------
params
   string  piGlobal :IN
   string  piParams :IN
   string  poParams :OUT
   string  $xcderro$  :OUT
   string  $xctxerro$ :OUT
endparams

	call BuscaOperador(piGlobal,piParams,poParams,$xcderro$,$xctxerro$)
	exit($status)
End ; operation BuscaOperador

;----------------------------
public Operation ValidaNumero
;---------------------------- 
	params 
		String strIN :IN
		String strOUT : OUT
	endparams
	$instancehandle->SetStatus(<STS_LOG>,"CFI0002","","ADICIONAL=%%$COMPONENTNAME%%%.VALIDANUMERO")

	length strIN
	while ($result > 0)
		if ((strIN[1:1] >= "0") & (strIN[1:1] <= "9")) 
			strOUT = "%%strOUT%%strIN[1:1]"
		endif
		strIN = strIN[2]
		length strIN
	endwhile
	return (0)
End ; Operation validaNumero
