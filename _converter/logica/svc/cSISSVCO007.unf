;---------------------------------
entry RedefinirContexto 
;---------------------------------
  params
    string pId       : INOUT
    string pContexto : INOUT
    string pCdErro   : INOUT
    string pCtxErro  : INOUT
  endparams
  $instancehandle->DefinirStatus(pId, pContexto)
  $instancehandle->DefinirMensagem(pId, pContexto)
  $instancehandle->DefinirInfo(pId, pContexto) 
  pCdErro = $item("STATUS", pContexto)
  pCtxErro = pContexto
  return (pCdErro)
end

;--------------------------------
entry DefinirMensagem 
;--------------------------------
  params
    string pId       : INOUT
    string pContexto : INOUT
  endparams
  variables
    string vId, vContexto, vDescricao
  endvariables
  putitem/id pContexto , "ID", pId
  if ($item("CATEGORIA",pContexto) = "<STS_CATEGORIA_PROCERROR>")
    $instancehandle->PrcErroProc(pID, vDescricao)
    putitem/id pContexto, "DESCRICAO", vDescricao
  elseif ($item("DESCRICAO",pContexto) = "")
    putitem/id pContexto, "DESCRICAO", $text("%%pId")
  endif
end

;----------------------------
entry DefinirInfo 
;----------------------------
  params
    string pId       : INOUT
    string pContexto : INOUT
  endparams
  if ($item("CATEGORIA",pCONTEXTO) = "<STS_CATEGORIA_PROCERROR>")
    $instancehandle->DefinirInfoProcerror(pId, pContexto)
  elseif ($item("CATEGORIA",pCONTEXTO) = "<STS_CATEGORIA_VALIDATE>")
    $instancehandle->DefinirInfoValidate(pId, pContexto)
  elseif ($item("CATEGORIA",pCONTEXTO) = "<STS_CATEGORIA_SISTEMA>")
    $instancehandle->DefinirInfoSistema(pId, pContexto)
  endif
end

;-------------------------------------
entry DefinirInfoProcerror 
;-------------------------------------
  params
    string pId       : INOUT
    string pContexto : INOUT
  endparams
  variables
    string vContexto,vMnem,vProcname,vTrigger,vLine,vAditional,vObject
  endvariables

  getitem/id vMnem, pContexto, "MNEM"
  getitem/id vProcname, pContexto, "PROCNAME"
  getitem/id vTrigger, pContexto, "TRIGGER"
  getitem/id vLine, pContexto, "LINE"
  getitem/id vAditional, pContexto, "ADITIONAL"
  getitem/id vObject, pContexto, "OBJECT"

  delitem/id pContexto, "MNEM"
  delitem/id pContexto, "PROCNAME"
  delitem/id pContexto, "TRIGGER"
  delitem/id pContexto, "LINE"
  delitem/id pContexto, "ADITIONAL"
  delitem/id pContexto, "OBJECT"

  putitem/id vContexto, "OBJETO","%%vObject"
  putitem/id vContexto, "OPERACAO", "%%vProcname"
  putitem/id vContexto, "LINHA","%%vLine"
  putitem/id vContexto, "ADICIONAL","%%vAditional"

  putitem/id pContexto, "INFO",vContexto
end

;------------------------------------
entry DefinirInfoValidate 
;------------------------------------
  params
    string pId       : INOUT
    string pContexto : INOUT
  endparams
  variables
    string vTopic, vOcc, vKey, vObject, vContexto
  endvariables

  getitem/id vTopic, pContexto, "TOPIC"
  getitem/id vOcc, pContexto, "OCC"
  getitem/id vKey, pContexto, "KEY"
  getitem/id vObject, pContexto, "OBJECT"

  delitem/id pContexto, "TOPIC"
  delitem/id pContexto, "OCC"
  delitem/id pContexto, "KEY"
  delitem/id pContexto, "OBJECT"

  putitem/id vContexto, "OBJETO", "%%vObject"
  putitem/id vContexto, "TOPICO", "%%vTopic"
  putitem/id vContexto, "OCORRENCIA", "%%vOcc"
  if (vKey)
    putitem/id vContexto, "CHAVE", "%%vKey"
  endif

  putitem/id pContexto, "INFO", vContexto
end

;-----------------------------------
entry DefinirInfoSistema 
;-----------------------------------
  params
    string pId       : INOUT
    string pContexto : INOUT
  endparams
  variables
    string vContexto
  endvariables
  putitem/id vContexto, "CAMPO", $item("CAMPO", pContexto)
  putitem/id vContexto, "ENTIDADE", $item("ENTIDADE", pContexto)
  putitem/id vContexto, "COMPONENTE", $item("COMPONENTE", pContexto)

  putitem/id pContexto, "INFO",vContexto
 
end

;------------------------------
entry DefinirStatus 
;------------------------------
  params
    string pId       : INOUT
    string pContexto : INOUT
  endparams
  variables 
    string vTipo
  endvariables
  vTipo = $item("TIPO",pContexto)
  if(vtipo = <STS_DICA>)
    putitem/id pContexto,"STATUS",1
  elseif (vTipo = <STS_AVISO>)
    putitem/id pContexto,"STATUS",2
  elseif(vtipo = <STS_INFO>)
    putitem/id pContexto,"STATUS",3
  elseif(vtipo = <STS_ERRO>)
    putitem/id pContexto,"STATUS",-1
  endif
  return ($item("STATUS",pContexto))
end

;------------------------------
entry IgnorarStatus 
;------------------------------
  params
    string pId : INOUT
  endparams
  variables 
    string vTipo
  endvariables

  ;Retorna uma indicaco se o status deve ser ignorado.
  selectcase pId
  case "0"
    return (1)
  case "0140"
    return (-1)
  case "0148"
    return (1)
  case "0149"
    return (1)
  case "0153"
    return (-1)
  elsecase
    return (0)
  endselectcase

end



;--------------------------
entry SetarStatus
;--------------------------
  params
    string pTipo     : INOUT ; Tipo de status DICA,AVISO,INFO,ERRO
    string pId       : INOUT ; Identificador do status
    string pContexto : INOUT ; Contexto do status
    string pInfo     : INOUT ; Informação adicional
  endparams

  pContexto = "%%pContexto%%%·;%%pInfo"
  if (pId = -35)
    if ("<$componenttype>" = <SVC>)
      pid = $xcderro$
      pContexto = $xctxerro$
    else
      pid = ""
      pContexto = ""
      return ($xcderro$)
    endif
  endif

  activate "SISSVCO007".IgnorarStatus(pId)
  if ($status = 0)
    $instancehandle->DefinirContexto(pTipo, pId, pContexto)
    if ($procerror)
      ;Erro na execucao da operacao
    elseif($status < 0)
      ;Erro na rotina de tratamento de erro
    elseif($status >= 0)
      activate "SISSVCO007".RedefinirContexto(pid,pContexto, $xcs$, $xlcs$)
      if ($procerror)
        putmess "%%$procerror - %%$procerrorcontext"
      else
        $instancehandle->ExibirMensagem(pId, pContexto)
      endif
    endif
  endif

  return ($status)
end

;--------------------------------
entry DefinirContexto
;--------------------------------
  params
    string pTipo     : IN
    string pId       : INOUT
    string pContexto : INOUT
  endparams

  variables
    string vGlobal
    string vSessao,vCategoria,vUsuario,vComponente
    string vTipo,vTipoComponente
    string vData,vHora
    string vCampo,vEntidade
  endvariables

  ;Definir identificador do status
  if (pId = "")
    pId = $item("ID",pContexto)
    if (pId = "")
        pId = 8006
    endif
  endif

  ;Definir global
  #if ("<$componenttype>" = <FRM>)
    vGlobal = $$GPARAMGLB
  #else
    ;vGlobal = $LGLOBAL$
  #endif

  ;Definir categoria
  if (pID < 0) 
    vCategoria = "<STS_CATEGORIA_PROCERROR>"
  elseif ($item("CONTEXT",pContexto) = "VALIDATION")
    vCategoria = "<STS_CATEGORIA_VALIDATE>"
  else
    vCategoria = "<STS_CATEGORIA_SISTEMA>"
  endif

  ;Definir tipo
  if (pTipo = "")    
    pTipo = $item("TIPO", pContexto)
  endif
  vTIPO = pTipo

  ;Definir Data
  vDATA = $date

  ;Definir Hora
  vHORA = $clock

  ;Definir Usuário
  vUSUARIO = $item("ID_USR", vGLOBAL)

  ;Definir Componente
  vCOMPONENTE = $item("COMPONENT", pContexto)
  if (vCOMPONENTE = "")
    vCOMPONENTE = $componentname
  endif

  ;Definir tipo Componente
  vTIPOCOMPONENTE = $componenttype(vComponente) ;(SVC,RPT,FRM)

  ;Definir Campo
  vCampo = $fieldname

  ;Definir Entidade
  vEntidade = $entname

  putitem/id pContexto, "TIPO", vTipo
  putitem/id pContexto, "DATA", vData
  putitem/id pContexto, "HORA", vHora
  putitem/id pContexto, "USUARIO", vUsuario
  putitem/id pContexto, "CATEGORIA", vCategoria
  putitem/id pContexto, "COMPONENTE", vComponente
  putitem/id pContexto, "ENTIDADE", vEntidade
  putitem/id pContexto, "CAMPO", vCampo
  putitem/id pContexto, "TIPO_COMPONENTE", vTipoComponente

end

;-------------------------------
entry ExibirMensagem
;-------------------------------
  params
    string pId       : INOUT
    string pContexto : INOUT
  endparams

  variables
    string vDescricao, vTipo, vObjeto, vInfo, vDica, vLog, vAdicional, vDsTipo
  endvariables

  vTipo = $item("TIPO", pContexto)
  vDescricao = $item("DESCRICAO", pContexto)
  vInfo = $item("INFO", pContexto)
  vDica = $item("DICA", pContexto)
  vAdicional = $item("ADICIONAL", pContexto)
  vLog = $item("LOG", pContexto)
  vObjeto = $item("OBJETO", vInfo)

  if (vTipo = <STS_DICA>)
    vDsTipo = "DICA"
    #if ("<$componenttype>" = <FRM>)
      message/hint "%%pId - %%vDescricao %%vAdicional %%vDica" 
    #endif
    $status = <STS_DICA>
  elseif (vTipo = <STS_AVISO>)
    vDsTipo = "AVISO"
    #if ("<$componenttype>" = <FRM>)
      message/warning "%%pId - %%vDescricao %%vAdicional"
    #endif
    putmess "%%vDsTipo%%%-> %%pId"
    putmess "    DESCR    -> %%vDescricao"
    if (vAdicional != "") putmess "    ADIC    -> %%vAdicional"
    if (vInfo != "")      putmess "    INFO    -> %%vInfo"
    if (vLog  != "")      putmess "    LOG    -> %%vLog"
    putmess " "
    $status = <STS_AVISO>
  elseif(vTipo = <STS_INFO>)
    vDsTipo = "INFO"
    #if ("<$componenttype>" = <FRM>)
      message/info "%%pId - %%vDescricao %%vAdicional" 
    #endif
    putmess "%%vDsTipo%%%-> %%pId"
    putmess "    DESCR    -> %%vDescricao"
    if (vAdicional != "") putmess "    ADIC    -> %%vAdicional"
    if (vInfo != "")      putmess "    INFO    -> %%vInfo"
    if (vLog  != "")      putmess "    LOG    -> %%vLog"
    putmess " "
    $status = <STS_INFO>
  elseif(vTipo = <STS_LOG>)
    vDsTipo = "LOG"
    putmess "%%vDsTipo%%%-> %%pId"
    putmess "    DESCR    -> %%vDescricao"
    if (vAdicional != "") putmess "    ADIC    -> %%vAdicional"
    if (vInfo != "")      putmess "    INFO    -> %%vInfo"
    if (vLog  != "")      putmess "    LOG    -> %%vLog"
    putmess " "
    $status = <STS_LOG>
  elseif (vTipo = <STS_ERRO>)
    vDsTipo = "ERRO"
    if (pId = "-50") | (pId = "-58") | (pId = "-59")
      vDescricao = "Componente em desenvolvimento!"
    endif
    #if ("<$componenttype>" = <FRM>)
      message/error "%%pId - %%vDescricao %%vAdicional"
    #endif
    putmess "%%vDsTipo%%%-> %%pId"
    putmess "    DESCR    -> %%vDescricao"
    if (vAdicional != "") putmess "    ADIC    -> %%vAdicional"
    if (vInfo != "")      putmess "    INFO    -> %%vInfo"
    if (vLog  != "")      putmess "    LOG    -> %%vLog"
    putmess " "
    $status = <STS_ERRO>
  endif

  #if ("<$componenttype>" = <FRM>)
    if (vDica != "")& (vTipo != <STS_DICA>) message/hint vDica
  #endif

  $xcderro$ = pID
  $xctxerro$ = pContexto
  $xcs$ = pId
  $xlcs$ = pContexto

  return ($status)
end