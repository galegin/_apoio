;------------------------
entry preEDIT 
;------------------------
	variables
		string viParams, voParams
	endvariables

	$instancehandle->valorPadrao()
	if ($procerror)
		$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		exit(0)
	elseif ($status < 0)
		exit(0)
	endif
	$instancehandle->GetTipoValor()
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	endif	
	viParams = ""
	activate "GERSVCO007".ListTipoSaldo(viParams, voParams, $xCdErro$, $xCtxErro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($xCdErro$)
		$instancehandle->SetErroApl($xCtxErro$, $xCdErro$, $xCtxErro$)
		return(-1)
	endif
	$valrep(TP_SALDO.SIS_FILTRO) = voParams
	
	return(0)
End ; operation preEDIT

;---------------------
partner operation INIT 
;---------------------
;
	variables
		string vpiParams, vpoParams
	endvariables
	
	vpiParams = ""
	putitem vpiParams, -1, "CD_EMPVALOR"
	activate "ADMSVCO001".GetLstParametro(vpiParams, vpoParams, $xCdErro$, $xCtxerro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($xCdErro$)
		$instancehandle->SetStatus(<STS_ERRO>, "GEN0001", "DESCRICAO=%%$item("DESCRICAO", $xCtxErro$)", "ADICIONAL=Operação->ADMSVCO001")
		return(-1)
	elseif ($status < 0)
		return(-1)
	endif
	$cdEmpresaValorSis$ = $item("CD_EMPVALOR", vpoParams)

	vpiParams = ""
	putitem vpiParams, -1, "NATUREZA_COMERCIAL_EMP"
	putitem vpiParams, -1, "CD_EMPRESA_VALOR"
	putitem vpiParams, -1, "CD_TIPOCLAS_ITEM_NF"
	activate "ADMSVCO001".getParamEmpresa($item("CD_EMPRESA", $$gParamGlb), vpiParams, vpoParams, $xcderro$, $xctxerro$)
	if ($procerror)
		$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	elseif ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
	endif
	$naturezaComercialEmp$ = $item("NATUREZA_COMERCIAL_EMP",vpoParams)
	$cdEmpresaValorEmp$    = $item("CD_EMPRESA_VALOR"      ,vpoParams)	
	$cdTipoClasItemNF$     = $item("CD_TIPOCLAS_ITEM_NF"   ,vpoParams)

	$formtitle = "%%$componentname - Geração da Escrita Fiscal Digital/Livro Eletrônico"
End ; operation INIT

;------------------------------------------------;
partner operation valorPadrao                    ;
;------------------------------------------------;
; MTF    : 03/12/2007    Projeto 061, tarefa 146 ; 
; Funcao : Carregar valores default.             ;
; Chamada: preEdit() / posClear()                ;
;------------------------------------------------;
;
	clear/e "SIS_FILTRO"
		
	fieldsyntax TP_SALDO.SIS_FILTRO           , "DIM"
	fieldsyntax TP_CUSTO.SIS_FILTRO           , "DIM"
	fieldsyntax DT_SALDO.SIS_FILTRO           , "DIM"
	fieldsyntax TP_CODPRODUTO.SIS_FILTRO      , "DIM"
	fieldsyntax TP_TIPOSALDO.SIS_FILTRO       , "DIM"
	fieldsyntax CD_CONTACONTABIL.SIS_FILTRO   , "DIM"
	fieldsyntax BT_LSTCC.SIS_FILTRO           , "DIM"
	fieldsyntax IN_SALDOTERCEIRO.SIS_FILTRO   , "DIM"
	fieldsyntax TP_INVENTARIO.SIS_FILTRO      , "DIM"
	fieldsyntax BT_INVENTARIO.SIS_FILTRO      , "DIM"
	fieldsyntax DS_LSTINVENTARIO.SIS_FILTRO   , "DIM"
	fieldsyntax BT_LIMPAR.SIS_FILTRO          , "DIM"
	fieldsyntax TP_MOTIVOINVENTARIO.SIS_FILTRO, "DIM"
	fieldsyntax CD_OPERACAO.SIS_FILTRO        , "DIM"
	fieldsyntax DS_OPERACAO.SIS_FILTRO        , "DIM"
	fieldsyntax BT_LSTOP.SIS_FILTRO           , "DIM"

	TP_INVENTARIO.SIS_FILTRO/init       = ""
	TP_SALDO.SIS_FILTRO/init            = ""
	TP_CUSTO.SIS_FILTRO/init            = ""
	DT_SALDO.SIS_FILTRO/init            = ""
	TP_CODPRODUTO.SIS_FILTRO/init       = ""
	TP_TIPOSALDO.SIS_FILTRO/init        = ""
	IN_SALDOTERCEIRO.SIS_FILTRO/init    = "" ;--> VSOUZA PRJ/TAR 170/392 11/11/2011
	DS_LSTINVENTARIO.SIS_FILTRO/init    = ""
	TP_MOTIVOINVENTARIO.SIS_FILTRO/init = ""
	CD_OPERACAO.SIS_FILTRO/init         = ""
	DS_OPERACAO.SIS_FILTRO/init         = ""

	cd_empresa.sis_filtro = $item("CD_EMPRESA", $$gParamGlb)
	clear/e "GER_EMPRESA"
	cd_empresa.ger_empresa/init = cd_empresa.sis_filtro
	retrieve/e "GER_EMPRESA"
	
	clear/e "PES_TIPOCONTATO"
    ds_tipocontato.pes_tipocontato/init = "FISCAL"
	retrieve/e "PES_TIPOCONTATO"
	if ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Falta definir tipo de contato Fiscal. Verifique.", "")
		return(-1)
	endif
	
	$instancehandle->validarEmpresa()
	if ($procerror)
		$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($status < 0)
		return(-1)
	endif
	
	return(0)
End ;operation valorPadrao.


;------------------------------------------------;
partner operation consultarEmpresa               ;
;------------------------------------------------;
; MTF    : 03/12/2007    Projeto 061, tarefa 146 ; 
; Funcao : Consultar lista de empresa.           ;
; Chamada: detail - cd_empresa.sis_filtro        ;
;------------------------------------------------;
;
	variables
		string vpiParams, vpoParams
	endvariables
	
	activate "GERFL009".exec(vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
	if ($procerror)
		$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($item("CD_EMPRESA", vpoParams) != "")
		cd_empresa.sis_filtro = $item("CD_EMPRESA", vpoParams)
		
		$instancehandle->validarEmpresa()
		if ($procerror)
			$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			return(-1)
		elseif ($status < 0)
			return(-1)
		endif
	endif
	
	return(0)
End ;operation consultarEmpresa.


;------------------------------------------------;
partner operation validarEmpresa                 ;
;------------------------------------------------;
; MTF    : 03/12/2007    Projeto 061, tarefa 146 ; 
; Funcao : Validar empresa.                      ;
; Chamada: value changed - cd_empresa.sis_filtro ;
;          operation     - validarEmpresa()      ;
;          operation     - valorPadrao()         ;
;          operation     - gerarSuperSintegra()  ;
;------------------------------------------------;
;
	variables
		string vpiParams, vpoParams
	endvariables
	
	if (cd_empresa.sis_filtro = "")
		clear/e "GER_EMPRESA"
		return(0)
	endif
	
	clear/e "GER_EMPRESA"
	cd_empresa.ger_empresa/init = cd_empresa.sis_filtro
	retrieve/e "GER_EMPRESA"
	if ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Empresa informada inválida.", "")
		cd_empresa.sis_filtro = ""
		clear/e "GER_EMPRESA"
		$prompt = cd_empresa.sis_filtro
		return(-1)
	else
		vpiParams = ""
		putitem/id vpiParams, "CD_EMPRESA",    cd_empresa.sis_filtro
		putitem/id vpiParams, "IN_CCUSTO",     <FALSE>
		putitem/id vpiParams, "CD_COMPONENTE", $componentname
		activate "FGRSVCO001".validarEmpresa($$gParamGlb, vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
		if ($procerror)
			$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			return(-1)
		elseif ($status < 0)
			$instancehandle->setStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
			cd_empresa.sis_filtro = ""
			clear/e "GER_EMPRESA"
			$prompt = cd_empresa.sis_filtro
			return(-1)
		endif
	endif
	
	clear/e "PES_CONTATO"
	cd_pessoa.pes_contato/init      = cd_pessoa.ger_empresa
	cd_tipocontato.pes_contato/init = cd_tipocontato.pes_tipocontato
	retrieve/e "PES_CONTATO"
	if ($status >= 0)
		nm_contato.sis_filtro = nm_contato.pes_contato
	else
		$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Falta definir contato para essa empresa. Verifique.", "")
		;return (-1)
		nm_contato.sis_filtro = "Informar contato"
	endif
	
	return(0)
End ;operation validarEmpresa.


;-------------------------------------------------;
partner operation validarIntervaloPeriodo         ;
;-------------------------------------------------;
; MTF    : 03/12/2007     Projeto 061, tarefa 146 ;
; Funcao : Valida intervalo de periodo.           ;
; Chamada: leave field - dt_periodoini.sis_filtro ;
;          leave field - dt_periodofin.sis_filtro ;
;          operation   - gerarSuperSintegra()     ;
;-------------------------------------------------;
;
	variables
		numeric vNrMesIni, vNrMesFin
		date    vDtAux
	endvariables
	
	if (dt_periodoini.sis_filtro = "") & (dt_periodofin.sis_filtro = "")
		dt_periodo.sis_filtro = ""
	else
		if (dt_periodoini.sis_filtro > dt_periodofin.sis_filtro)
			$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Data inicial maior que a final.", "")
			$prompt = dt_periodoini.sis_filtro
			return (-1)
		endif 
		if (dt_periodoini.sis_filtro = "") | (dt_periodofin.sis_filtro = "")
			dt_periodoini.sis_filtro  = ""
			dt_periodofin.sis_filtro  = ""
			dt_periodo.sis_filtro = ""
		else
			vDtAux    = dt_periodoini.sis_filtro
			vNrMesIni = vDtAux[5:2]
			vDtAux    = dt_periodofin.sis_filtro
			vNrMesFin = vDtAux[5:2]
			if (vNrMesIni <> vNrMesFin)
				$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Data inicial e data final devem corresponder ao mesmo mês.", "")
				$prompt = dt_periodoini.sis_filtro
				return (-1)
			endif
			dt_periodo.sis_filtro = "·>·=%%dt_periodoini.sis_filtro·&·<·=%%dt_periodofin.sis_filtro"
		endif
	endif
	
	return (0)
End ;operation validarIntervaloPeriodo.


;-----------------------------
partner operation GetTipoValor
	;-----------------------------
	variables
		string viParams,voParams
	endvariables
	putitem/id viParams, "TP_VALOR", "C"
	activate "GERSVCO007".ListTipoValor(viParams,voParams,$xCdErro$,$xCtxErro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($xCdErro$)
		$instancehandle->SetErroApl($xCtxErro$, $xCdErro$, $xCtxErro$)
		return(-1)
	elseif ($status < 0)
		return(-1)
	endif
	$valrep(TP_CUSTO.SIS_FILTRO) = voParams
	return(0)
end


;-----------------------------------------------;
partner operation gerarSuperSintegra            ;
;-----------------------------------------------;
; MTF    : 03/12/2007   Projeto 061, tarefa 146 ;
; Funcao : Gerar arquivo do super sintegra.     ;
; Chamada: detail - bt_processar.sis_filtro     ;
;-----------------------------------------------;
;
	variables
		string  vpiParams, vpoParams, vDsProcesso, vDsConteudo, vDsMensagem
		boolean vInErro, vInNovoSpedFiscal
	endvariables
	
	vDsProcesso = ""
	if (tp_opcao.sis_filtro = 1)
		vDsProcesso = "Livro Eletrônico"
	else
		vDsProcesso = "Escrituração Fiscal Digital"
	endif
	if (cd_empresa.sis_filtro = "")
		$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Informar empresa para gerar sintegra.", "")
		$prompt = cd_empresa.sis_filtro
		return(-1)
	endif
	$instancehandle->validarEmpresa()
	if ($procerror)
		$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($status < 0)
		return(-1)
	endif
	if (dt_periodo.sis_filtro = "")
		$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Informar intervalo de período.", "")
		$prompt = dt_periodoini.sis_filtro
		return(-1)
	endif
	$instancehandle->validarIntervaloPeriodo()
	if ($procerror)
		$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	elseif ($status < 0)
		return(-1)
	endif
	if (dt_venctoimp.sis_filtro = "")
		$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Informar vencimento do imposto a recolher.", "")
		$prompt = dt_venctoimp.sis_filtro
		return(-1)
	endif

	;-- MAD [Proj/Tar.061/511] - 20/11/2008
	;if (tp_opcao.sis_filtro = 2) & ((dt_venctoimp.sis_filtro < dt_periodoini.sis_filtro) |(dt_venctoimp.sis_filtro > dt_periodofin.sis_filtro))
	;	$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Data de vencimento do imposto a recolher fora do intervalo do período informado.", "")
	if (tp_opcao.sis_filtro = 2) & (dt_venctoimp.sis_filtro < dt_periodoini.sis_filtro)
		$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Data de vencimento do imposto não pode ser menor que a data inicial do período.", "")
	;;
		dt_venctoimp.sis_filtro = ""
		$prompt = dt_venctoimp.sis_filtro
		return(-1)
	endif
	$cdUf$ = ""
	if (cd_uf.sis_filtro != "00")
		$cduf$ = cd_uf.sis_filtro
	endif
	if (tp_opcao.sis_filtro = 1)
		if (tp_natureza.sis_filtro = "")
			$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Informar natureza da operação para gerar %%vDsProcesso%%%.", "")
			$prompt = tp_natureza.sis_filtro
			return(-1)
		endif
		if (tp_finalidade.sis_filtro = "")
			$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Informar a finalidade do arquivo para gerar %%vDsProcesso%%%.", "")
			$prompt = tp_finalidade.sis_filtro
			return(-1)
		endif
	endif
	if (ds_path.sis_filtro = "")
		$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Informar o arquivo para gerar %%vDsProcesso%%%.", "")
		$prompt = bt_path.sis_filtro
		return(-1)
	endif

	;-- MAD [Proj/Tar.170/539] - 06/09/2012
	if (TP_OPCAO.SIS_FILTRO != 1) & (TP_MOTIVOINVENTARIO.SIS_FILTRO > 1) & (CD_OPERACAO.SIS_FILTRO = "")
		$instancehandle->setStatus(<STS_AVISO>, "GEN001", "DESCRICAO=Para o motivo do inventário selecionado é obrigatório %%^o preenchimento do campo 'Operação reg. H020'.", "")
		$prompt = CD_OPERACAO.SIS_FILTRO
		return(-1)
	endif
	;;

	;-- MAD [Proj/Tar.170/460] - 18/06/2012
	;Bucar o valor do parametro para empresa informada no filtro
	vpiParams = ""
	putitem vpiParams, -1, "IN_NOVO_SPED_FISCAL" 
	activate "ADMSVCO001".getParamEmpresa(CD_EMPRESA.SIS_FILTRO, vpiParams, vpoParams, $xcderro$, $xctxerro$)
	if ($procerror)
		$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	elseif ($status < 0)
		$instancehandle->setStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
	endif
	vInNovoSpedFiscal = $item("IN_NOVO_SPED_FISCAL",vpoParams)
	;;

	vInErro     = <FALSE>
	vDsProcesso = ""

	vpiParams = ""
	vpoParams = ""
	putitem/id vpiParams, "DT_PERIODOINI"     , DT_PERIODOINI.SIS_FILTRO
	putitem/id vpiParams, "DT_PERIODOFIM"      , DT_PERIODOFIN.SIS_FILTRO
	putitem/id vpiParams, "TP_FINALIDADE"      , TP_FINALIDADE.SIS_FILTRO
	putitem/id vpiParams, "CD_PESSOA"          , CD_PESSOA.GER_EMPRESA
	putitem/id vpiParams, "IN_BLOCOC"          , IN_BLOCOC.SIS_FILTRO
	putitem/id vpiParams, "CD_EMPRESA"         , CD_EMPRESA.SIS_FILTRO
	putitem/id vpiParams, "DT_PERIODO"         , DT_PERIODO.SIS_FILTRO
	putitem/id vpiParams, "TP_PERFIL"          , TP_PERFIL.SIS_FILTRO
	putitem/id vpiParams, "IN_BLOCOD"          , IN_BLOCOD.SIS_FILTRO
	putitem/id vpiParams, "DT_VENCTOIMP"       , DT_VENCTOIMP.SIS_FILTRO
	putitem/id vpiParams, "IN_BLOCOH"          , IN_BLOCOH.SIS_FILTRO
	putitem/id vpiParams, "TP_CODPRODUTO"      , TP_CODPRODUTO.SIS_FILTRO
	putitem/id vpiParams, "IN_BLOCOE"          , IN_BLOCOE.SIS_FILTRO
	putitem/id vpiParams, "DT_SALDO"           , DT_SALDO.SIS_FILTRO
	putitem/id vpiParams, "TP_TIPOSALDO"       , TP_TIPOSALDO.SIS_FILTRO
	putitem/id vpiParams, "TP_CUSTO"           , TP_CUSTO.SIS_FILTRO
	putitem/id vpiParams, "TP_SALDO"           , TP_SALDO.SIS_FILTRO
	putitem/id vpiParams, "DS_PATH"            , DS_PATH.SIS_FILTRO
	putitem/id vpiParams, "CD_CONTACONTABIL"   , CD_CONTACONTABIL.SIS_FILTRO
	putitem/id vpiParams, "IN_SALDOTERCEIRO"   , IN_SALDOTERCEIRO.SIS_FILTRO ;--> VSOUZA PRJ/TAR 170/392 11/11/2011
	putitem/id vpiParams, "TP_INVENTARIO"      , TP_INVENTARIO.SIS_FILTRO 
	putitem/id vpiParams, "DS_LSTINVENTARIO"   , DS_LSTINVENTARIO.SIS_FILTRO
	putitem/id vpiParams, "TP_MOTIVOINVENTARIO", TP_MOTIVOINVENTARIO.SIS_FILTRO
	putitem/id vpiParams, "CD_OPERACAO"        , CD_OPERACAO.SIS_FILTRO

	if (tp_opcao.sis_filtro = 1)
		;Livro eletronico.
		activate "FISSVCO040".gerarSUP($$gParamGlb, vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
		if ($procerror)
			$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			vInErro = <TRUE>
		elseif ($status < 0)
			$instancehandle->setStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
			vInErro = <TRUE>
		endif
		vDsProcesso = "Livro Eletrônico"
	else
								
		;Escrituracao fiscal digital.
		;-- MAD [Proj/Tar.170/460] - 18/06/2012
		if (vInNovoSpedFiscal = <TRUE>)
			;RAR [Proj/Tar.170/191 - 15/08/2012]
			vDsMensagem = "O arquivo gerado a seguir é resultado de uma nova versão da implementação, %%^"
			vDsMensagem = "%%vDsMensagem%%%criada para melhor atender as exigências do conteúdo e agilizar a geração em menor tempo. %%^"
			vDsMensagem = "%%vDsMensagem%%%Favor conferir o arquivo gerado, informando ao suporte eventuais problemas. %%^"
			vDsMensagem = "%%vDsMensagem%%%A versão anterior ainda estará disponível sendo configurada por meio do parâmetro IN_NOVO_SPED_FISCAL."
			message/info vDsMensagem
				
			activate "FISSVCO082".gerarEFD($$gParamGlb, vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
		else
		;;
			activate "FISSVCO039".gerarEFD($$gParamGlb, vpiParams, vpoParams, $xCdErro$, $xCtxErro$)
		endif
		if ($procerror)
			$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
			vInErro = <TRUE>
		elseif ($status < 0)
			$instancehandle->setStatus(<STS_ERRO>, $xCdErro$, $xCtxErro$, "")
			vInErro = <TRUE>
		endif
		vDsProcesso = "Escrituração Fiscal Digital"
	endif
	
	if (vInErro = <FALSE>)
		$instancehandle->setStatus(<STS_INFO>, "GEN001", "DESCRICAO=Arquivo de %%vDsProcesso gerado com sucesso.", "")
	else
		$instancehandle->setStatus(<STS_ERRO>, "GEN001", "DESCRICAO=Erro na geração do arquivo de %%vDsProcesso%%%.", "")
	endif
	$instancehandle->valorPadrao()
	if ($procerror)
		$instancehandle->setStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	endif
	$prompt = cd_empresa.sis_filtro
	
	return(0)
End ;operation gerarSuperSintegra.



;----------------------------------
partner operation validaConta 
;----------------------------------
	variables
		string viParams, voParams
	endvariables

;--------------------------------------------------------------------
;	Verifica se é código reduzido
;--------------------------------------------------------------------
	if (CD_CONTACONTABIL.SIS_FILTRO > 0)
		length CD_CONTACONTABIL.SIS_FILTRO
		if ($result <= 7)
			scan CD_CONTACONTABIL.SIS_FILTRO, '.'
			if ($result = 0)
				clear/e "CTB_PLANO"
				CD_POOLEMPRESA.CTB_PLANO/init = $item("CD_POOLEMPRESA", $$gParamGlb)
				CD_REDUZIDO.CTB_PLANO/init = CD_CONTACONTABIL.SIS_FILTRO
				retrieve/e "CTB_PLANO"
				if ($status >= 0)
					CD_CONTACONTABIL.SIS_FILTRO/init = CD_CONTA.CTB_PLANO
					return (0)
				endif
			endif
		endif
	endif
;--------------------------------------------------------------------

	clear/e "CTB_GRUPOCTA"
	CD_GRUPOCTA.CTB_GRUPOCTA/init = CD_CONTACONTABIL.SIS_FILTRO[1,1]
	if (CD_GRUPOCTA.CTB_GRUPOCTA = "")
		return(0)
	endif
	retrieve/e "CTB_GRUPOCTA"
	if ($status < 0)
		message/info "Grupo de conta não encontrado!"
		CD_GRUPOCTA.CTB_GRUPOCTA/init = 0
		CD_CONTACONTABIL.SIS_FILTRO/init = ""
		return (-1)
	endif

	viParams = ""
	putitem/id viParams, "NR_DIGITADO", CD_CONTACONTABIL.SIS_FILTRO
	putitem/id viParams, "CD_MASCARA", CD_MASCARA.CTB_GRUPOCTA
	putitem/id viParams, "QT_GRAU", QT_GRAU.CTB_GRUPOCTA
	putitem/id viParams, "CD_GRUPOCONTA", CD_GRUPOCTA.CTB_GRUPOCTA
	activate "CTBSVCO005".validaMascara($$gParamGlb, viParams, voParams, $xcderro$, $xctxerro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
		return(-1)
	else
		if ($xcdErro$)
			$instancehandle->SetErroApl($xCtxErro$, $xCdErro$, $xCtxErro$)
			return(-1)
		elseif ($status < 0)
			return (-1)
		endif
	endif
	CD_CONTACONTABIL.SIS_FILTRO = $item("DS_CONTA", voParams)

	return(0)
End ; validaConta


;------------------------
partner operation consultaInventario
	;------------------------

	variables
		string viParams, voParams
	endvariables

	viParams = ""
	putitem/id viParams, "CD_EMPRESA", CD_EMPRESA.SIS_FILTRO
	putitem/id viParams, "DT_INVENTARIO", DT_PERIODOINI.SIS_FILTRO
	activate "FISFF013".EXEC(viParams, voParams,$xCdErro$,$xCtxErro$)
	if ($procerror)
		$instancehandle->SetStatus(<STS_ERRO>, $procerror, $procerrorcontext, "")
	elseif ($xcdErro$)
		$instancehandle->SetErroApl($xCtxErro$, $xCdErro$, $xCtxErro$)
	endif
	if($item("DS_LSTINVENTARIO", voParams) != "")
		DS_LSTINVENTARIO.SIS_FILTRO = $item("DS_LSTINVENTARIO", voParams)
	endif

	return(0)

End ; consultaInventario